# Oglavlenie
1. [Программная работа с перечисленями, константами и справочниками.](#1)
	+ [ООП ПОЯ](#1-1)
    + [Методы глобального контекста](#1-2)
    + [Программная работа с константами](#1-3)
    + [Программная работа с перечислениями](#1-4)
    + [Программная работа со справочниками](#1-5)
    + [Табличная часть](#1-6)

1. [Программна работа с документами, ПВХ, планами счетов и планами видов расчета.](#2)
	+ [Программная работа с документами](#2-1)
    + [Программная работа с ПВХ](#2-2)
    + [Программная работа с планом счетов](#2-3)
    + [Программная раюота с планом видов расчета](#2-4)

1. [Программная работа с регистрами сведений и регистрами накоплений.](#3)
	+ [Программная работа с регистрами](#3-1)
    + [Программная работа с регистрами сведений](#3-2)
		+ [Переодический](#3-2-1)
    + [Прогрмманая работа с регистрами накоплений](#3-3)
    + [Дополнительно](#3-4)

1. [Программная работа с регистрами бухгалтерии и регистрами сведений + Бизнес процессы.](#4)
	+ [Программная работа с регистрами бухгалтерии](#4-1)
    + [Регистры расчета](#4-2)
    + [Бизнес процессы](#4-3)
    	+ [Задачи](#4-3-1)
		+ [Бизнес процессы](#4-3-2)

1. [События и виды модулей](#5)
	+ [Вопрос перед завершением работы](#5-1)
    + [Заполенение реквизитов объекта при создании объекта](#5-2)
    + [Проверка заполнения реквизитов по условию](#5-3)
    + [Заполенние реквизитов перед записью](#5-4)
	+ [Переопределение алгоритма копирования объекта](#5-5)
    + [Добавление префикса в номер документа](#5-6)
    + [Изменение представления объекта](#5-7)
    + [Обработка получения формы](#5-8)
	+ [Проверка заполнения типов отношений с контрагентом](#5-9)
    + [Очистка реквизитов при копировании Приходной накладной](#5-10)
    + [Пометка на удаление договоров при пометке на удаление владельца (контрагента)](#5-11)



# 1
# Программная работа с перечисленями, константами и справочниками.

# 1-1
# ООП ПОЯ
[Оглавление](#Oglavlenie)
![ООП ПОЯ](/images/ООП/ООП.png)

![ООП ПОЯ](/images/ООП/Объектная%20модель%201.png)
![ООП ПОЯ](/images/ООП/Объктная%20модель%202.png)

![ООП ПОЯ](/images/ООП/Свойства%20и%20методы.png)

![ООП ПОЯ](/images/ООП/Объктная%20модель%201С.png)

![ООП ПОЯ](/images/ООП/ПОЯ.png)

# 1-2 
# Методы глобального контекста
[Оглавление](#Oglavlenie)

+ Запись журнала регистрации
```
ЗаписьЖурналаРегистрации("ТестоваяЗапись", УровеньЖурналаРегистрации.Информация);
```
+ Запустить приложение
```
ПутьКФайлу = "E:\1_IRONSKILLS\1_Курсы\0_Программист 1С - с нуля до профи (2023)\Домашки\P16 - Начисления.xlsx";
ЗапуститьПриложениеАсинх(ПутьКФайлу);
```
+ Получить имя временного файла
```
ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ИмяФайла;
Сообщение.Сообщить();
```
+ Открыть форму
```
ОткрытьФорму("Документ.ЗаказКлиента.ФормаСписка");
```
+ Открыть справкуи
```
ОткрытьСправку();
```
+ Обновить интерфейс
```
ОбновитьИнтерфейс();
```
+ Имя компьютера
```
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ИмяКомпьютера();
Сообщение.Сообщить();
```

# 1-3
# Программная работа с константами
[Оглавление](#Oglavlenie)

![Програмная работа с константами](/images/ООП/Програмная%20работа%20с%20константами.png)


+ Получить значение константы
```
Валюта = Константы.ВалютаУчета.Получить();

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Валюта;
Сообщение.Сообщить();
```

+ Установить значение константы
```
Константы.ЗаголовокСистемы.Установить("Управление компанией, ред. 2.0");

НовоеЗначение = Константы.ЗаголовокСистемы.Получить();
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = НовоеЗначение;
Сообщение.Сообщить();
```
+ Набор констант
```
НаборКонстант = Константы.СоздатьНабор("ВалютаУчета, ЗаголовокСистемы");
НаборКонстант.Прочитать();

Текст = СтрШаблон("Валюта учета: %1; Заголовок: %2",
					 НаборКонстант.ВалютаУчета, НаборКонстант.ЗаголовокСистемы);
					 
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Обход всех констант
```
Для каждого МенеджерКонстанты ИЗ Константы Цикл

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = МенеджерКонстанты.Получить();
	Сообщение.Сообщить();
	
КонецЦикла;
```

# 1-4
# Программная работа с перечислениями
[Оглавление](#Oglavlenie)

 ![Програмная работа с перечислениями](/images/ООП/Програмная%20работа%20с%20перечислениями.png)

 
 + Получить значение перечисления
 ```
День = Перечисления.ДниНедели.Воскресенье;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = День;
Сообщение.Сообщить();

// Установить значение константы
Константы.СпособДоставкиПоУмолчанию.Установить(Перечисления.СпособыДоставки.Самовывоз);

ТекущийСпособДоставки = Константы.СпособДоставкиПоУмолчанию.Получить();
Если ТекущийСпособДоставки = Перечисления.СпособыДоставки.Почта Тогда
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Способ доставки - Почта";
	Сообщение.Сообщить();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Текущий способ доставки: " + ТекущийСпособДоставки;
	Сообщение.Сообщить();
КонецЕсли;
 ```

 + Значение перечисления на клиенте
 ```
 //СпособДоставки = Перечисления.СпособыДоставки.Почта;
СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Почта");

 ```
+ Пустая ссылка на перечисление 
```
ПустаяСсылка = Перечисления.ТипыСкладов.ПустаяСсылка();
```

+ Обход значений перечисления в цикле

```
Для каждого Значение Из Перечисления.ДниНедели Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Значение;
	Сообщение.Сообщить();
КонецЦикла;

День = Перечисления.ДниНедели["Воскресенье"];
```

# 1-5 
# Программная работа со справочниками
[Оглавление](#Oglavlenie)

+ ![Программная работа со справочниками](/images/ООП/Програмная%20работа%20со%20справочниками.png)

![Программная работа со справочниками](/images/ООП/СправочникСслыка%20и%20Справочник%20Объект.png)

+ Создать элемент
```
НовыйТовар = Справочники.Номенклатура.СоздатьЭлемент();
НовыйТовар.Наименование = "Мышь компьютерная";
НовыйТовар.Артикул = "А-1";
НовыйТовар.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
НовыйТовар.СпособПополнения = Перечисления.СпособыПополненияТоваров.Закупка;
НовыйТовар.Записать();
```

+ Создать группу
```
//НоваяГруппа = Справочники.Номенклатура.СоздатьГруппу();
//НоваяГруппа.Наименование = "Умные часы";
//НоваяГруппа.Записать();

НоваяГруппа = Справочники.ВидыЦен.СоздатьГруппу();
НоваяГруппа.Наименование = "Оптовые";
НоваяГруппа.Записать();
```
+ Найти элемент
```
// поиск по коду
Товар = Справочники.Номенклатура.НайтиПоКоду("00000000018");
Если Товар.Пустая() Тогда
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Товар не найден";
	Сообщение.Сообщить();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Артикул товара: " + Товар.Артикул;
	Сообщение.Сообщить();
КонецЕсли;

// поиск по наименованию
Товар = Справочники.Номенклатура.НайтиПоНаименованию("Телевизор Sony Bravia X90K XR-65X90K", Истина);
Если Товар.Пустая() Тогда
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Товар не найден";
	Сообщение.Сообщить();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Артикул товара: " + Товар.Артикул;
	Сообщение.Сообщить();
КонецЕсли;

// поиск по реквизиту
Товар = Справочники.Номенклатура.НайтиПоРеквизиту("НаименованиеПолное", "Телевизор Xiaomi TV Q2 55"" (международная версия)");  
Если Товар.Пустая() Тогда
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Товар не найден";
	Сообщение.Сообщить();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Артикул товара: " + Товар.Артикул;
	Сообщение.Сообщить();
КонецЕсли;
```
+ Найти элемент (внутри группы)
```
Группа = Справочники.Номенклатура.НайтиПоНаименованию("Планшеты");

Если НЕ Группа.Пустая() Тогда
	
	Товар = Справочники.Номенклатура.НайтиПоНаименованию("Планшет HONOR Pad 8 128GB",,Группа);
	
	Если Товар.Пустая() Тогда
		Текст = "Товар не найден";
	Иначе
		Текст = "Артикул товара: " + Товар.Артикул
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Группа не найдена";
	Сообщение.Сообщить();
КонецЕсли;

```
+ Найти элемент (в пределах подчинения)
```
Контрагент = Справочники.Контрагенты.НайтиПоНаименованию("МИКО");
Если НЕ Контрагент.Пустая() Тогда

	Договор = Справочники.ДоговорыКонтрагентов.НайтиПоРеквизиту("НомерДоговора", "15",,Контрагент);
	Если НЕ Договор.Пустая() Тогда
		Текст = СтрШаблон("Дата договора: %1; Владелец: %2", Договор.ДатаДоговора, Договор.Владелец);
	Иначе
		Текст = "Договор не найден";
	КонецЕсли;
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден в справочнике";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Создать элемент внутри группы
```
Группа = Справочники.Номенклатура.НайтиПоКоду("00000000034");

Если НЕ Группа.Пустая() Тогда

	НовыйТовар = Справочники.Номенклатура.СоздатьЭлемент();
	НовыйТовар.Наименование = "Мышь компьютерная";
	НовыйТовар.Родитель = Группа;
	НовыйТовар.Артикул = "А-1";
	НовыйТовар.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
	НовыйТовар.СпособПополнения = Перечисления.СпособыПополненияТоваров.Закупка;
	НовыйТовар.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Группа не найдена";
	Сообщение.Сообщить();	
КонецЕсли;
```
+ Изменить элемент
```
// 1. Найти ссылку
Товар = Справочники.Номенклатура.НайтиПоКоду("00000000035");
Если НЕ Товар.Пустая() Тогда

	// 2. По ссылке получить объект (если ссылка не пустая)
	ТоварОбъект = Товар.ПолучитьОбъект();
	
	// 3. Изменить свойства объекта
	ТоварОбъект.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
	
	// 4. Записать объект
	ТоварОбъект.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Товар не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Пометить элемент на удаление
```
Товар = Справочники.Номенклатура.НайтиПоКоду("00000000035");

Если НЕ Товар.Пустая() Тогда
	ТоварОбъект = Товар.ПолучитьОбъект();
	ТоварОбъект.УстановитьПометкуУдаления(Истина);		
	Текст = "Товар успешно помечен на удаление";
Иначе
	Текст = "Товар не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Пометить группу на удаление
```
// пример пометки на удаление группы
Группа = Справочники.Номенклатура.НайтиПоКоду("00000000034");

Если НЕ Группа.Пустая() Тогда
	ГруппаОбъект = Группа.ПолучитьОбъект();
	ГруппаОбъект.УстановитьПометкуУдаления(Истина);		
	Текст = "Группа успешно помечена на удаление";
Иначе
	Текст = "Группа не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Пустая ссылка на справочник
```
ПустаяСсылка = Справочники.Номенклатура.ПустаяСсылка();
```
+ Выборка элементов справочника
```
Выборка = Справочники.Номенклатура.Выбрать();

Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Выборка.Наименование;
	Сообщение.Сообщить();
КонецЦикла;

```
+ Выбрать все планшеты
```
Группа = Справочники.Номенклатура.НайтиПоКоду("00000000015");
Если НЕ Группа.Пустая() Тогда 
	Выборка = Справочники.Номенклатура.Выбрать(Группа);
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Наименование;
		Сообщение.Сообщить();
	КонецЦикла;
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Группа не найдена";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Выбрать все договоры контрагента
```
Контрагент = Справочники.Контрагенты.НайтиПоНаименованию("МИКО");
Если НЕ Контрагент.Пустая() Тогда
	
	Выборка = Справочники.ДоговорыКонтрагентов.Выбрать(, Контрагент);
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Наименование;
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();
КонецЕсли;
		
```
+ Выбрать все товары
```
Отбор = Новый Структура;
Отбор.Вставить("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Товар);
Выборка = Справочники.Номенклатура.Выбрать(,,Отбор);
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Выборка.Наименование;
	Сообщение.Сообщить();
КонецЦикла;
```
+ Перенести несколько товаров в группу
```

НоваяГруппа = Справочники.Номенклатура.НайтиПоКоду("00000000034");

Если НЕ НоваяГруппа.Пустая() Тогда

	ПустаяГруппа = Справочники.Номенклатура.ПустаяСсылка();
	Отбор = Новый Структура;
	Отбор.Вставить("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Товар);
	
	Выборка = Справочники.Номенклатура.Выбрать(ПустаяГруппа,,Отбор);
	Пока Выборка.Следующий() Цикл
		ТоварОбъект = Выборка.ПолучитьОбъект();
		ТоварОбъект.Родитель = НоваяГруппа;
		ТоварОбъект.Записать();
	КонецЦикла;
	Текст = "Товары перемещены в группу";
Иначе
	Текст = "Группа не найдена";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

# 1-6
# Табличная часть
[Оглавление](#Oglavlenie)

+ Добавить строку
```
// поиск элемента справочника с табличной частью
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000002");
Если НЕ Контрагент.Пустая() Тогда

	// получение объекта по ссылке
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();

	// добавление строки в табличную часть
	СтрокаТаблицы = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
	
	// заполнение реквизитов табличной части
	СтрокаТаблицы.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
	СтрокаТаблицы.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	СтрокаТаблицы.Представление = "+7 (495) 456-87-94";
	
	// запись объекта
	КонтрагентОбъект.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();	
КонецЕсли;

```
+ Найти одну строку
```
// поиск элемента справочника с табличной частью
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000002");
Если НЕ Контрагент.Пустая() Тогда

	// получение объекта по ссылке
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();

	ВидКИ = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	
	СтрокаТаблицы = КонтрагентОбъект.КонтактнаяИнформация.Найти(ВидКИ, "Вид");
	
	Если СтрокаТаблицы = Неопределено Тогда
	
		// добавление строки в табличную часть
		СтрокаТаблицы = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
	
		// заполнение реквизитов табличной части
		СтрокаТаблицы.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
		СтрокаТаблицы.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
		
	КонецЕсли;
	
	СтрокаТаблицы.Представление = "+7 (495) 784-87-94";
	
	// запись объекта
	КонтрагентОбъект.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();	
КонецЕсли;
```
+ Найти несколько строк
```
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000001");
Если НЕ Контрагент.Пустая() Тогда
		
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	МассивСтрок = Контрагент.КонтактнаяИнформация.НайтиСтроки(ПараметрыОтбора);
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(СтрокаТаблицы.Вид) + ": " + СтрокаТаблицы.Представление;
		Сообщение.Сообщить();
	КонецЦикла;

	Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Получить количество строк
```
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000001");
Если НЕ Контрагент.Пустая() Тогда
		
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	Количество = Контрагент.КонтактнаяИнформация.Количество();
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Кол-во контактов: " + Количество;
	Сообщение.Сообщить();

Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Обойти строки в цикле
```
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000001");
Если НЕ Контрагент.Пустая() Тогда
	
	Для каждого СтрокаТаблицы ИЗ Контрагент.КонтактнаяИнформация Цикл
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(СтрокаТаблицы.Вид) + ":" + СтрокаТаблицы.Представление;
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Выгрузить в таблицу значений
```
Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000001");
Если НЕ Контрагент.Пустая() Тогда
	
	ТаблицаКонтактов = Контрагент.КонтактнаяИнформация.Выгрузить();
	
	Для каждого СтрокаТаблицы ИЗ ТаблицаКонтактов Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(СтрокаТаблицы.Вид) + ":" + СтрокаТаблицы.Представление;
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Сортировать
```
СсылкаНаДокумент = Документы.ЗаказКлиента.НайтиПоНомеру("000000004", ТекущаяДата());

Если НЕ СсылкаНаДокумент.Пустая() Тогда
	
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	ДокументОбъект.Товары.Сортировать("СуммаВсего Убыв");
	ДокументОбъект.Записать();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Таблица отсортирована";
	Сообщение.Сообщить();
	
Иначе

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
	
КонецЕсли;
```
+ Получить итог по колонке
```
СсылкаНаДокумент = Документы.ЗаказКлиента.НайтиПоНомеру("000000004", ТекущаяДата());

Если НЕ СсылкаНаДокумент.Пустая() Тогда
	
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	
	СуммаДокумента = ДокументОбъект.Товары.Итог("СуммаВсего");
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Сумма заказа: " + СуммаДокумента;
	Сообщение.Сообщить();
	
Иначе

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
	
КонецЕсли;
```

# 2
# Программна работа с документами, ПВХ, планами счетов и планами видов расчета
[Оглавление](#Oglavlenie)

# 2-1 Программная работа с документами
[Оглавление](#Oglavlenie)

![Программная работа с документами](/images/Программна%20работа%20с%20документами,%20ПВХ,%20планами%20счетов%20и%20планами%20видов%20расчета/Прикладная%20работа%20с%20документами.png)

+ Создать документ
```
ДокОбъект = Документы.ЗаказКлиента.СоздатьДокумент();

ДокОбъект.Дата = ТекущаяДата();
ДокОбъект.Организация = Справочники.Организации.ОсновнаяОрганизация;
ДокОбъект.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000018");
ДокОбъект.ДатаОтгрузки = ТекущаяДата() + 5*86400;
ДокОбъект.Валюта = Справочники.Валюты.НайтиПоКоду("643");

СтавкаНДС = Справочники.СтавкиНДС.НДС20;

СтрокаТаблицы = ДокОбъект.Товары.Добавить();
СтрокаТаблицы.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000019");
СтрокаТаблицы.Количество = 3;
СтрокаТаблицы.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
СтрокаТаблицы.Цена = 10000;
СтрокаТаблицы.Сумма = СтрокаТаблицы.Количество * СтрокаТаблицы.Цена;
СтрокаТаблицы.СтавкаНДС = Справочники.СтавкиНДС.НДС20;
СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Сумма * СтавкаНДС.Ставка / 100;
СтрокаТаблицы.СуммаВсего = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;

ДокОбъект.Записать();
```

+ Записать документ
```
ДокОбъект = Документы.ЗаказКлиента.СоздатьДокумент();

ДокОбъект.Дата = ТекущаяДата();
ДокОбъект.Организация = Справочники.Организации.ОсновнаяОрганизация;
ДокОбъект.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000018");
ДокОбъект.ДатаОтгрузки = ТекущаяДата() + 5*86400;
ДокОбъект.Валюта = Справочники.Валюты.НайтиПоКоду("643");


ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
```

+ Провести
``` ДокОбъект = Документы.ЗаказКлиента.СоздатьДокумент();

ДокОбъект.Дата = ТекущаяДата();
ДокОбъект.Организация = Справочники.Организации.ОсновнаяОрганизация;
ДокОбъект.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000018");
ДокОбъект.ДатаОтгрузки = ТекущаяДата() + 5*86400;
ДокОбъект.Валюта = Справочники.Валюты.НайтиПоКоду("643");

ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
```

+ Отменить проведение
```
ДокОбъект = Документы.ЗаказКлиента.СоздатьДокумент();

ДокОбъект.Дата = ТекущаяДата();
ДокОбъект.Организация = Справочники.Организации.ОсновнаяОрганизация;
ДокОбъект.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000000018");
ДокОбъект.ДатаОтгрузки = ТекущаяДата() + 5*86400;
ДокОбъект.Валюта = Справочники.Валюты.НайтиПоКоду("643");


ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);

ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
```

+ Найти документ
```
// по номеру
ДокументСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000006", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда
	Текст = Строка(ДокументСсылка);
Иначе
	Текст = "Документ не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();

// по реквизиту
Контрагент = Справочники.Контрагенты.НайтиПоНаименованию("Сисадмингрупп");
Если НЕ Контрагент.Пустая() Тогда
	ДокументСсылка = Документы.ЗаказКлиента.НайтиПоРеквизиту("Контрагент", Контрагент);
	Если НЕ ДокументСсылка.Пустая() Тогда
		Текст = Строка(ДокументСсылка);
	Иначе
		Текст = "Документ с таким номером не найден";
	КонецЕсли;
Иначе
	Текст = "Контрагент не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Проведение или запись
	+ Если мы программно записываем документ который проведен, то он просто запишется, если мы хотим его провести, то надо в явном виде это указывать через `РежимЗаписиДокумента`
```
НакладнаяСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000005", ТекущаяДата());
Если НЕ НакладнаяСсылка.Пустая() Тогда
	
	НакладнаяОбъект = НакладнаяСсылка.ПолучитьОбъект();
	
	СтавкаНДС = Справочники.СтавкиНДС.НДС20;
	
	СтрокаТаблицы = НакладнаяОбъект.Товары.Добавить();
	СтрокаТаблицы.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000019");
	СтрокаТаблицы.Количество = 3;
	СтрокаТаблицы.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
	СтрокаТаблицы.Цена = 10000;
	СтрокаТаблицы.Сумма = СтрокаТаблицы.Количество * СтрокаТаблицы.Цена;
	СтрокаТаблицы.СтавкаНДС = Справочники.СтавкиНДС.НДС20;
	СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Сумма * СтавкаНДС.Ставка / 100;
	СтрокаТаблицы.СуммаВсего = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
	
	НакладнаяОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Помека на удаление
```
ЗаказСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000293", ТекущаяДата());
Если НЕ ЗаказСсылка.Пустая() Тогда
	ЗаказОбъект = ЗаказСсылка.ПолучитьОбъект();
	ЗаказОбъект.УстановитьПометкуУдаления(Истина);
	Текст = "Документ помечен на удаление";
Иначе
	Текст = "Документ не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Проверить заполнение документа
```
ЗаказСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000294", ТекущаяДата());
Если НЕ ЗаказСсылка.Пустая() Тогда
	ЗаказОбъект = ЗаказСсылка.ПолучитьОбъект();
	НетОшибок = ЗаказОбъект.ПроверитьЗаполнение();
	Текст = ?(НетОшибок, "Ошибок не обнаружено", "Есть ошибки");
Иначе
	Текст = "Документ не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Заполнить на основании
```
ЗаказСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000196", ТекущаяДата());
Если НЕ ЗаказСсылка.Пустая() Тогда
	
	НакладнаяОбъект = Документы.РасходнаяНакладная.СоздатьДокумент();
	НакладнаяОбъект.Заполнить(ЗаказСсылка);
	НакладнаяОбъект.Дата = ТекущаяДата();
	
	НакладнаяОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Текст = "На основании заказа создан документ: " + НакладнаяОбъект.Ссылка;	
Иначе
	Текст = "Документ не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Работа с табличной частью 
```
ЗаказСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000199", ТекущаяДата());
Если НЕ ЗаказСсылка.Пустая() Тогда

	ЗаказОбъект = ЗаказСсылка.ПолучитьОбъект();
	
	Для каждого СтрокаТаблицы ИЗ ЗаказОбъект.Товары Цикл
		СтавкаНДС = СтрокаТаблицы.СтавкаНДС;
		СтрокаТаблицы.Цена = СтрокаТаблицы.Цена * 1.1;
		СтрокаТаблицы.Сумма = СтрокаТаблицы.Количество * СтрокаТаблицы.Цена;
		СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Сумма * СтавкаНДС.Ставка / 100;
		СтрокаТаблицы.СуммаВсего = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
	КонецЦикла;
	
	ЗаказОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Текст = "Сумма документа изменена";
	
Иначе
	Текст = "Документ не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Получить выборку
```
// без отбора
Выборка = Документы.ЗаказКлиента.Выбрать();
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("№ %1 от %2", Выборка.Номер, Выборка.Дата);
	Сообщение.Сообщить();
КонецЦикла;

// за период
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

Выборка = Документы.ЗаказКлиента.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("№ %1 от %2", Выборка.Номер, Выборка.Дата);
	Сообщение.Сообщить();
КонецЦикла;

// с отбором
ДатаНачала = НачалоГода(ТекущаяДата());
ДатаОкончания = КонецГода(ТекущаяДата());

Контрагент = Справочники.Контрагенты.НайтиПоНаименованию("ИнноТрейд");
Отбор = Новый Структура;
Отбор.Вставить("Контрагент", Контрагент);

Выборка = Документы.ЗаказКлиента.Выбрать(ДатаНачала, ДатаОкончания, Отбор);
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("№ %1 от %2", Выборка.Номер, Выборка.Дата);
	Сообщение.Сообщить();
КонецЦикла;
```

+ ПустаяСсылка

```
ПустаяСсылка = Документы.ЗаказКлиента.ПустаяСсылка();
```

+ ДвиженияДокумента 
```
НакладнаяСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000007", ТекущаяДата());
Если НЕ НакладнаяСсылка.Пустая() Тогда
	
	НакладнаяОбъект = НакладнаяСсылка.ПолучитьОбъект();
	
	Движения = НакладнаяОбъект.Движения;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Движения по регистру ""Товары на складах""";
	Сообщение.Сообщить();
	
	ДвиженияТоварыНаСкладах = Движения["ТоварыНаСкладах"];
	ДвиженияТоварыНаСкладах.Прочитать();
	Для каждого ЗаписьРегистра Из ДвиженияТоварыНаСкладах Цикл
		Текст = СтрШаблон("%1, %2 ед., склад: %3", 
						ЗаписьРегистра.Номенклатура, ЗаписьРегистра.Количество, ЗаписьРегистра.Склад);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Сообщить();	
	КонецЦикла;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Движения по регистру ""Продажи""";
	Сообщение.Сообщить();

	ДвиженияПродажи = Движения.Продажи;
	ДвиженияПродажи.Прочитать();
	Для каждого ЗаписьРегистра Из ДвиженияПродажи Цикл
		Текст = СтрШаблон("%1, %2 ед., сумма: %3", 
						ЗаписьРегистра.Номенклатура, ЗаписьРегистра.Количество, ЗаписьРегистра.Сумма);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Сообщить();	
	КонецЦикла;
	
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```
+ Создать на основании (открыть форму)
```
ЗаказСсылка = Документы.ЗаказКлиента.НайтиПоНомеру("000000196", ТекущаяДата());
Если НЕ ЗаказСсылка.Пустая() Тогда

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", ЗаказСсылка);
	
	ОткрытьФорму("Документ.РасходыфнаяНакладная.ФормаОбъекта", ПараметрыФормы);
	
Иначе
	Текст = "Документ не найден";
КонецЕсли;
```

# 2-2
# Программная работа с ПВХ
[Оглавление](#Oglavlenie)

+ ![Программная работа с ПВХ](/images/Программна%20работа%20с%20документами,%20ПВХ,%20планами%20счетов%20и%20планами%20видов%20расчета/Программная%20работа%20с%20ПВХ.png)

+ Создать элемент
```
НовыйЭлемент = ПланыВидовХарактеристик.ДополнительныеРеквизиты.СоздатьЭлемент();
НовыйЭлемент.Наименование = "Количество цветов";
ПараметрыЧисла = Новый КвалификаторыЧисла(10, 0);
НовыйЭлемент.ТипЗначения = Новый ОписаниеТипов("Число", ПараметрыЧисла);
НовыйЭлемент.Записать();
```

+ Создать группу
```
Группа = ПланыВидовХарактеристик.ДополнительныеРеквизиты.СоздатьГруппу();
Группа.Наименование = "Дополнительные реквизиты документ ""Заказ клиента""";
Группа.Записать();
```
+ Найти элемент
```
Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000006");
Если НЕ Свойство.Пустая() Тогда
	Текст = Свойство.Наименование;
Иначе
	Текст = "Элемент не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Найти элемент в группе
```
ГруппаПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_ДоговорыКонтрагентов;
Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000007", ГруппаПВХ);
Если НЕ Свойство.Пустая() Тогда
	Текст = Свойство.Наименование;
Иначе
	Текст = "Элемент не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Изменить элемент
```
ГруппаПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_ДоговорыКонтрагентов;
Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000007", ГруппаПВХ);
Если НЕ Свойство.Пустая() Тогда
	
	СвойствоОбъект = Свойство.ПолучитьОбъект();
	СвойствоОбъект.Наименование = "№ по данным клиента";
	СвойствоОбъект.Записать();
	
	Текст = "Наименование успешно изменено";
	
Иначе
	Текст = "Элемент не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Пометить на удаление
```
ГруппаПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_ДоговорыКонтрагентов;
Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000007", ГруппаПВХ);
Если НЕ Свойство.Пустая() Тогда
	
	СвойствоОбъект = Свойство.ПолучитьОбъект();
	СвойствоОбъект.УстановитьПометкуУдаления(Истина);

	Текст = "Элемент успешно помечен на удаление";
	
Иначе
	Текст = "Элемент не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Проверить заполнение элемента
```
СвойствоЦвет = ПланыВидовХарактеристик.ДополнительныеРеквизиты.СоздатьЭлемент();
СвойствоЦвет.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.СтраныМира");
СвойствоЦвет.Наименование = "Цвет";
СвойствоЦвет.Записать();
НетОшибок = СвойствоЦвет.ПроверитьЗаполнение();
Текст = ?(НетОшибок, "Ошибок не обнаружено", "Есть ошибки");
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Получить выборку

```
// все доп. реквизиты
Выборка = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Выбрать();
Пока Выборка.Следующий() Цикл
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Выборка.Наименование;
	Сообщение.Сообщить();
	
КонецЦикла;


// доп. реквизиты справочника "Номенклатура"
ГруппаПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_Номенклатура;
Выборка = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Выбрать(ГруппаПВХ);
Пока Выборка.Следующий() Цикл
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Выборка.Наименование;
	Сообщение.Сообщить();
	
КонецЦикла;
```

+ Пустая ссылка

```
ПустаяСсылка = ПланыВидовХарактеристик.ДополнительныеРеквизиты.ПустаяСсылка();
```

+ Предопределенный элемент

```
ГруппаКонтрагенты = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_Контрагенты;
ГруппаОрганизации = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_Организации;
```

+ Создать элемент в группе
```
НовыйЭлемент = ПланыВидовХарактеристик.ДополнительныеРеквизиты.СоздатьЭлемент();
НовыйЭлемент.Родитель = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_Контрагенты;
НовыйЭлемент.Наименование = "Режим работы";
НовыйЭлемент.ТипЗначения = Новый ОписаниеТипов("Строка");
НовыйЭлемент.Записать();

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = "Доп. реквизит успешно добавлен";
Сообщение.Сообщить();
```

# 2-3
# Программная работа с планом счетов
 [Оглавление](#Oglavlenie)

![Программная работа с планом счетов](/images/Программна%20работа%20с%20документами,%20ПВХ,%20планами%20счетов%20и%20планами%20видов%20расчета/ПланСчетов.png)

 + Создать счет
 ```
 СчетОбъект = ПланыСчетов.Хозрасчетный.СоздатьСчет();
СчетОбъект.Код = "01";
СчетОбъект.Вид = ВидСчета.Активный;
СчетОбъект.Наименование = "Основные средства";
СчетОбъект.Записать();
 ```
 + Создать субсчет
 ```
 СчетРодитель = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01");
Если НЕ СчетРодитель.Пустая() Тогда
	
	СчетОбъект = ПланыСчетов.Хозрасчетный.СоздатьСчет();
	СчетОбъект.Код = "01.01";
	СчетОбъект.Родитель = СчетРодитель;
	СчетОбъект.Вид = ВидСчета.Активный;
	СчетОбъект.Наименование = "Транспортные средства";
	СчетОбъект.Записать();
	Текст = "Субсчет успешно создан";	
Иначе
	Текст = "Не найден родительский счет";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
 ```

 + Найти счет
 ```
 // по коду
СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.01");
Если НЕ СчетСсылка.Пустая() Тогда
	Текст = Строка(СчетСсылка) + ": " + СчетСсылка.Вид;
Иначе
	Текст = "Счет не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
 ```
 + Изменить счет

 ```
 // по коду
СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.01");
Если НЕ СчетСсылка.Пустая() Тогда
	
	СчетОбъект = СчетСсылка.ПолучитьОбъект();
	СчетОбъект.Код = "01.02";
	СчетОбъект.Записать();
	Текст = "Код счета изменен";
Иначе
	Текст = "Счет не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();

 ```

 + Пометить на удаление
 ```
 // по коду
СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.02");
Если НЕ СчетСсылка.Пустая() Тогда
	СчетОбъект = СчетСсылка.ПолучитьОбъект();
	СчетОбъект.УстановитьПометкуУдаления(Истина);
	Текст = "Счет помечен на удаление";
Иначе
	Текст = "Счет не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
 ```

 + Определить вид счета

 ```
 // по коду
СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.02");
Если НЕ СчетСсылка.Пустая() Тогда
	Текст = СчетСсылка.Вид;
Иначе
	Текст = "Счет не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();

 ```
 + Виды субконто счета
 ```
 // обход всех видов субконто счета
СчетТовары = ПланыСчетов.Хозрасчетный.Товары;
СчетТоварыОбъект = СчетТовары.ПолучитьОбъект();
Для каждого СтрокаВида Из СчетТоварыОбъект.ВидыСубконто Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрокаВида.ВидСубконто;
	Сообщение.Сообщить();
КонецЦикла;

// добавление вида субконто
СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01");
Если НЕ СчетСсылка.Пустая() Тогда
	
	СчетОбъект = СчетСсылка.ПолучитьОбъект();
	
	СтрокаВида = СчетОбъект.ВидыСубконто.Добавить();
	СтрокаВида.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
	
	СчетОбъект.Записать();
	
	Текст = "Субконто успешно добавлено";
	
Иначе
	Текст = "Счет не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
 ```

 + Проверить заполнение счета
 ```
 СчетСсылка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01");
Если НЕ СчетСсылка.Пустая() Тогда
	
	СчетОбъект = СчетСсылка.ПолучитьОбъект();

	НетОшибок = СчетОбъект.ПроверитьЗаполнение();
	
	Текст = ?(НетОшибок, "Ошибок не обнаружено", "Есть ошибки");
	
Иначе
	Текст = "Счет не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
 ```
 + Получить выборку
 ```
 //// выборка всех счетов
//Выборка = ПланыСчетов.Хозрасчетный.Выбрать();
//Пока Выборка.Следующий() Цикл
//	Сообщение = Новый СообщениеПользователю;
//	Сообщение.Текст = Выборка.Код + ". " + Выборка.Наименование;
//	Сообщение.Сообщить();
//КонецЦикла;

// выборка субсчетов конкретного счета
СчетОС = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01");
Если НЕ СчетОС.Пустая() Тогда
	
	Выборка = ПланыСчетов.Хозрасчетный.Выбрать(СчетОС);
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Код + ". " + Выборка.Наименование;
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Счет не найден";
	Сообщение.Сообщить();
КонецЕсли;
 ```
+ Пустая ссылка
```
ПустаяСсылка = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
```

+ Предопределенный счет
```
СчетРасчетныеСчета = ПланыСчетов.Хозрасчетный.РасчетныеСчета;
СчетТовары = ПланыСчетов.Хозрасчетный.Товары;
```
# 2-4
# Программная раюота с планом видов расчета
[Оглавление](#Oglavlenie)

![Программная работа с документами](/images/Программна%20работа%20с%20документами,%20ПВХ,%20планами%20счетов%20и%20планами%20видов%20расчета/План%20Видов%20Регистра.png)

+ Создать вид расчета
```
ВидРасчета = ПланыВидовРасчета.Начисления.СоздатьВидРасчета();
ВидРасчета.Код = "ГНР";
ВидРасчета.Наименование = "Гонорар";
ВидРасчета.Записать();
```

+ Найти вид расчета
```
// по коду
ВидРасчета = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");

Если НЕ ВидРасчета.Пустая() Тогда
	Текст = Строка(ВидРасчета);
Иначе	
	Текст = "Вид расчета не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
	
// по наименованию

// по реквизиту
```

+ Изменить вид расчета
```
// по коду
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();
	ВидРасчетаОбъект.Наименование = "Гонорар за успехи в работе";
	ВидРасчетаОбъект.Записать();
	Текст = "Вид расчета изменен";
Иначе
	Текст = "Вид расчета не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Пометить на удаление
```
// по коду
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();
	ВидРасчетаОбъект.УстановитьПометкуУдаления(Истина);
	Текст = "Вид расчета помечен на удаление";
Иначе
	Текст = "Вид расчета не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Базовые виды расчета
```
// по коду
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();
	
	СтрокаТаблицы = ВидРасчетаОбъект.БазовыеВидыРасчета.Добавить();
	СтрокаТаблицы.ВидРасчета = ПланыВидовРасчета.Начисления.Оклад;
	
	ВидРасчетаОбъект.Записать();
	Текст = "Вид расчета изменен";

Иначе
	
	Текст = "Вид расчета не найден";

КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Ведущие виды расчета
```
// по коду
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();
	
	СтрокаТаблицы = ВидРасчетаОбъект.ВедущиеВидыРасчета.Добавить();
	СтрокаТаблицы.ВидРасчета = ПланыВидовРасчета.Начисления.Оклад;

	СтрокаТаблицы = ВидРасчетаОбъект.ВедущиеВидыРасчета.Добавить();
	СтрокаТаблицы.ВидРасчета = ПланыВидовРасчета.Начисления.Прогул;
	
	ВидРасчетаОбъект.Записать();
	Текст = "Вид расчета изменен";

Иначе
	
	Текст = "Вид расчета не найден";

КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```
+ Вытесняющие виды расчета
```
// по коду
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();
	
	СтрокаТаблицы = ВидРасчетаОбъект.ВытесняющиеВидыРасчета.Добавить();
	СтрокаТаблицы.ВидРасчета = ПланыВидовРасчета.Начисления.Прогул;
	
	ВидРасчетаОбъект.Записать();
	Текст = "Вид расчета изменен";

Иначе
	
	Текст = "Вид расчета не найден";

КонецЕсли;
```

+ Проверить заполнение вида расчета
```
ВидРасчетаСсылка = ПланыВидовРасчета.Начисления.НайтиПоКоду("ГНР");
Если НЕ ВидРасчетаСсылка.Пустая() Тогда
	
	ВидРасчетаОбъект = ВидРасчетаСсылка.ПолучитьОбъект();

	НетОшибок = ВидРасчетаОбъект.ПроверитьЗаполнение();
	
	Текст = ?(НетОшибок, "Ошибок не обнаружено", "Есть ошибки");
	
Иначе
	Текст = "Счет не найден";
КонецЕсли;

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Получить выборку
```
Выборка = ПланыВидовРасчета.Начисления.Выбрать();
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Выборка.Код + ", " + Выборка.Наименование;
	Сообщение.Сообщить();
КонецЦикла;
```

+ Пустая ссылка
```
ПустаяСсылка = ПланыВидовРасчета.Начисления.ПустаяСсылка();
```

+ Предопределенный вид расчета
```
ВидРасчетаОклад = ПланыВидовРасчета.Начисления.Оклад;
ВидРасчетаПремия = ПланыВидовРасчета.Начисления.ПремияПроцентом;
```

# 3 
# Программная работа с регистрами сведений и регистрами накоплений.
[Оглавление](#Oglavlenie)

# 3-1
# Программная работа с регистрами

![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/Набор%20Записей.png)
![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/НаборЗаписей%202.png)
![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/НаборЗаписей%203.png)
![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/НаборЗаписей%204.png)

# 3-2
# Программная работа с регистрами сведений
[Оглавление](#Oglavlenie)

![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/Программная%20работа%20с%20регитсрами%20сведений.png)

+ На что обращать внимание
![Программная работа с регистрами](/images/РегистрСведенийРегистрНакоплений/Вниманеие%20регистр%20сведенй.png)

+ Выборка записей (периодический)
```
// все записи
Выборка = РегистрыСведений.КурсыВалют.Выбрать();
Пока Выборка.Следующий() Цикл
	Текст = СтрШаблон("1 %1 = %2 за %3 ед.", 
			Выборка.Валюта, Выборка.Курс, Выборка.Кратность);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
КонецЦикла;


// история изменения курсов за текущий месяц
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = ТекущаяДата();

Выборка = РегистрыСведений.КурсыВалют.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	Текст = СтрШаблон("1 %1 = %2 за %3 ед.", 
			Выборка.Валюта, Выборка.Курс, Выборка.Кратность);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
КонецЦикла;

// история изменения курса USD за текущий месяц
Валюта = Справочники.Валюты.НайтиПоКоду("840");
Если НЕ Валюта.Пустая() Тогда
	
	Отбор = Новый Структура("Валюта", Валюта);
	
	Выборка = РегистрыСведений.КурсыВалют.Выбрать(ДатаНачала, ДатаОкончания, Отбор);
	Пока Выборка.Следующий() Цикл
		Текст = СтрШаблон("1 %1 = %2 за %3 ед.",
		Выборка.Валюта, Выборка.Курс, Выборка.Кратность);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Не найдена валюта";
	Сообщение.Сообщить();
КонецЕсли;	
```

+ Выборка записей (непериодический)
```
Выборка = РегистрыСведений.ГрафикРаботы.Выбрать();
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "" + Выборка.ДатаГрафика + "; " + Выборка.Значение;
	Сообщение.Сообщить();
КонецЦикла;
```

+ Выборка записей по регистратору
```
ДокументСсылка = Документы.ПриходнаяНакладная.НайтиПоНомеру("00000000007", ТекущаяДата());

Если НЕ ДокументСсылка.Пустая() Тогда
	
	Выборка = РегистрыСведений.ЦеныПоставщиков.ВыбратьПоРегистратору(ДокументСсылка);
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Товар: %1; Цена: %2",
							Выборка.Номенклатура, Выборка.Цена);
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Не найден документ";
	Сообщение.Сообщить();
КонецЕсли;	
	
```

+ Добавить запись
```
// 1. Менеджер записи
Валюта = Справочники.Валюты.НайтиПоКоду("840");
Если НЕ Валюта.Пустая() Тогда	
	Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДата();
	Запись.Валюта = Валюта;
	Запись.Курс = 98.92;
	Запись.Кратность = 1;
	Запись.Записать();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;

// 2. Набор записей
Набор = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();

// ВАЖНО: Если не установить отбор в наборе записей, регистр будет очищен
Набор.Отбор.Валюта.Установить(Валюта);
Набор.Отбор.Период.Установить(ТекущаяДата());

Запись = Набор.Добавить();
Запись.Период = ТекущаяДата();
Запись.Валюта = Валюта;
Запись.Курс = 97.48;
Запись.Кратность = 1;

Набор.Записать();
```

+ Изменить запись
```
// 1. Менеджер записи
Валюта = Справочники.Валюты.НайтиПоКоду("840");
Если НЕ Валюта.Пустая() Тогда	
	Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
	Запись.Период = '20231110';
	Запись.Валюта = Валюта;
	Запись.Прочитать();
	Запись.Курс = 95.98;
	Запись.Записать();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;

// 2. Набор записей
Набор = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
Набор.Отбор.Валюта.Установить(Валюта);
Набор.Прочитать();

Для каждого Запись Из Набор Цикл
	
	Если Запись.Период >= '20231101' И Запись.Период <= '20231130' Тогда
		Запись.Курс = Запись.Курс + 0.01;
	КонецЕсли;
	
КонецЦикла;

Набор.Записать();

//ТаблицаЗаписей = Набор.Выгрузить();
//ТаблицаЗаписей.ВыбратьСтроку();
```

+ Удалить запись

```
// 1. Менеджер записи
Валюта = Справочники.Валюты.НайтиПоКоду("840");
ДатаКурса = '20231103';
Если НЕ Валюта.Пустая() Тогда	
	
	Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
	Запись.Валюта = Валюта;
	Запись.Период = ДатаКурса;
	Запись.Удалить();

Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;

// 2. Набор записей
Набор = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
Набор.Отбор.Валюта.Установить(Валюта);
Набор.Отбор.Период.Установить(ДатаКурса);

	ТаблицаЗаписей = Набор.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку();

Набор.Записать();
```

+ Получить ресурсы по измерениям (периодический)
```
ДатаКурса = '20231105';

Валюта = Справочники.Валюты.НайтиПоКоду("840");
ДатаКурса = '20231101';
Если НЕ Валюта.Пустая() Тогда	
	Отбор = Новый Структура("Валюта", Валюта);
	ДанныеКурса = РегистрыСведений.КурсыВалют.Получить(ДатаКурса, Отбор);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("%1 за %2 ед.", ДанныеКурса.Курс, ДанныеКурса.Кратность);
	Сообщение.Сообщить();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;

```

+ Получить ресурсы по измерениям (непериодический)
```
Дата = '20231031';
Отбор = Новый Структура("Дата", Дата);
ДанныеГрафика = РегистрыСведений.ДанныеПроизводственногоКалендаря.Получить(Отбор);
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ДанныеГрафика.ВидДня;
Сообщение.Сообщить();
```

# 3-2-1 
# Переодический 
[Оглавление](#Oglavlenie)

+ Получить первое
```
ДатаСегодня = '20231103';

Валюта = Справочники.Валюты.НайтиПоКоду("840");

Если НЕ Валюта.Пустая() Тогда	

	Отбор = Новый Структура("Валюта", Валюта);
	
	ДанныеКурсаНаЗавтра = РегистрыСведений.КурсыВалют.ПолучитьПервое(ДатаСегодня, Отбор);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ДанныеКурсаНаЗавтра.Курс;
	Сообщение.Сообщить();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;
```

+ Получить последнее

```
ДатаСегодня = '20231103';

Валюта = Справочники.Валюты.НайтиПоКоду("840");

Если НЕ Валюта.Пустая() Тогда	

	Отбор = Новый Структура("Валюта", Валюта);
	
	ДанныеКурсаАктуального = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаСегодня, Отбор);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ДанныеКурсаАктуального.Курс;
	Сообщение.Сообщить();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюта не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;
```

+ Срез первых

```
ДатаСегодня = '20231103';

ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
ВалютаEUR = Справочники.Валюты.НайтиПоКоду("978");
Если НЕ ВалютаUSD.Пустая() И НЕ ВалютаEUR.Пустая() Тогда	

	ТаблицаКурсовНаЗавтра = РегистрыСведений.КурсыВалют.СрезПервых(ДатаСегодня);
	ТаблицаКурсовНаЗавтра.ВыбратьСтроку();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюты не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;
```

+ Срез последних
```
ДатаСегодня = '20231103';

ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
ВалютаEUR = Справочники.Валюты.НайтиПоКоду("978");
Если НЕ ВалютаUSD.Пустая() И НЕ ВалютаEUR.Пустая() Тогда	

	ТаблицаКурсовНаЗавтра = РегистрыСведений.КурсыВалют.СрезПоследних(ДатаСегодня);
	ТаблицаКурсовНаЗавтра.ВыбратьСтроку();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Валюты не найдена";	
	Сообщение.Сообщить();	
КонецЕсли;
``` 

+ Добавить цены поставщиков
```
Документ = Документы.УстановкаЦен.СоздатьДокумент();
Документ.Дата = ТекущаяДата();
Документ.ВидЦен = Справочники.ВидыЦен.НайтиПоНаименованию("Розничная", Истина);

СтрокаТаблицы = Документ.Товары.Добавить();
СтрокаТаблицы.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000024");
СтрокаТаблицы.Цена = 35000;

Документ.Записать(РежимЗаписиДокумента.Проведение);
```

+ Изменение записей регистра, подчиненного регистратору
```
ДокументСсылка = Документы.УстановкаЦен.НайтиПоНомеру("000000004", ТекущаяДата());
Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000024");
Если НЕ ДокументСсылка.Пустая() Тогда
	
	Набор = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокументСсылка);
	Набор.Прочитать();

	ТаблицаЗаписей = Набор.Выгрузить();
	СтрокаТаблицы = ТаблицаЗаписей.Найти(Номенклатура, "Номенклатура");
	Если СтрокаТаблицы <> Неопределено Тогда
		СтрокаТаблицы.Цена = 45000;
	КонецЕсли;

	Набор.Загрузить(ТаблицаЗаписей);
	
	Набор.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

# 3-3
# Прогрмманая работа с регистрами накоплений
[Оглавление](#Oglavlenie)

+ Выборка записей
```
// выборка за весь период
Выборка = РегистрыНакопления.ТоварыНаСкладах.Выбрать();
Пока Выборка.Следующий() Цикл
	
	Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		Знак = "+";
	Иначе
		Знак = "-";
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("%1 %2%3", Выборка.Номенклатура, Знак, Выборка.Количество);
	Сообщение.Сообщить();
	
КонецЦикла;

// выборка за месяц
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());
Выборка = РегистрыНакопления.ТоварыНаСкладах.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	
	Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		Знак = "+";
	Иначе
		Знак = "-";
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("%1 %2%3", Выборка.Номенклатура, Знак, Выборка.Количество);
	Сообщение.Сообщить();
	
КонецЦикла;

// выборка за месяц по конкретному товару
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000025");
Если НЕ Номенклатура.Пустая() Тогда
	
	Отбор = Новый Структура("Номенклатура", Номенклатура);
	
	Выборка = РегистрыНакопления.ТоварыНаСкладах.Выбрать(ДатаНачала, ДатаОкончания, Отбор);
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			Знак = "+";
		Иначе
			Знак = "-";
		КонецЕсли;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("%1 %2%3", Выборка.Номенклатура, Знак, Выборка.Количество);
		Сообщение.Сообщить();
		
	КонецЦикла;
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Номенклатура не найден";
	Сообщение.Поле = "";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Выборка записей по регистратору
```
// 1. Через менеджер регистра
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда
	Выборка = РегистрыНакопления.ТоварыНаСкладах.ВыбратьПоРегистратору(ДокументСсылка);
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Номенклатура;
		Сообщение.Сообщить();
	КонецЦикла;
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
	
// 2. Через движения документа
Если НЕ ДокументСсылка.Пустая() Тогда
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	НаборЗаписей = ДокументОбъект.Движения.ТоварыНаСкладах;
	НаборЗаписей.Прочитать();
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Добавить запись
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыНакопления.ТоварыНаСкладах.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период = ТекущаяДата();
	Запись.ВидДвижения = ВидДвиженияНакопления.Расход;
	Запись.Регистратор = ДокументСсылка;;
	Запись.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000017");
	Запись.Склад = Справочники.Склады.НайтиПоКоду("000000002");
	Запись.Количество = 100;
	
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку();
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

```

+ Изменить запись

```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
НовыйСклад = Справочники.Склады.НайтиПоКоду("000000001");
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыНакопления.ТоварыНаСкладах.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();

	Для каждого Запись ИЗ НаборЗаписей Цикл
		Запись.Склад = НовыйСклад;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Удалить запись
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию("Планшет Apple iPad Air 2022 64GB MM9C3", Истина);
Если НЕ ДокументСсылка.Пустая() И НЕ Номенклатура.Пустая() Тогда

	НаборЗаписей = РегистрыНакопления.ТоварыНаСкладах.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();

	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	СтрокаТовар = ТаблицаЗаписей.Найти(Номенклатура, "Номенклатура");
	Если СтрокаТовар <> Неопределено Тогда
		ТаблицаЗаписей.Удалить(СтрокаТовар);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Получить остатки
```

ДатаОстатков = ТекущаяДата();

// Остатки на текущую дату в разрезе складов и номенклатуры
ТаблицаОстатков = РегистрыНакопления.ТоварыНаСкладах.Остатки();

// Остатки на текущую дату в разрезе номенклатуры
ТаблицаОстатков = РегистрыНакопления.ТоварыНаСкладах.Остатки(,,"Номенклатура");

// Остатки на выбранную дату в разрезе номенклатуры
ТаблицаОстатков = РегистрыНакопления.ТоварыНаСкладах.Остатки(ДатаОстатков,,"Номенклатура");

// Остатки по одному товару на выбранную дату в разрезе номенклатуры
НоменклатураСсылка = Справочники.Номенклатура.НайтиПоНаименованию("Смартфон Apple iPhone 14 128GB", Истина);
Если Не НоменклатураСсылка.Пустая() Тогда
	Отбор = Новый Структура("Номенклатура", НоменклатураСсылка);
	ТаблицаОстатков = РегистрыНакопления.ТоварыНаСкладах.Остатки(ДатаОстатков, Отбор,"Номенклатура");
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Номенклатура на найдена";
	Сообщение.Сообщить();
КонецЕсли;

ТаблицаОстатков.ВыбратьСтроку();
```

+ Получить обороты

```

// продажи за весь период по всем товарам и контрагентам
ТаблицаОборотов = РегистрыНакопления.Продажи.Обороты();


// продажи за месяц по всем товарам и контрагентам
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

ТаблицаОборотов = РегистрыНакопления.Продажи.Обороты(ДатаНачала, ДатаОкончания);


// продажи по одному товару за весь период по контрагентам
НоменклатураСсылка = Справочники.Номенклатура.НайтиПоНаименованию("Планшет Apple iPad Air 2022 64GB MM9C3", Истина);
Если Не НоменклатураСсылка.Пустая() Тогда
	Отбор = Новый Структура("Номенклатура", НоменклатураСсылка);
	ТаблицаОборотов = РегистрыНакопления.Продажи.Обороты(,,Отбор);
	ТаблицаОборотов.ВыбратьСтроку();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Номенклатура на найдена";
	Сообщение.Сообщить();
КонецЕсли;
```

# 3-4
# Дополнительно
[Оглавление](#Oglavlenie)

+ Цены поставщиков
```
Номенклатура = Справочники.Номенклатура.НайтиПоКоду("00000000004");
Если НЕ Номенклатура.Пустая() Тогда
	
	Отбор = Новый Структура("Номенклатура", Номенклатура);
	Выборка = РегистрыСведений.ЦеныПоставщиков.Выбрать(,,Отбор);
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Товар: %1; Цена: %2", Выборка.Номенклатура, Выборка.Цена);
		Сообщение.Сообщить();
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Контрагент не найден";
	Сообщение.Сообщить();	
КонецЕсли;
```

+ Общая сумма продаж по номенклатуре

```
// продажи по одному товару за весь период по контрагентам
НоменклатураСсылка = Справочники.Номенклатура.НайтиПоНаименованию("Планшет Apple iPad Air 2022 64GB MM9C3", Истина);
Если Не НоменклатураСсылка.Пустая() Тогда
	Отбор = Новый Структура("Номенклатура", НоменклатураСсылка);
	ТаблицаОборотов = РегистрыНакопления.Продажи.Обороты(,,Отбор, "Номенклатура", "Сумма");
	ТаблицаОборотов.ВыбратьСтроку();
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Номенклатура на найдена";
	Сообщение.Сообщить();
КонецЕсли;
```

# 4 
# Программная работа с регистрами бухгалтерии и регистрами сведений + Бизнес процессы

[Оглавление](#Oglavlenie)
# 4-1 
# Программная работа с регистрами бухгалтерии
![Программная работа с регистрами бухгалтерии](/images/Регистры%20бухгалтерии%20и%20регистры%20расчета/РегистрБухгалтерии.png)

+ Выборка записей
```

// выборка всех проводок за весь период
Выборка = РегистрыБухгалтерии.Хозрасчетный.Выбрать();
Пока Выборка.Следующий() Цикл
	
	ТекстПроводки = СтрШаблон("Дт %1 Кт %2 Сумма: %3", Выборка.СчетДт, Выборка.СчетКт, Выборка.Сумма);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстПроводки;
	Сообщение.Сообщить();
	
КонецЦикла;

// выборка проводок за месяц
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

Выборка = РегистрыБухгалтерии.Хозрасчетный.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	
	ТекстПроводки = СтрШаблон("Дт %1 Кт %2 Сумма: %3", Выборка.СчетДт, Выборка.СчетКт, Выборка.Сумма);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстПроводки;
	Сообщение.Сообщить();
		
КонецЦикла;

// выборка проводок за месяц по организации
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

// ВАЖНО: Измерение/реквизит отбора должно быть проиндексировано
//Отбор = Новый Структура("Организация", Справочники.Организации.ОсновнаяОрганизация);
Отбор = Новый Структура("Счет", ПланыСчетов.Хозрасчетный.Товары);

Выборка = РегистрыБухгалтерии.Хозрасчетный.Выбрать(ДатаНачала, ДатаОкончания, Отбор);
Пока Выборка.Следующий() Цикл
	
	ТекстПроводки = СтрШаблон("Дт %1 Кт %2 Сумма: %3", Выборка.СчетДт, Выборка.СчетКт, Выборка.Сумма);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстПроводки;
	Сообщение.Сообщить();
	
КонецЦикла;

```

+ Выборка записей по регистратору
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());

// 1. Через менеджер регистра
Если НЕ ДокументСсылка.Пустая() Тогда
	Выборка = РегистрыБухгалтерии.Хозрасчетный.ВыбратьПоРегистратору(ДокументСсылка);
	Пока Выборка.Следующий() Цикл
		
		ТекстПроводки = СтрШаблон("Дт %1 Кт %2 Сумма: %3", Выборка.СчетДт, Выборка.СчетКт, Выборка.Сумма);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстПроводки;
		Сообщение.Сообщить();
	КонецЦикла;

	Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
//	
// 2. Через движения документа
Если НЕ ДокументСсылка.Пустая() Тогда
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	НаборЗаписей = ДокументОбъект.Движения.Хозрасчетный;
	НаборЗаписей.Прочитать();
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку(); //отладка
	
	Для каждого Запись Из НаборЗаписей Цикл

		ТекстПроводки = СтрШаблон("Дт %1 Кт %2 Сумма: %3", Запись.СчетДт, Запись.СчетКт, Запись.Сумма);

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстПроводки;
		Сообщение.Сообщить();
		
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

```

+ Добавить запись
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период = ТекущаяДата();
	Запись.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
	Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = ДокументСсылка.Контрагент;
	Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоговорыКонтрагентов] = ДокументСсылка.Договор;

	Запись.СчетКт = ПланыСчетов.Хозрасчетный.Товары;
	Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = Справочники.Номенклатура.НайтиПоКоду("00000000017");
	Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады] = Справочники.Склады.НайтиПоКоду("000000002");
	Запись.КоличествоКт = 10;
	Запись.Сумма = 10000;
	Запись.Регистратор = ДокументСсылка;
	
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку();
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;


```

+ Изменить запись
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
НовыйСклад = Справочники.Склады.НайтиПоКоду("000000001");
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();

	ВидСубконтоСклад = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
	
	Для каждого Запись ИЗ НаборЗаписей Цикл
		//Запись.СубконтоКт[ВидСубконтоСклад] = НовыйСклад;
		Запись.СубконтоКт.Склады = НовыйСклад;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

```

+ Удалить запись
```
ДокументСсылка = Документы.РасходнаяНакладная.НайтиПоНомеру("000000006", ТекущаяДата());
Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию("Планшет Apple iPad Air 2022 64GB MM9C3", Истина);
Если НЕ ДокументСсылка.Пустая() И НЕ Номенклатура.Пустая() Тогда

	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();

	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	СтрокаТовар = ТаблицаЗаписей.Найти(Номенклатура, "СубконтоКт1");
	Если СтрокаТовар <> Неопределено Тогда
		ТаблицаЗаписей.Удалить(СтрокаТовар);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

```

+ Получить остатки 
```
ДатаОстатков = ТекущаяДата();

// Остатки на текущую дату по всем счетам
ТаблицаОстатков = РегистрыБухгалтерии.Хозрасчетный.Остатки();
ТаблицаОстатков.ВыбратьСтроку();

 Остатки на выбранную дату по всем счетам
ТаблицаОстатков = РегистрыБухгалтерии.Хозрасчетный.Остатки(ДатаОстатков);
ТаблицаОстатков.ВыбратьСтроку();

 Остатки на текущую дату по счету 41 (Товары) в разрезе всех субконто
Счет = ПланыСчетов.Хозрасчетный.Товары;
Отбор = Новый Структура("Счет", Счет);
ТаблицаОстатков = РегистрыБухгалтерии.Хозрасчетный.Остатки(,,Отбор);
ТаблицаОстатков.ВыбратьСтроку();

// Остатки на текущую дату по счету 41 (Товары) в разрезе субконто "Товары"
Счет = ПланыСчетов.Хозрасчетный.Товары;
Отбор = Новый Структура("Счет", Счет);
МассивВидовСубконто = Новый Массив;
МассивВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);

ТаблицаОстатков = РегистрыБухгалтерии.Хозрасчетный.Остатки(, МассивВидовСубконто, Отбор, "Счет, Субконто1","Сумма");
ТаблицаОстатков.ВыбратьСтроку();

```
+ Получить обороты

```
Счет = ПланыСчетов.Хозрасчетный.Товары;

// обороты по счету 41 (Товары) за весь период
Отбор = Новый Структура("Счет", Счет);
ТаблицаОборотов = РегистрыБухгалтерии.Хозрасчетный.Обороты(,,,,Отбор,);
ТаблицаОборотов.ВыбратьСтроку();

// обороты по дебету счета 41 (Товары) за весь период
Отбор = Новый Структура("СчетДт", Счет);
ТаблицаОборотов = РегистрыБухгалтерии.Хозрасчетный.ОборотыДтКт(,,,,Отбор,);
ТаблицаОборотов.ВыбратьСтроку();

```
![Программная работа с регистрами бухгалтерии](/images/Регистры%20бухгалтерии%20и%20регистры%20расчета/На%20что%20обращать%20внимание%20РБ.png)

# 4-2 
# Регистры расчета
[Оглавление](#Oglavlenie)
![Программная работа с регистрами бухгалтерии](/images/Регистры%20бухгалтерии%20и%20регистры%20расчета/РегистрБухгалтерии.png)

+ Выборка записей
```
// выборка записей за весь период
Выборка = РегистрыРасчета.Начисления.Выбрать();
Пока Выборка.Следующий() Цикл
	
	ТекстСообщения = СтрШаблон("Сотрудник:%1; Начисление: %2; Сумма: %3", 
								Выборка.Сотрудник, Выборка.ВидРасчета, Выборка.Результат);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецЦикла;

// выборка записей за месяц
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

Выборка = РегистрыРасчета.Начисления.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	
	ТекстСообщения = СтрШаблон("Сотрудник:%1; Начисление: %2; Сумма: %3", 
								Выборка.Сотрудник, Выборка.ВидРасчета, Выборка.Результат);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
		
КонецЦикла;

// выборка записей за месяц с отбором по сотруднику
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());

// ВАЖНО: Измерение/реквизит отбора должно быть проиндексировано
Отбор = Новый Структура("Сотрудник", Справочники.Сотрудники.НайтиПоКоду("00001"));

Выборка = РегистрыРасчета.Начисления.Выбрать(ДатаНачала, ДатаОкончания, Отбор);
Пока Выборка.Следующий() Цикл
	
	ТекстСообщения = СтрШаблон("Сотрудник:%1; Начисление: %2; Сумма: %3", 
								Выборка.Сотрудник, Выборка.ВидРасчета, Выборка.Результат);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецЦикла;

```

+ Выборка записей по регистратору
```

ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000003", ТекущаяДата());

// 1. Через менеджер регистра
Если НЕ ДокументСсылка.Пустая() Тогда
	Выборка = РегистрыРасчета.Начисления.ВыбратьПоРегистратору(ДокументСсылка);
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон("Сотрудник:%1; Начисление: %2; Сумма: %3",
		Выборка.Сотрудник, Выборка.ВидРасчета, Выборка.Результат);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

// 2. Через движения документа
Если НЕ ДокументСсылка.Пустая() Тогда
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	НаборЗаписей = ДокументОбъект.Движения.Начисления;
	НаборЗаписей.Прочитать();
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку(); //отладка
	
	Для каждого Запись Из НаборЗаписей Цикл
		
		ТекстСообщения = СтрШаблон("Сотрудник:%1; Начисление: %2; Сумма: %3",
		Запись.Сотрудник, Запись.ВидРасчета, Запись.Результат);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
	КонецЦикла;
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Добавить запись
```
ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000003", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыРасчета.Начисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	ДатаНачала = НачалоМесяца(ТекущаяДата());
	ДатаОкончания = КонецМесяца(ТекущаяДата());
	
	Запись = НаборЗаписей.Добавить();
	Запись.ПериодДействияНачало = ДатаНачала;
	Запись.ПериодДействияКонец = ДатаОкончания;
	Запись.БазовыйПериодНачало = ДатаНачала;
	Запись.БазовыйПериодКонец = ДатаОкончания;
	Запись.ПериодРегистрации = ДатаНачала;
	Запись.Сотрудник = Справочники.Сотрудники.НайтиПоКоду("00001");
	Запись.ВидРасчета = ПланыВидовРасчета.Начисления.ПремияПроцентом;
	Запись.Размер = 10;
	Запись.Результат = 1000;
	
	Запись.Регистратор = ДокументСсылка;
	
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ВыбратьСтроку();
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;

```

+ Изменить запись
```
ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000003", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыРасчета.Начисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	Для каждого Запись ИЗ НаборЗаписей Цикл
		Запись.Результат = Запись.Результат * 1.1;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Удалить запись
```
ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000003", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	НаборЗаписей = РегистрыРасчета.Начисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();

	ВидРасчета = ПланыВидовРасчета.Начисления.Оклад;
	
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	СтрокаТаблицы = ТаблицаЗаписей.Найти(ВидРасчета, "ВидРасчета");
	Если СтрокаТаблицы <> Неопределено Тогда
		ТаблицаЗаписей.Удалить(СтрокаТаблицы);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	
	НаборЗаписей.Записать();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ Получить данные графика
```
ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000001", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	// Получение информации о количестве часов по графику
	Отбор = Новый Структура("Регистратор", ДокументСсылка);
//	ДанныеГрафика = РегистрыРасчета.Начисления.ПолучитьДанныеГрафика(Отбор, ВидПериодаРегистраРасчета.ПериодДействия);
//	ДанныеГрафика.ВыбратьСтроку();
//
//	 Получение информации о количестве отработанных часов (с учетом вытеснения)
	 
	Отбор = Новый Структура("Регистратор", ДокументСсылка);
	ДанныеГрафика = РегистрыРасчета.Начисления.ПолучитьДанныеГрафика(Отбор, ВидПериодаРегистраРасчета.ФактическийПериодДействия);
	ДанныеГрафика.ВыбратьСтроку();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

+ ПолучитьБазу
```
ДокументСсылка = Документы.НачислениеЗарплаты.НайтиПоНомеру("000000002", ТекущаяДата());
Если НЕ ДокументСсылка.Пустая() Тогда

	// Получение базы для расчета премии
	Отбор = Новый Структура("Регистратор", ДокументСсылка);
	Ресурсы = Новый Массив;
	Ресурсы.Добавить("Начисления.Результат");
	
	Измерения = Новый Структура("Сотрудник", "Начисления.Сотрудник");
	
	ДанныеГрафика = РегистрыРасчета.Начисления.ПолучитьБазу(Отбор, Ресурсы, Измерения);
	ДанныеГрафика.ВыбратьСтроку();
	
Иначе
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ не найден";
	Сообщение.Сообщить();
КонецЕсли;
```

# 4-3 
# Бизнес процессы

# 4-3-1 
# Задачи
[Оглавление](#Oglavlenie)

![Программная работа с регистрами бухгалтерии](/images/Регистры%20бухгалтерии%20и%20регистры%20расчета/Задачи.png)


+ Создать задачу
```
Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
Задача.Дата = ТекущаяДата();
Задача.Наименование = "Отправить счет контрагенту Рога и Копыта";
Задача.Автор = ПараметрыСеанса.ТекущийПользователь;
Задача.Описание = "Отправить счет за товары контрагенту";
Задача.СрокИсполнения = ТекущаяДата() + 2*3600;
Задача.Записать();

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = "Создана задача: " + Задача.Ссылка;
Сообщение.Сообщить();
```

+ Найти задачу
```
// по наименованию
Задача = Задачи.ЗадачаИсполнителя.НайтиПоНаименованию("Заказ товара", Истина);
Если НЕ Задача.Пустая() Тогда
	ТекстСообщения = Задача.Наименование;
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();
	
// по номеру
Задача = Задачи.ЗадачаИсполнителя.НайтиПоНомеру("000000047");
Если НЕ Задача.Пустая() Тогда
	ТекстСообщения = Задача.Наименование;
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();

// по реквизиту
Пользователь = Справочники.Пользователи.НайтиПоКоду("Миша");
Задача = Задачи.ЗадачаИсполнителя.НайтиПоРеквизиту("Исполнитель", Пользователь);
Если НЕ Задача.Пустая() Тогда
	ТекстСообщения = Задача.Наименование;
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();
```

+ Изменить задачу
```
Задача = Задачи.ЗадачаИсполнителя.НайтиПоНомеру("000000047");
Если НЕ Задача.Пустая() Тогда
	ЗадачаОбъект = Задача.ПолучитьОбъект();
	ЗадачаОбъект.СрокИсполнения = ТекущаяДата() + 2*86400;
	ЗадачаОбъект.Записать();
	ТекстСообщения = "Срок исполнения задачи изменен";
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();
```

+ Пометить на удаление
```
Задача = Задачи.ЗадачаИсполнителя.НайтиПоНомеру("000000047");
Если НЕ Задача.Пустая() Тогда
	ЗадачаОбъект = Задача.ПолучитьОбъект();
	ЗадачаОбъект.УстановитьПометкуУдаления(Истина);
	ТекстСообщения = "Задача помечена на удаление";
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();
```

+ Выполнить
```
// по номеру
Задача = Задачи.ЗадачаИсполнителя.НайтиПоНомеру("000000050");
Если НЕ Задача.Пустая() Тогда
	
	ЗадачаОбъект = Задача.ПолучитьОбъект();
	ЗадачаОбъект.ВыполнитьЗадачуИнтерактивно();
	
Иначе
	ТекстСообщения = "Задача не найдена";
КонецЕсли;	

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = ТекстСообщения;
Сообщение.Сообщить();
```

+ Выбрать задачи
```
ДатаНачала = НачалоМесяца(ТекущаяДата());
ДатаОкончания = КонецМесяца(ТекущаяДата());


Выборка = Задачи.ЗадачаИсполнителя.Выбрать(ДатаНачала, ДатаОкончания);
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("№ %1, исполнитель %2, выполнена: %3",
					Выборка.Номер, Выборка.Исполнитель, Выборка.Выполнена);
	Сообщение.Сообщить();
КонецЦикла;
```

# 4-3-2
# Бизнес процессы
[Оглавление](#Oglavlenie)

![Программная работа с регистрами бухгалтерии](/images/Регистры%20бухгалтерии%20и%20регистры%20расчета/Бизнес%20Процессы.png)

+ Точки маршрута 
```
Для каждого ТочкаМаршрута Из БизнесПроцессы.Закупка.ТочкиМаршрута Цикл
	ТекстСообщения = СтрШаблон("%1 (вид: %2)", ТочкаМаршрута.Имя, ТочкаМаршрута.Вид);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
КонецЦикла;

```

+ Создать бизнес-процесс
```
БизнесПроцесс = БизнесПроцессы.Закупка.СоздатьБизнесПроцесс();
БизнесПроцесс.Дата = ТекущаяДата();
БизнесПроцесс.Записать();

Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = "Создан бизнес-процесс: " + БизнесПроцесс.Ссылка;
Сообщение.Сообщить();
```

+ Найти бизнес-процесс
```
БизнесПроцесс = БизнесПроцессы.Закупка.НайтиПоНомеру("000000008");
Если НЕ БизнесПроцесс.Пустая() Тогда
	Текст = Формат(БизнесПроцесс.Дата, "ДФ=dd.MM.yyyy");
Иначе
	Текст = "Бизнес-процесс не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
	
```

+ Стартовать бизнес-процесс
```
БизнесПроцесс = БизнесПроцессы.Закупка.НайтиПоНомеру("000000008");
Если НЕ БизнесПроцесс.Пустая() Тогда
	
	БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
	БизнесПроцессОбъект.Старт();
	Текст = "Бизнес-процесс стартован";
	
Иначе
	Текст = "Бизнес-процесс не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
	
```


+ Изменить бизнес-процесс
```
БизнесПроцесс = БизнесПроцессы.Закупка.НайтиПоНомеру("000000008");
Если НЕ БизнесПроцесс.Пустая() Тогда
	
	БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
	БизнесПроцессОбъект.Дата = ТекущаяДата() - 86400;
	БизнесПроцессОбъект.Записать();
	Текст = "Дата бизнес-процесса изменена";
	
Иначе
	Текст = "Бизнес-процесс не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
	
```

+ Получить карту маршрута
```
БизнесПроцесс = БизнесПроцессы.Закупка.НайтиПоНомеру("000000008");
Если НЕ БизнесПроцесс.Пустая() Тогда
	
	БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
	КартаМаршрута = БизнесПроцессОбъект.ПолучитьКартуМаршрута();
	КартаМаршрута.Показать();
	
Иначе
	Текст = "Бизнес-процесс не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();	
```

+ Пометить на удаление
```
БизнесПроцесс = БизнесПроцессы.Закупка.НайтиПоНомеру("000000008");
Если НЕ БизнесПроцесс.Пустая() Тогда
	
	БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
	БизнесПроцессОбъект.УстановитьПометкуУдаления(Истина);
	Текст = "Бизнес-процесс помечен на удаление";
	
Иначе
	Текст = "Бизнес-процесс не найден";
КонецЕсли;
Сообщение = Новый СообщениеПользователю;
Сообщение.Текст = Текст;
Сообщение.Сообщить();
```

+ Выборка бизнес-процессов
```
Выборка = БизнесПроцессы.Закупка.Выбрать('20231116', '20231116235959');
Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("№ %1 от %2", Выборка.Номер, Выборка.Дата);
	Сообщение.Сообщить();
КонецЦикла;
```
# 5
# События и виды модулей
# Примеры использования событий
[Оглавление](#Oglavlenie)

# 5-1
# Вопрос перед завершением работы
[Оглавление](#Oglavlenie)

+ Это делается в модуле приложения в самой конфигурации
```
Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	Отказ = Истина;
	ТекстПредупреждения = "Завершить работу с программой?";
	
КонецПроцедуры

```

# 5-2
# Заполенение реквизитов объекта при создании объекта
[Оглавление](#Oglavlenie)

+ Заполняется в модуле объекта нужного документа
```
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Организация = Справочники.Организации.ОсновнаяОрганизация;
	Валюта = Константы.ВалютаУчета.Получить();
	
КонецПроцедуры
```

# 5-3
# Проверка заполнения реквизитов по условию
[Оглавление](#Oglavlenie)

+ При добавлени нового контрагента , если это физ лицо, то ИНН становится не обяхательныи для заполнения.
```
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизЛицо Тогда
		Индекс = ПроверяемыеРеквизиты.Найти("ИНН");			
		Если Индекс <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

```

# 5-4
# Заполенние реквизитов перед записью
[Оглавление](#Oglavlenie)

+ Мы создаем новый реквизит "СуммаДокумента" и перед провдением документа будет заполнятся сумма всего документа
```
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	СуммаДокумента = Товары.Итог("СуммаВсего");	
	
КонецПроцедуры

```

# 5-5 
# Переопределение алгоритма копирования объекта
[Оглавление](#Oglavlenie)

+ Сделаем что бы при копировании товара "Артикул очищался"
+ Обязателно запомнить если мы работает с объектами у которых есть иерархия, то надо делать проверку на группу
```
Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		Артикул = "";	
	КонецЕсли;
	
КонецПроцедуры
	
```

# 5-6
# Добавление префикса в номер документа
[Оглавление](#Oglavlenie)

+ Для начала делаем константу Прфикс.
+ Далее мы заходим в "подписка на события" и создаем "Установить перфикс ноиера докумиента".
+ И создаем обработчик в модуле общего назначения.
+ Префикс сжирает длину кода.
```
Процедура УстановитьПрефиксНомераДокументаПриУстановкеНовогоНомера(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ПрефиксИБ = Константы.ПрефиксИнформационнойБазы.Получить();
	Если НЕ ПустаяСтрока(ПрефиксИБ) Тогда
		Префикс = ПрефиксИБ  + "-";	
	КонецЕсли;
	
КонецПроцедуры
```

# 5-7
# Изменение представления объекта
[Оглавление](#Oglavlenie)

+ Как будет представлен в других документах "Заказ клиента"
+ Это делается в менеждер объекта
```
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомерБезНулей = СокрЛП(СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Данные.Номер, "0"));
	Представление = СтрШаблон("Заказ клиента %1 от %2", НомерБезНулей, Формат(Данные.Дата, "ДФ=dd.MM.yyyy"));
	
КонецПроцедуры
```

# 5-8
# Обработка получения формы
[Оглавление](#Oglavlenie)

+ Первое создаем в спарвочиники пользователи реквизит "ИспользоватьУпрощеннуюФормуВыбораТоваров" булево
+ Далее пишем в модуле менеджера "номенклатура" код 
+ Суть такая что при выборе "ИспользоватьУпрощеннуюФормуВыбораТоваров" у нас будет открываться упращенная форма выбора. Форму которой мы созадли в справочнике "Номенклатура".
```
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;	
		Если НЕ ТекущийПользователь.Пустая() Тогда
			
			ИспользоватьУпрощеннуюФорму = ТекущийПользователь.ИспользоватьУпрощеннуюФормуВыбораТоваров;
			
			Если ИспользоватьУпрощеннуюФорму Тогда
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораУпрощенная";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

```

# 5-9
# Проверка заполнения типов отношений с контрагентом
[Оглавление](#Oglavlenie)

```

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизЛицо Тогда
		Индекс = ПроверяемыеРеквизиты.Найти("ИНН");			
		Если Индекс <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс);
		КонецЕсли;
	КонецЕсли;
	
	Если Не Покупатель И Не Поставщик И Не Прочее Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Надо указать хотя бы одно значение: ""Покупатель"", ""Поставщик"" Или ""Прочее""";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

```

# 5-10
# Очистка реквизитов при копировании Приходной накладной
[Оглавление](#Oglavlenie)

```
Процедура ПриКопировании(ОбъектКопирования)
	
	Комментарий = "";
	ДокументОснование = Документы.ЗаказПоставщику.ПустаяСсылка();
	
КонецПроцедуры

```

# 5-11
#  Пометка на удаление договоров при пометке на удаление владельца (контрагента)

[Оглавление](#Oglavlenie)

+ Сначала пишем код в модуле объекта контраегнта
```
Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.Ссылка.ПометкаУдаления <> ПометкаУдаления Тогда
		
		Справочники.ДоговорыКонтрагентов.ОбновитьПодчиненныеДоговора(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

```

+ Далее пишем код в модули менеджера договры контрагента

```
Функция ОбновитьПодчиненныеДоговора(ВладелецСсылка) Экспорт
	
	
	Выборка = Выбрать(, ВладелецСсылка);
	
	Пока Выборка.Следующий() Цикл
		
		ДоговорОб = Выборка.Ссылка.ПолучитьОбъект();
		ДоговорОб.УстановитьПометкуУдаления(ВладелецСсылка.ПометкаУдаления);		
		
	КонецЦикла;
	ИзменениеЗавершено = Истина;

	Возврат ИзменениеЗавершено;
	
КонецФункции
```

 [Ссылка!](Тест\vidy_rasteniy.pdf)