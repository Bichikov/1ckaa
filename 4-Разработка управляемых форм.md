# Oglavlenie
1. [Пользоваетльский интерфейс. Знакомство с формами.](#1)
	+ [Виды интерфейсов](#1-1)
        + [Обычный интерфейс](#1-1-1)
        + [Командный интерфейс](#1-1-2)
        + [Интерфейс такси](#1-1-3)
    + [Знакомство с формами](#1-2)
        + [Знакомство с формами](#1-2-1)
        + [Устройство формы](#1-2-2)
        + [Редактор Обычной формы](#1-2-3)
        + [Редактор Управляемой формы](#1-2-4)

2. [Типы и виды управляемой формы](#2)

3. [Программное открытие форм. Программная работа с управляемой формой](#3)
	+ [Программное открытие форм](#3-1)
        + [Форма объекта](#3-1-1)
        + [Форма списка](#3-1-2)
        + [Форма выбора](#3-1-3)
        + [Форма отчета](#3-1-4)
    + [Программная работа с управляемой формой](#3-2)
        + [Изменение Свойств Формы](#3-2-1)
        + [Тип Декорация Формы](#3-2-2)
        + [Тип Поле Формы](#3-2-3)
        + [Тип Таблица Формы](#3-2-4)
        + [Тип Команда Формы](#3-2-5)
        + [Тип Группа Формы](#3-2-6)
        + [Перемещение ЭлементовФормы и добавление всех элементов на форму](#3-2-7)

4. [Реквизиты и параметры формы. Жизненный цикл управляемой формы. События формы](#4)
	+ [Реквизиты формы](#4-1)
    + [Параметры формы](#4-2)
	+ [Жизненный цикл](#4-3)
	+ [Примеры](#4-4)
		+ [Пример отображение дополнительных реквизитов в виде полей формы](#4-4-1)
		+ [Изменение свойств элементов при открытии формы(Не показвать ИНН/КПП для физ лица.)](#4-4-2)
		+ [Отображение актуальных цен номенклатуры в форме](#4-4-3)


5. [События элементов формы. Разбор типовых задач.](#5)
	+ [События элементов форм](#5-1)
    + [иповые задачи](#5-2)
        + [Событие ПриИзменении (При изменении краткого наименования номенклатуры заполнять полное)](#5-2-1)
        + [Событие ПриИзменении(При изменении номенклатуры втроке табл. части заполнять единицу измерения и ставку НДС)](#5-2-2)
        + [Событие ПриИзменении(Пересчитать суммы при изменении значений от которых они зависят)](#5-2-3)
        + [Событие НачалоВыбора (Предопределить открываемую форму для выбора номенклатуры в заказе поставщику)](#5-2-4)
        + [Событие НачалоВыбора (Реализовать возможность выбора файла через диалог)](#5-2-5)
        + [Событие НачалоВыбора (Реализовать возможность подбора товаров в заказ клиента через стандартуню форму выбора)](#5-2-6)
        + [Событие НачалоВыбора (При выборе товара в списке добавлять его в отдельную таблицу "Корзина")](#5-2-7)
		+ [Событие ПриАктивизицииСтроки (При активизации строки в списке номенклаутры показывать остатки и цены текущего товара)](#5-2-8)
	    + [Событие ПриНачалеРедактирования (При добавлении новой строки в заказ клиента заполнять "Количество" значением пл умполчанию)](#5-2-9)
		+ [Событие ПриИзменении (Заполнение цены продажи в заказе клиента)](#5-2-10)


6. [Работа с файлами и картинками](#6)
	+ [ДиалогВыбораФайла](#6-1)
    + [Двоичные данные](#6-2)
		+ [Временное хранилище](#6-2-1)
	+ [Решение задачи](#6-3)
# 1
# Пользоваетльский интерфейс. Знакомство с формами.

# 1-1
# Пользовательский интерфейс 
# Виды интерфейсов
[Оглавление](#Oglavlenie)

![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Виды%20интерфейсов.jpg)

# 1-1-1
# Обычный интерфейс
[Оглавление](#Oglavlenie)

![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Обычный%20интерфейс.jpg)
![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Обычный%20интерфейс%202.jpg)

# 1-1-2
# Командный интерфейс
[Оглавление](#Oglavlenie)

![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Командный%20интерфейс.jpg)
![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Командный%20интерфейс2.jpg)
![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Командный%20интерфейс%203.jpg)

# 1-1-3
# Интерфейс такси
[Оглавление](#Oglavlenie)

![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Интерфейс%20такси.jpg)
![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/интерфейс%20такси%202.jpg)
![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/интерфейс%20такси%20%203.jpg)


# 1-2
# Знакомство с формами
[Оглавление](#Oglavlenie)

# 1-2-1 
# Виды форм
[Оглавление](#Oglavlenie)

![Виды интерфейсов](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Виды%20форм.jpg)

# 1-2-2
# Устройство формы
[Оглавление](#Oglavlenie)

![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Устройство%20формы.jpg)

# 1-2-3
# Редактор Обычной формы
[Оглавление](#Oglavlenie)

![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20обычной%20формы.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Типовые%20задачи.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Разработка%20формы%20элемента%20справочника.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Разработка%20формы%20документа.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20обычной%20формы%20разаботка%20формы%20списка.jpg)

# 1-2-4
# Редактор Управляемой формы
[Оглавление](#Oglavlenie)

![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Типовые%20задачи.jpg)

![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Разработка%20формы%20элемента%20справочника.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Разработка%20формы%20документа.jpg)
![Устройство формы](/images/пользовательский%20интерфейс%20Знакомство%20с%20формами/Редактор%20управляемой%20формы.%20Разарботка%20формы%20списка.jpg)

# 2
# Типы и виды управляемой формы
[Оглавление](#Oglavlenie)

+ В файлы по лекциям

# 3
# Программное открытие форм. Программная работа с управляемой формой
[Оглавление](#Oglavlenie)
+ Файл есть в файлы по лекциям
+ СинтаксисПомощник --> Интерфейс (управляемый) --> Форма клиентского приложения --> Расширение...
# 3-1 
# Программное открытие форм
[Оглавление](#Oglavlenie)

![Программное открытие форм](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программное%20открытие%20форм.jpg)
![Программное открытие форм](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программное%20открытие%20форм2.jpg)
![Программное открытие форм](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программное%20открытие%20форм%203.jpg)

# 3-1-1 
# Форма объекта
[Оглавление](#Oglavlenie)
### Пример Открыть Форму Объекта
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъекта(Команда)
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаОбъекта");
	
КонецПроцедуры
```

### Пример Открыть Форму Объекта Существующего
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъектаСуществующего(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", НайтиДокументПоНомеру("000000325"));
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция НайтиДокументПоНомеру(Номер)

	Возврат Документы.ЗаказКлиента.НайтиПоНомеру(Номер, ТекущаяДата());

КонецФункции // НайтиДокументПоНомеру()

```

### Пример Открыть Форму Объекта ТолькоПросмотр
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъектаТолькоПросмотр(Команда)
	
	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("Ключ", НайтиДокументПоНомеру("000000325"));
	ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиДокументПоНомеру(Номер)

	Возврат Документы.ЗаказКлиента.НайтиПоНомеру(Номер, ТекущаяДата());

КонецФункции // НайтиДокументПоНомеру()
```

### Пример Открыть Форму Объекта С ЗаполнениемНаОсновании
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъектаСЗаполнениемНаОсновании(Команда)

	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("Основание", НайтиДокументПоНомеру("000000325"));
	
	ОткрытьФорму("Документ.РасходнаяНакладная.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиДокументПоНомеру(Номер)

	Возврат Документы.ЗаказКлиента.НайтиПоНомеру(Номер, ТекущаяДата());

КонецФункции // НайтиДокументПоНомеру()
```

### Пример Открыть Форму Объекта Копии
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъектаКопии(Команда)

	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("ЗначениеКопирования", НайтиДокументПоНомеру("000000325"));
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиДокументПоНомеру(Номер)

	Возврат Документы.ЗаказКлиента.НайтиПоНомеру(Номер, ТекущаяДата());

КонецФункции // НайтиДокументПоНомеру()
```

### Пример Открыть Форму Объекта Значения Заполнения
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОбъектаЗначенияЗаполнения(Команда)
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Артикул", "А-123");
	
	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("ЗначенияЗаполнения", ДанныеЗаполнения);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры
```

# 3-1-2
# Форма списка
[Оглавление](#Oglavlenie)
### Пример Открыть Форму Списка
```
Процедура Пример_ОткрытьФормуСписка(Команда)
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаСписка");
	
КонецПроцедуры

```

### Пример Открыть Форму Списка С Отбором
```
&НаКлиенте
Процедура Пример_ОткрытьФормуСпискаСОтбором(Команда)
	
	МассивКонтрагентов = Новый Массив;
	МассивКонтрагентов.Добавить(НайтиКонтрагентаПоКоду("000000018"));
	МассивКонтрагентов.Добавить(НайтиКонтрагентаПоКоду("000000001"));
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Контрагент", МассивКонтрагентов);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Документ.ЗаказКлиента.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция НайтиКонтрагентаПоКоду(Код)
	
	Возврат Справочники.Контрагенты.НайтиПоКоду(Код);

КонецФункции
```

# 3-1-3
# Форма выбора
[Оглавление](#Oglavlenie)
### Пример_Открыть Форму Выбора
+ Создаем реквизит "ВыбранноеЗначение" и указываем тип "СправочникСсылка.Номенклатура"
+ Здесь мы должны помимо команды создать "Поле ввода" --> Наименование "ВыбранноеЗначение" и в это поле будет помещаться выбор.
+ Указать "путь к данным" 
```
&НаКлиенте
Процедура Пример_ОткрытьФормуВыбора(Команда)
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",,Элементы.ВыбранноеЗначение);
	
КонецПроцедуры
```

### Пример Открыть Форму Выбора Обработка Результата Закрытия
+ Тоже самое что "Пример Открыть Форму Выбора", только при закрытии будет оповещение.
```
&НаКлиенте
Процедура Пример_ОткрытьФормуВыбораОбработкаРезультатаЗакрытия(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораНоменклатуры", ЭтотОбъект);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",,Элементы.ВыбранноеЗначение,,,,Оповещение);

КонецПроцедуры 
```


### Пример Открыть Форму Выбора Множественный Выбор
+ Создаем реквизит "СписокВыбранныхЗначений" и указываем тип "СписокЗначений"
+ Здесь мы должны помимо команды создать "Таблицу" --> Наименование "СписокВыбранныхЗначений" и в эту таблицу будет помещаться выбор.
+ Указать "путь к данным" 
```
&НаКлиенте
Процедура Пример_ОткрытьФормуВыбораМножественныйВыбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтотОбъект.Элементы.СписокВыбранныхЗначений);

КонецПроцедуры
```

# 3-1-4
# Форма отчета
[Оглавление](#Oglavlenie)
### Пример Открыть Форму Отчета
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОтчета(Команда)

	ОткрытьФорму("Отчет.ОстаткиТоваров.ФормаОбъекта");
	
КонецПроцедуры
```

### Пример Открыть Форму Отчета И Сформировать
```
&НаКлиенте
Процедура Пример_ОткрытьФормуОтчетаИСформировать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ОстаткиТоваров.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры
```

### Пример Открыть Форму Отчета С Отбором
```
 &НаКлиенте
Процедура Пример_ОткрытьФормуОтчетаСОтбором(Команда)
	
	Наименование = "Планшет HONOR Pad 8 128GB";
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Период", ТекущаяДата());
	СтруктураОтбора.Вставить("Номенклатура", НайтиНоменклатуруПоНаименованию(Наименование));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Отчет.ОстаткиТоваров.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры
```


# 3-2
# Программная работа с управляемой формой
[Оглавление](#Oglavlenie)

+ Домашка Есть в файлы по лециям "программная работы с формами"

![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой1.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой2.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой3.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой4.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой5.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой6.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой7.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой8.jpg)
![Программная работа с управляемой формой](/images/Программное%20открытие%20форм.%20Программная%20работа%20с%20управляемой%20формой/Программная%20работа%20с%20управялемой%20формой9.jpg)
# 3-2-1
# Изменение Свойств Формы
[Оглавление](#Oglavlenie)
### Изменить Заголовок Формы
```
&НаКлиенте
Процедура ИзменитьЗаголовокФормы(Команда)
	
	ЭтотОбъект.АвтоЗаголовок = Ложь;
	ЭтотОбъект.Заголовок = "IRONSKILLS - Программная работа с формой";
	
КонецПроцедуры

```

### Отключить Командную Панель Формы
```
&НаКлиенте
Процедура ОтключитьКоманднуюПанельФормы(Команда)
	
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	
КонецПроцедуры

```
# 3-2-2
# Тип Декорация Формы
[Оглавление](#Oglavlenie)
### Добавить Декорацию Надпись
```
&НаКлиенте
Процедура ДобавитьДекорациюНадпись(Команда)
	ДобавитьДекорациюНадписьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеНадписиНаСервере()
	
	ИмяРеквизита = "Пользователь";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;	
	НовыйЭлемент.Гиперссылка = Истина;	
	
	// заполнение реквизита формы
	ЭтотОбъект[ИмяРеквизита] = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

```
### ДобавитьДекорациюКартинку
```
&НаКлиенте
Процедура ДобавитьДекорациюКартинку(Команда)
	ДобавитьДекорациюКартинкуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьДекорациюКартинкуНаСервере()

	Если Элементы.Найти("ПростаяКартинка") = Неопределено Тогда
		
		НовыйЭлемент = Элементы.Добавить("ПростаяКартинка", Тип("ДекорацияФормы"), Элементы.РабочаяОбласть);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
		НовыйЭлемент.Картинка = БиблиотекаКартинок.Документ;

	КонецЕсли;
	
КонецПроцедуры
```

# 3-2-3
# Тип Поле Формы
[Оглавление](#Oglavlenie)
### Добавить Поле Надписи
```
&НаКлиенте
Процедура ДобавитьПолеНадписи(Команда)
	ДобавитьПолеНадписиНаСервере();
КонецПроцедуры

Процедура ДобавитьПолеНадписиНаСервере()
	
	ИмяРеквизита = "Пользователь";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;	
	НовыйЭлемент.Гиперссылка = Истина;	
	
	// заполнение реквизита формы
	ЭтотОбъект[ИмяРеквизита] = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

```

### Добавить Поле Ввода
```
&НаКлиенте
Процедура ДобавитьПолеВвода(Команда)
	ДобавитьПолеВводаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеВводаНаСервере()

	ИмяРеквизита = "Контрагент";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
	
	ИмяРеквизита = "ДатаДоставки";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Дата доставки";
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьРеквизитФормы(Форма, ИмяРеквизита) Экспорт
    
    КлючУникальности   = Новый УникальныйИдентификатор;
	СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
	
	ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Форма);
    
    Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
    
КонецФункции

```

### Добавить поле флажка
```
&НаКлиенте
Процедура ДобавитьПолеФлажка(Команда)
	ДобавитьПолеФлажкаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеФлажкаНаСервере()
	
	ИмяРеквизита = "ЕстьДисконтнаяКарта";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Булево"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.Заголовок = "Есть дисконтная карта";
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
	
КонецПроцедуры
```

### Добавить Поле Переключателя

```
&НаКлиенте
Процедура ДобавитьПолеПереключателя(Команда)
	ДобавитьПолеПереключателяНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеПереключателяНаСервере()
	
	ИмяРеквизита = "СпособДоставки";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы
	ДобавляемыеРеквизиты = Новый Массив;
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Строка"));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 2. Добавление элемента формы
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.Заголовок = "Способ доставки";
	НовыйЭлемент.ПутьКДанным = ИмяРеквизита;   
	НовыйЭлемент.ВидПереключателя = ВидПереключателя.Тумблер;
	НовыйЭлемент.СписокВыбора.Добавить("Самовывоз");
	НовыйЭлемент.СписокВыбора.Добавить("Курьер");
	НовыйЭлемент.СписокВыбора.Добавить("ПунктВыдачи", "Пункт выдачи");
	
КонецПроцедуры
```
# 3-2-4
# Тип Таблица Формы
[Оглавление](#Oglavlenie)
### Добавить Таблицу
```
&НаКлиенте
Процедура ДобавитьТаблицу(Команда)
	ДобавитьТаблицуНаСервере();
КонецПроцедуры              

&НаСервере
Процедура ДобавитьТаблицуНаСервере()

	ИмяРеквизитаТаблицы = "ТаблицаТовары";
	
	Если ЕстьРеквизитФормы(ЭтотОбъект, ИмяРеквизитаТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление реквизита формы (таблица)
	ДобавляемыеРеквизиты = Новый Массив;
	РеквизитТаблица = Новый РеквизитФормы(ИмяРеквизитаТаблицы, Новый ОписаниеТипов("ТаблицаЗначений"));
	ДобавляемыеРеквизиты.Добавить(РеквизитТаблица);
	
	// 2. Добавление реквизитов формы (колонки)
	КолонкаТаблицы = Новый РеквизитФормы("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"), ИмяРеквизитаТаблицы);
	ДобавляемыеРеквизиты.Добавить(КолонкаТаблицы);

	КолонкаТаблицы = Новый РеквизитФормы("Количество", Новый ОписаниеТипов("Число"), ИмяРеквизитаТаблицы);
	ДобавляемыеРеквизиты.Добавить(КолонкаТаблицы);                                                        
	
	КолонкаТаблицы = Новый РеквизитФормы("Цена", Новый ОписаниеТипов("Число"), ИмяРеквизитаТаблицы);
	ДобавляемыеРеквизиты.Добавить(КолонкаТаблицы);                         
	
	КолонкаТаблицы = Новый РеквизитФормы("Сумма", Новый ОписаниеТипов("Число"), ИмяРеквизитаТаблицы);
	ДобавляемыеРеквизиты.Добавить(КолонкаТаблицы);
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// 3. Добавление элемента формы (таблица)
	ТаблицаФормы = Элементы.Добавить(ИмяРеквизитаТаблицы, Тип("ТаблицаФормы"), Элементы.РабочаяОбласть);
    ТаблицаФормы.ПутьКДанным = ИмяРеквизитаТаблицы;
	
	НовыйЭлемент = Элементы.Добавить(ИмяРеквизитаТаблицы + "Номенклатура", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.ПутьКДанным = ИмяРеквизитаТаблицы + ".Номенклатура";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Номенклатура";

	НовыйЭлемент = Элементы.Добавить(ИмяРеквизитаТаблицы + "Количество", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.ПутьКДанным = ИмяРеквизитаТаблицы + ".Количество";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Количество"; 
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПересчитатьСуммыВТекущейСтроке");

	НовыйЭлемент = Элементы.Добавить(ИмяРеквизитаТаблицы + "Цена", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.ПутьКДанным = ИмяРеквизитаТаблицы + ".Цена";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Цена";          
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПересчитатьСуммыВТекущейСтроке");

	НовыйЭлемент = Элементы.Добавить(ИмяРеквизитаТаблицы + "Сумма", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.ПутьКДанным = ИмяРеквизитаТаблицы + ".Сумма";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Сумма";


КонецПроцедуры // ДобавитьТаблицуНаСервере()

&НаСервереБезКонтекста
Функция ЕстьРеквизитФормы(Форма, ИмяРеквизита) Экспорт
    
    КлючУникальности   = Новый УникальныйИдентификатор;
	СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
	
	ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Форма);
    
    Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
    
КонецФункции

&НаКлиенте
Процедура ПересчитатьСуммыВТекущейСтроке()
	
	Если Элементы.Найти("ТаблицаТовары") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ТаблицаТовары.ТекущиеДанные;
	ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена;

КонецПроцедуры // ПересчитатьСуммыВТекущейСтроке()
```
# 3-2-5
# Тип Команда Формы
[Оглавление](#Oglavlenie)
### Добавить Кнопку

```
&НаКлиенте
Процедура ДобавитьКнопку(Команда)
	ДобавитьКнопкуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкуНаСервере()

	ИмяКоманды = "Подтвердить";
	Если Команды.Найти(ИмяКоманды) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Добавление команды формы         
	НоваяКоманда = Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Действие = "ПодтвердитьЗаказ";	
	
	// 2 Добавление элемента (кнопки) формы
	НовыйЭлемент = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Элементы.РабочаяОбласть);
	НовыйЭлемент.ИмяКоманды = ИмяКоманды;
	НовыйЭлемент.Заголовок = "Подтвердить";
	НовыйЭлемент.Картинка = БиблиотекаКартинок.ОформлениеЗнакФлажок;
	НовыйЭлемент.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПодтвердитьЗаказ(Команда)
	
	Ответ = Ждать ВопросАсинх("Оформить заказ?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Текст = "Заказ успешно оформлен";
	Иначе                                
		Текст = "Заказ отменен";
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
	
КонецПроцедуры // ПодтвердитьЗаказ()
```

# 3-2-6
# Тип Группа Формы
[Оглавление](#Oglavlenie)
### Добавить Обычную Группу
```
&НаКлиенте
Процедура ДобавитьОбычнуюГруппу(Команда)
	ДобавитьОбычнуюГруппуНаСервере();
	ПоказатьОповещениеПользователя("Группы успешно созданы: Шапка, ШапкаЛево, ШапкаПраво");
КонецПроцедуры

&НаСервере
Процедура ДобавитьОбычнуюГруппуНаСервере()
	
	Если ЭтотОбъект.Элементы.Найти("Шапка") <> Неопределено 
		ИЛИ ЭтотОбъект.Элементы.Найти("ШапкаЛево") <> Неопределено
		ИЛИ ЭтотОбъект.Элементы.Найти("ШапкаПраво") <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ГруппаШапка = ЭтотОбъект.Элементы.Добавить("Шапка", Тип("ГруппаФормы"), ЭтотОбъект.Элементы.РабочаяОбласть);
	ГруппаШапка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаШапка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаШапка.ОтображатьЗаголовок = Ложь; 
	ГруппаШапка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ГруппаШапкаЛево = ЭтотОбъект.Элементы.Добавить("ШапкаЛево", Тип("ГруппаФормы"), ГруппаШапка);
	ГруппаШапкаЛево.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаШапкаЛево.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаШапкаЛево.ОтображатьЗаголовок = Ложь; 
	ГруппаШапкаЛево.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;

	ГруппаШапкаПраво = ЭтотОбъект.Элементы.Добавить("ШапкаПраво", Тип("ГруппаФормы"), ГруппаШапка);
	ГруппаШапкаПраво.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаШапкаПраво.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаШапкаПраво.ОтображатьЗаголовок = Ложь; 
	ГруппаШапкаПраво.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
КонецПроцедуры
```

### Добавить Группу Страницы
```
&НаКлиенте
Процедура ДобавитьГруппуСтраницы(Команда)
	ДобавитьГруппуСтраницыНаСервере();
	ПоказатьОповещениеПользователя("Группы успешно созданы: Страницы, СтраницаТовары, СтраницаДополнительно");
КонецПроцедуры

&НаСервере
Процедура ДобавитьГруппуСтраницыНаСервере()
	
	Если ЭтотОбъект.Элементы.Найти("Страницы") <> Неопределено 
		ИЛИ ЭтотОбъект.Элементы.Найти("СтраницаТовары") <> Неопределено
		ИЛИ ЭтотОбъект.Элементы.Найти("СтраницаДополнительно") <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ГруппаСтраницы = ЭтотОбъект.Элементы.Добавить("Страницы", Тип("ГруппаФормы"), ЭтотОбъект.Элементы.РабочаяОбласть);
	ГруппаСтраницы.Вид = ВидГруппыФормы.Страницы;
	
	СтраницаТовары = ЭтотОбъект.Элементы.Добавить("СтраницаТовары", Тип("ГруппаФормы"), ГруппаСтраницы);
	СтраницаТовары.Вид = ВидГруппыФормы.Страница;
	СтраницаТовары.Заголовок = "Товары";

	СтраницаДополнительно = ЭтотОбъект.Элементы.Добавить("СтраницаДополнительно", Тип("ГруппаФормы"), ГруппаСтраницы);
	СтраницаДополнительно.Вид = ВидГруппыФормы.Страница;
	СтраницаДополнительно.Заголовок = "Дополнительно";
	
КонецПроцедуры

```
# 3-2-7
# Перемещение ЭлементовФормы и добавление всех элементов на форму
[Оглавление](#Oglavlenie)
### Добавить Все Элементы
```
&НаКлиенте
Процедура ДобавитьВсеЭлементы(Команда)
	ДобавитьВсеЭлементыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьВсеЭлементыНаСервере()
	
	ДобавитьДекорациюНадписьНаСервере();
	ДобавитьДекорациюКартинкуНаСервере();
	ДобавитьПолеНадписиНаСервере();
	ДобавитьПолеВводаНаСервере();
	ДобавитьПолеФлажкаНаСервере();
	ДобавитьПолеПереключателяНаСервере();
	ДобавитьТаблицуНаСервере();
	ДобавитьКнопкуНаСервере();
	ДобавитьОбычнуюГруппуНаСервере();
	ДобавитьГруппуСтраницыНаСервере();
	
КонецПроцедуры

```

### ПереместитьЭлементы
```
&НаКлиенте
Процедура ПереместитьЭлементы(Команда)
	ПереместитьЭлементыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПереместитьЭлементыНаСервере()
	
	Если Элементы.Найти("Шапка") = Неопределено 
		ИЛИ Элементы.Найти("ШапкаЛево") <> Неопределено
		ИЛИ Элементы.Найти("ШапкаПраво") <> Неопределено 
		ИЛИ Элементы.Найти("Страницы") <> Неопределено 
		ИЛИ Элементы.Найти("СтраницаТовары") <> Неопределено
		ИЛИ Элементы.Найти("СтраницаДополнительно") <> Неопределено Тогда

		ДобавитьВсеЭлементыНаСервере();
		
	КонецЕсли;                                                                         
	
	Элементы.Переместить(Элементы.Контрагент, Элементы.ШапкаЛево);
	Элементы.Переместить(Элементы.ЕстьДисконтнаяКарта, Элементы.ШапкаЛево);
	
	Элементы.Переместить(Элементы.СпособДоставки, Элементы.ШапкаПраво);
	Элементы.Переместить(Элементы.ДатаДоставки, Элементы.ШапкаПраво);
	
	Элементы.Переместить(Элементы.ТаблицаТовары, Элементы.СтраницаТовары);
	Элементы.Переместить(Элементы.Пользователь, Элементы.СтраницаДополнительно);
	
	Элементы.Переместить(Элементы.Подтвердить, Элементы.РабочаяОбласть);
	
КонецПроцедуры

```

# 4
# Реквизиты и параметры формы. Жизненный цикл управляемой формы. События формы
[Оглавление](#Oglavlenie)

# 4-1
# Реквизиты формы
[Оглавление](#Oglavlenie)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты.jpg)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты2.jpg)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты3.jpg)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты4.jpg)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты5.jpg)
![Реквизиты](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Реквизиты6.jpg)

# 4-2
# Параметры формы
[Оглавление](#Oglavlenie)
![Параметры](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Параметры1.jpg)
![Параметры](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Параметры2.jpg)
![Параметры](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Параметры3.jpg)

# 4-3
# Жизненный цикл
[Оглавление](#Oglavlenie)

![Жизненный цикл](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/События%20формы.jpg)
![Жизненный цикл](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Жизненный%20цикл%20формы%20при%20открытии.jpg)
![Жизненный цикл](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы/Жизненный%20цикл%20при%20записи%20объекта%20формы.jpg)



# 4-4
# Примеры
[Оглавление](#Oglavlenie)
# 4-4-1
# Пример отображение дополнительных реквизитов в виде полей формы
[Оглавление](#Oglavlenie)
### ЧТОБЫ НА ФОРМЕ ПОКАЗЫВАТЬ ДАННЫЕ ИЗ ТАБЛИЧНОЙ ЧАСТИ В ВИДЕ ОТДЕЛЬНЫХ ПОЛЕЙ НАДО

### Шаг первый 
+ нужно создать на форме элемента дополнительный реквизит с типом таблица значений:

+ Тип значения --> нужен что бы понимать что бы будем создавать поле ввода или поле флажка

+ Имя реквизита --> будем хранить имя программно создоваемого элемента 
+ ДОполнительный реквизит --> храним ссылку на план видов характеристик
+ значение --> храним значение ПВХ


![Программная работа с управляемой формой](/images/Реквизиты%20и%20параметры%20формы.%20Жизненный%20цикл%20управляемой%20формы.%20События%20формы//Первый%20шаг.png)

### Шаг второй
+ Нужно в событии "При чтении на сервере" нужно создавать реквизиты формы создать элементы формы и так как это событии возникает только для существующих объектов, то нам нужно еще и звполнять дополнитльные реквизиты

```
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьДополнительныеРеквизитыФормы(ТекущийОбъект);
	
КонецПроцедуры


&НаСервере
Процедура ОбновитьДополнительныеРеквизитыФормы(ТекущийОбъект = Неопределено)
	
	// удаление старых элементов и реквизитов формы
	УдаляемыеРеквизиты = Новый Массив;
	Для каждого ОписаниеРеквизита Из ОписаниеДополнительныхРеквизитов Цикл
		
		НайденныйЭлемент = Элементы.Найти(ОписаниеРеквизита.ИмяРеквизита);
		Если НайденныйЭлемент <> Неопределено Тогда
			Элементы.Удалить(НайденныйЭлемент);
		КонецЕсли;  
		
		УдаляемыеРеквизиты.Добавить(ОписаниеРеквизита.ИмяРеквизита);
	
	КонецЦикла;   
	ИзменитьРеквизиты(,УдаляемыеРеквизиты); 
	ОписаниеДополнительныхРеквизитов.Очистить();
	
	// создание новых элементов и реквизитов формы
	ГруппаПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Справочник_Номенклатура;
	ДобавляемыеРеквизиты = Новый Массив;
	
	Выборка = ПланыВидовХарактеристик.ДополнительныеРеквизиты.Выбрать(ГруппаПВХ);
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитСсылка = Выборка.Ссылка;
		
		Идентификатор = Новый УникальныйИдентификатор;
		ИмяРеквизита = "Реквизит_" + СтрЗаменить(Идентификатор, "-", "");
		
		РеквизитФормы = Новый РеквизитФормы(ИмяРеквизита, Выборка.ТипЗначения, , Выборка.Наименование);
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы); 
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ДополнительныйРеквизит", РеквизитСсылка);
		
		ЗначениеРеквизита = Неопределено;
		Если ТекущийОбъект <> Неопределено Тогда
			МассивСтрок = ТекущийОбъект.ДополнительныеРеквизиты.НайтиСтроки(ПараметрыОтбора); 
			Если МассивСтрок.Количество() > 0 Тогда
				ЗначениеРеквизита = МассивСтрок[0].Значение;			
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеРеквизита = ОписаниеДополнительныхРеквизитов.Добавить();
		ОписаниеРеквизита.ДополнительныйРеквизит = РеквизитСсылка;
		ОписаниеРеквизита.Значение = ЗначениеРеквизита;
		ОписаниеРеквизита.ТипЗначения = Выборка.ТипЗначения;
		ОписаниеРеквизита.ИмяРеквизита = ИмяРеквизита;
	
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);  
		
		ТипБулево = Новый ОписаниеТипов("Булево");
		ТипДополнительноеЗначение = Новый ОписаниеТипов("СправочникСсылка.ЗначенияДополнительныхРеквизитов");
		
		Для каждого ОписаниеРеквизита Из ОписаниеДополнительныхРеквизитов Цикл
			
			Если ОписаниеРеквизита.ТипЗначения = ТипБулево Тогда
				ВидПоля = ВидПоляФормы.ПолеФлажка;
			Иначе                                 
				ВидПоля = ВидПоляФормы.ПолеВвода;
			КонецЕсли;
			
			ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита] = ОписаниеРеквизита.Значение;
			
			НовыйЭлемент = Элементы.Добавить(ОписаниеРеквизита.ИмяРеквизита, Тип("ПолеФормы"), Элементы.ГруппаДополнительныеРеквизиты);
			НовыйЭлемент.Вид = ВидПоля;
			НовыйЭлемент.ПутьКДанным = ОписаниеРеквизита.ИмяРеквизита; 
			
			Если ОписаниеРеквизита.ТипЗначения = ТипДополнительноеЗначение Тогда 
				
				ПараметрВыбора = Новый ПараметрВыбора("Отбор.Владелец", ОписаниеРеквизита.ДополнительныйРеквизит);
				МассивПараметровВыбора = Новый Массив;
				МассивПараметровВыбора.Добавить(ПараметрВыбора);
				
				НовыйЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
				
			КонецЕсли;     
			
			НовыйЭлемент.УстановитьДействие("ПриИзменени", "Подключаемый_ДополнительныеРеквизитыПриИзменении");
					
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры // ()


&НаКлиенте
Процедура Подключаемый_ДополнительныеРеквизитыПриИзменении(Элемент)

	Модифицированность = Истина;	

КонецПроцедуры // Подключаемый_ДополнительныеРеквизитыПриИзменении()
```

### Шаг третий
+ При создании новых объектов нам нам нужно тоже создавать новые реквизиты и поля формы, но так как эта процедура возникает и при открытии новых объектов, то нам надо проверять, новый это объект или нет и если это новый добавлять в него реквизиты и поля, делается это для того что бы не было ошибки связанной с дублей элементов формы
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбновитьДополнительныеРеквизитыФормы(); // выше код.
	КонецЕсли;
	
КонецПроцедуры
```

### Шаг четвертый
+ Нам нужно перенести из полей формы в табличную часть объекта данные которые мы заполнили

```
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для каждого ОписаниеРеквизита Из ОписаниеДополнительныхРеквизитов Цикл
		
		СтрокаТаблицы = ТекущийОбъект.ДополнительныеРеквизиты.Найти(ОписаниеРеквизита.ДополнительныйРеквизит, 
																				"ДополнительныйРеквизит");
		Если СтрокаТаблицы = Неопределено Тогда
			СтрокаТаблицы = ТекущийОбъект.ДополнительныеРеквизиты.Добавить();
			СтрокаТаблицы.ДополнительныйРеквизит = ОписаниеРеквизита.ДополнительныйРеквизит;
		КонецЕсли;
		
		СтрокаТаблицы.Значение = ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита];
	
	КонецЦикла;
	
КонецПроцедуры

```

### Шаг Пятый

+ Тут данные на форме обновляются данными объекта, это если программно в модуле объекта было выполнено модификация данных , то после записи будет выполнена синхнронизация дынных
```
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьДополнительныеРеквизитыФормы(ТекущийОбъект);
	
КонецПроцедуры

```

# 4-4-2
# Изменение свойств элементов при открытии формы(Не показвать ИНН/КПП для физ лица.)
[Оглавление](#Oglavlenie)
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизЛицо Тогда
		Элементы.ИНН.Видимость = Ложь;
		Элементы.КПП.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

```

# 4-4-3
# Отображение актуальных цен номенклатуры в форме
[Оглавление](#Oglavlenie)
+ Мы должны создать на уровене формы реквизит с типом "Таблица Значенй" наименование "Таблица цен" далее создаем колонки первая это "ВидЦен" с типом "СправочникиСсылка.ВидыЦен" и вторая "Цена" с типом "число"
+ Далее содаем создаем обычную группу с наименованием "ГруппаЦен"
+ Далее мы перемещаем реквизит "ТаблицаЦен" на форму
+ далее мы откулючаем у командой панели отключаем "Автозаполнение"
+ Далее для самой таблицы "ПоложениеСостоянияПросмотра" ставим в "нет"
+ Далее ставим для самой таблицы "ТолькоПросмотр" "Истина"
+ Далее создаем кнопку, чтобы при нажатии обновлялись актуальные цены номенклатуры
И переносим ее на форму
```
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьАктуальныеЦены(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЦены(Команда)
	
	//Прочитать();
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ОбновитьАктуальныеЦены(Объект.Ссылка);
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Номенклатура не записана, поэтому получение цен невозможно.";
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры



&НаСервере
Процедура ОбновитьАктуальныеЦены(Номенклатура)

	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Номенклатура);
	АктуальныеЦены = РегистрыСведений.ЦеныНоменклатуры.СрезПоследних(, Отбор);
	
	ТаблицаЦен.Загрузить(АктуальныеЦены);

КонецПроцедуры // ОбновитьАктуальныеЦены()

```
# 5
# События элементов формы. Разбор типовых задач
[Оглавление](#Oglavlenie)

# 5-1
# События элементов форм

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/СобытияЭлементовФормы.jpg)
![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/СобытияЭлементовФормы2.jpg)


# 5-2
# Типовые задачи
[Оглавление](#Oglavlenie)

# 5-2-1
# Событие ПриИзменении (При изменении краткого наименования номенклатуры заполнять полное)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача1.jpg)

```
&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;	
	КонецЕсли;
	
КонецПроцедуры

```

# 5-2-2
# Событие ПриИзменении(При изменении номенклатуры втроке табл. части заполнять единицу измерения и ставку НДС)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача2.jpg)

```
&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;

	ЗначенияРеквизитов = РеквизитыНоменклатуры(ТекущаяСтрока.Номенклатура);
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ЗначенияРеквизитов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыНоменклатуры(Номенклатура)

	Единица = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
	СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
	
	Если НЕ Номенклатура.Пустая() Тогда
		Единица = Номенклатура.ЕдиницаИзмерения;
		СтавкаНДС = Номенклатура.СтавкаНДС;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЕдиницаИзмерения", Единица);
	СтруктураДанных.Вставить("СтавкаНДС", СтавкаНДС);
	
	Возврат СтруктураДанных;
	
КонецФункции // РеквизитыНоменклатуры()
```

# 5-2-3
# Событие ПриИзменении(Пересчитать суммы при изменении значений от которых они зависят)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача3.jpg)


```
---------------------------------------------------------------------------------
// Это делается в документе "заказ клиента", форма документа.
---------------------------------------------------------------------------------
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;	
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТЧ(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;	
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТЧ(ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;	
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДСИВсего(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;	
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДСИВсего(ТекущаяСтрока);

КонецПроцедуры

---------------------------------------------------------------------------------
// Это делается в общем модуле "ОбработкаТабличныхЧастейКлиент"
---------------------------------------------------------------------------------

Процедура РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти) Экспорт

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
	РассчитатьСуммуНДСИВсего(СтрокаТабличнойЧасти);

КонецПроцедуры

Процедура РассчитатьСуммуНДСИВсего(СтрокаТабличнойЧасти) Экспорт

	СуммаВключаетНДС = Истина;
	СтавкаНДС = ОбработкаТабличныхЧастейПовтИсп.ЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.Сумма / ((СтавкаНДС + 100) / 100);
	
	СтрокаТабличнойЧасти.СуммаВсего = СтрокаТабличнойЧасти.Сумма;

КонецПроцедуры
```

# 5-2-4
# Событие НачалоВыбора (Предопределить открываемую форму для выбора номенклатуры в заказе поставщику)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача4.jpg)

```
&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбораЗакупки", ,Элемент);
	
КонецПроцедуры

```

# 5-2-5
# Событие НачалоВыбора (Реализовать возможность выбора файла через диалог)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача5.jpg)

+ Тут настаиваем в элемете "Кнопка выбора" --> "ДА"
```
&НаКлиенте
Асинх Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл с контрагентами";
	
	МассивФайлов = Ждать Диалог.ВыбратьАсинх();
	Если МассивФайлов <> Неопределено Тогда
		Объект.ПутьКФайлу = МассивФайлов[0];		
	КонецЕсли;
	
КонецПроцедуры
```

# 5-2-6
# Событие НачалоВыбора (Реализовать возможность подбора товаров в заказ клиента через стандартуню форму выбора)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача6.jpg)

+ создаем кнопку "подбор" и пишем код
+ Не забываем про свойство "изменяет сохраняемые данные" ставим галочку
```
&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.Товары);
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", ВыбранноеЗначение);

	МассивСтрок = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
	
	Если МассивСтрок.Количество() = 0 Тогда
		СтрокаТаблицы = Объект.Товары.Добавить();
		СтрокаТаблицы.Номенклатура = ВыбранноеЗначение; 
		ЗначенияРеквизитов = РеквизитыНоменклатуры(ВыбранноеЗначение);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЗначенияРеквизитов);
	Иначе
		СтрокаТаблицы = МассивСтрок[0];
	КонецЕсли;
	
	СтрокаТаблицы.Количество = СтрокаТаблицы.Количество + 1; 
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТЧ(СтрокаТаблицы);

КонецПроцедуры

----------------------------------------------------------------------------------
Тут обращаемся к общему модулю, а в общем модуле такой код:
----------------------------------------------------------------------------------

Процедура РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти) Экспорт

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
	РассчитатьСуммуНДСИВсего(СтрокаТабличнойЧасти);

КонецПроцедуры

Процедура РассчитатьСуммуНДСИВсего(СтрокаТабличнойЧасти) Экспорт

	СуммаВключаетНДС = Истина;
	СтавкаНДС = ОбработкаТабличныхЧастейПовтИсп.ЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.Сумма / ((СтавкаНДС + 100) / 100);
	
	СтрокаТабличнойЧасти.СуммаВсего = СтрокаТабличнойЧасти.Сумма;

КонецПроцедуры

```


# 5-2-7
# Событие НачалоВыбора (При выборе товара в списке добавлять его в отдельную таблицу "Корзина")
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача7.jpg)
![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/7%20задача.png)
```
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗапрашиватьКоличество Тогда
		ЗапроситьКоличествоТовара(ВыбраннаяСтрока);
	Иначе
		ДобавитьТоварВКорзину(ВыбраннаяСтрока, 1);
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Асинх Процедура ЗапроситьКоличествоТовара(Номенклатура)
	
	Количество = Ждать ВвестиЧислоАсинх(1, "Введите количество товара", 10, 3);
	Если Количество <> Неопределено Тогда
		ДобавитьТоварВКорзину(Номенклатура, Количество)		
	КонецЕсли;

КонецПроцедуры // ЗапроситьКоличествоТовара()

&НаКлиенте
Процедура ДобавитьТоварВКорзину(Номенклатура, Количество)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);

	МассивСтрок = Корзина.НайтиСтроки(ПараметрыОтбора);
	
	Если МассивСтрок.Количество() = 0 Тогда
		СтрокаТаблицы = Корзина.Добавить();
		СтрокаТаблицы.Номенклатура = Номенклатура; 
	Иначе
		СтрокаТаблицы = МассивСтрок[0];
	КонецЕсли;
	
	СтрокаТаблицы.Количество = СтрокаТаблицы.Количество + Количество; 

КонецПроцедуры // ДобавитьТоварВКорзину()

```


# 5-2-8
# Событие ПриАктивизицииСтроки (При активизации строки в списке номенклаутры показывать остатки и цены текущего товара)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача8.jpg)
![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/Задача%208.png)


```
&НаКлиенте
Процедура ПослеАктивизацииСтрокиВСписке()

	Номенклатура = Элементы.Список.ТекущаяСтрока;
	Если Номенклатура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОстаткиИЦены = ОстаткиИЦеныНоменклатуры(Номенклатура);
	
	ТаблицаОстатки.Очистить();
	ТаблицаЦены.Очистить(); 
	
	Для каждого СтрокаОстатка Из ОстаткиИЦены.Остатки Цикл
		НоваяСтрока = ТаблицаОстатки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатка);
	КонецЦикла;

	Для каждого СтрокаЦены Из ОстаткиИЦены.Цены Цикл
		НоваяСтрока = ТаблицаЦены.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЦены);
	КонецЦикла;

КонецПроцедуры // ()


&НаСервереБезКонтекста
Функция ОстаткиИЦеныНоменклатуры(Номенклатура)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Номенклатура", Номенклатура);
	
	ТаблицаОстатков = РегистрыНакопления.ТоварыНаСкладах.Остатки(,СтруктураОтбора);
	
	МассивОстатков = Новый Массив;
	Для каждого СтрокаОстатка Из ТаблицаОстатков Цикл
		СтруктураОстатка = Новый Структура("Склад, Количество");
		ЗаполнитьЗначенияСвойств(СтруктураОстатка, СтрокаОстатка);
		МассивОстатков.Добавить(СтруктураОстатка);
	КонецЦикла;
	
	ТаблицаЦен = РегистрыСведений.ЦеныНоменклатуры.СрезПоследних(, СтруктураОтбора);
	МассивЦен = Новый Массив;
	Для каждого СтрокаЦен Из ТаблицаЦен Цикл
		СтруктураЦены = Новый Структура("ВидЦен, Цена");
		ЗаполнитьЗначенияСвойств(СтруктураЦены, СтрокаЦен);
		МассивЦен.Добавить(СтруктураЦены);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Остатки", МассивОстатков);
	Результат.Вставить("Цены", МассивЦен);
	
	Возврат Результат;
	
КонецФункции

```

# 5-2-9
# Событие ПриНачалеРедактирования (При добавлении новой строки в заказ клиента заполнять "Количество" значением пл умполчанию)
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/События%20элементов%20формы.%20Разбор%20типовых%20задач/ТиповаяЗадача9.jpg)

```
&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
	
КонецПроцедуры

```

# 5-2-10
# Событие ПриИзменении (Заполнение цены продажи в заказе клиента)
[Оглавление](#Oglavlenie)
+ Нужно автоматизировать заполнение цен в документе "Заказ клиента".Добавьте в документ новый реквизит "Вид цен":
+ В настройках программы нужно дать возможность выбрать вид цен по умолчанию.  Для хранения вида цен по умолчанию создайте константу. Реализуйте заполнение вида цен для новых заказов значением по умолчанию.
После этого реализуйте автоматическое заполнение цен в табличной части документа.

+ При изменении номенклатуры в строке табличной части нужно заполнять цену актуальной ценой на дату документа по виду цен, выбранному в документе:
+ Цены нужно получать из регистра сведений "Цены номенклатуры".
Также после заполнения цены нужно выполнять пересчет сумм (Сумма, Сумма НДС и Сумма всего).

+ Обратите внимание, что товары могут добавляться через подбор (форму, которая открывается по кнопке "Подбор"), в этом случае цены также должны заполняться автоматически:


```
-----------------------------------------------------------
Это делается в модуле объекта.
-----------------------------------------------------------
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ВидЦен = Константы.ВидыЦенПоУмолчанию.Получить();
КонецПроцедуры


-----------------------------------------------------------
Это делается форме констант для того если изменили в форме ВидЦен, то он менялся бы на форме "заказа клиента"
-----------------------------------------------------------
&НаКлиенте
Процедура ВидыЦенПоУмолчаниюПриИзменении(Элемент)
	Оповестить("ИзменилсяВидЦенПоУмолчанию",НаборКонстант.ВидыЦенПоУмолчанию ,ЭтотОбъект);
КонецПроцедуры

-----------------------------------------------------------
Это делается форме документа (событие в самой форме) для того если изменили в форме ВидЦен, то он менялся бы на форме "заказа клиента"
-----------------------------------------------------------
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменилсяВидЦенПоУмолчанию" Тогда 
		
		Объект.ВидЦен = Параметр;
		
	КонецЕсли;
КонецПроцедуры


```


```
-------------------------------------------------------------------
Это делается в форме документа "Заказ клиента" для того чтобы получать актуальные цены при изменнеи "ВидаЦен"
-------------------------------------------------------------------
&НаКлиенте
Процедура ВидЦенПриИзменении(Элемент)
	ОбновитьЦеныПоВидуЦен();
	
	Для каждого Стр Из Объект.Товары Цикл
		
		ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТЧ(Стр);
		
	КонецЦикла;
КонецПроцедуры 


&НаСервере
Процедура ОбновитьЦеныПоВидуЦен()
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	текВидЦен = ТекОбъект.ВидЦен;
	
	Для каждого Стр  Из текОбъект.Товары  Цикл
		
		Стр.Цена = ПолучитьАктуальнуюЦену(Стр.Номенклатура, текВидЦен);		
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НасервереБезКонтекста
Функция ПолучитьАктуальнуюЦену(Номенклатура, ВидЦен = Неопределено)

	новЦена = 0;
	
	Отбор = Новый Структура("ВидЦен, Номенклатура", ВидЦен, Номенклатура);
	ТабЦены = РегистрыСведений.ЦеныНоменклатуры.СрезПоследних(, Отбор);
	
	Если ТабЦены.Количество() > 0 Тогда
		новЦена = ТабЦены[0].Цена;
	КонецЕсли;
	
	Возврат новЦена;

КонецФункции // ПолучитьАктуальнуюЦену()

```


# 6
# Работа с файлами и картинками
[Оглавление](#Oglavlenie)
# 6-1
# ДиалогВыбораФайла
# Обратного вызова
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДиалогВыбораФайла.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДиалогВыбораФайла2.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДиалогВыбораФайла3.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДиалогВыбораФайла4.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДиалогВыбораФайла5.jpg)


# ДиалогВыбораФайла Асинхронные 
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Асинх Процедура ВыбратьФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.Фильтр = "Текстовый документ (*.txt)|*.txt|Табличный документ (*.mxl)|*.mxl";
	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();

	Если ВыбранныеФайлы <> Неопределено Тогда
		Результат = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьКаталог(Команда)

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Выберите каталог";

	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();

	Если ВыбранныеФайлы <> Неопределено Тогда
		Результат = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры                          

&НаКлиенте
Асинх Процедура СохранитьФайл(Команда)

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = "Список контрагентов";
	Диалог.Расширение = "xlsx";
	Диалог.Заголовок = "Сохранение файла";

	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();

	Если ВыбранныеФайлы <> Неопределено Тогда
		Результат = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры       
```


# 6-2
# Двоичные данные
[Оглавление](#Oglavlenie)


![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДвоичныеДанные.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДвоичныеДанные2.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДвоичныеДанные3.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДвоичныеДанные4.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ДвоичныеДанные5.jpg)
```
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПутьКФайлу = "";
	ДанныеФайла = "";
	
	ВыбратьФайлИПолучитьДвоичныеДанные();
	
КонецПроцедуры 

&НаКлиенте
Асинх Процедура ВыбратьФайлИПолучитьДвоичныеДанные()

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();

	Если ВыбранныеФайлы <> Неопределено Тогда
		ПутьКФайлу = ВыбранныеФайлы[0];
	КонецЕсли;   
	
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		ДанныеФайла = Новый ДвоичныеДанные(ПутьКФайлу);
	КонецЕсли;

КонецПроцедуры // ВыбратьФайлИПолучитьДвоичныеДанные()

```
# 6-2-1
# Временное хранилище
[Оглавление](#Oglavlenie)

![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ВременноеХранилище.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/ВременноеХранилищеМетоды.jpg)

```
&НаКлиенте
Процедура ВременноеХранилище_Поместить(Команда)
	
	Если НЕ ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Возврат;
	КонецЕсли;  
	
	ДанныеФайла = Новый ДвоичныеДанные(ПутьКФайлу);
	
	// данные из временного хранилища будут удалены при серверном вызове
	//АдресДанных = ПоместитьВоВременноеХранилище(ДанныеФайла);
	
	// данные из временного хранилища будут удалены после закрытия формы
	АдресДанных = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);

	// данные из временного хранилища будут удалены после завершения сеанса
	//УИД = Новый УникальныйИдентификатор;
	//АдресДанных = ПоместитьВоВременноеХранилище(ДанныеФайла, УИД);

КонецПроцедуры

&НаКлиенте
Процедура ВременноеХранилище_Получить(Команда)
	
	СерверныйВызов();
	
	Если ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		ДанныеФайлаИзХранилища = ПолучитьИзВременногоХранилища(АдресДанных);
	КонецЕсли;
	
КонецПроцедуры                

&НаКлиенте
Процедура СоздатьНовыйФайл(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		
		ДанныеФайлаИзХранилища = ПолучитьИзВременногоХранилища(АдресДанных);
		ДанныеФайлаИзХранилища.Записать("C:\temp\Копия картинки.jpg");

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Создан новый файл";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

```

# 6-3
# Решение задачи 

[Оглавление](#Oglavlenie)

+ Как в номенклатуре отобрадать изображение и хранить файлы связааные с этой номенклатурой.
### Шаг первый

1. Надо создать справочник "НоменклатураПрисоедененныеФайлы"
1. Длина кода в ноль 
1. Длина наименования 150
1. Создаем реквизиты
1. создаем нашу форму элемента и форму списка
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Структура%20хранения%20данных.jpg)

### Шаг второй 
+ Заходим в НоменклатураПрисоедененныеФайлы --> комады и отключаем (Галочка) "Использовать стандартные команды". Это позволит нам не показыать этот справочник в интерфейсе

+ Далее создаем команду чтобы она отображалась в номенклатуре
	1. Создаем конманду в  "НоменклатураПрисоедененныеФайлы"
	2. Наименовпние "Файлы"
	3. Группа --> Это где будет размещаться конмада
	4. ТипПараметрыКоманды --> Это откуда будет вызываться эта команда( "СправочникСсылкаНоменклатура")
+ Далее пишем код в команде для того чтобы был отбор файла по номенклатуре
+ В ПараметрКоманды находится, то что мы сделали в 4 пункте 
```
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВладелецФайла", ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура("Отбор", ПараметрыОтбора);
	ОткрытьФорму("Справочник.НоменклатураПрисоединенныеФайлы.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

```
### Шаг третий
+ Создаем регитср сведений, там будут хранится двоичные данные фалйа
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Регитср%20сведений.jpg)

+ На вкадке "команды"  отключаем (Галочка) "Использовать стандартные команды". Это позволит нам не показыать этот справочник в интерфейсе

### Шаг четвертый

+ нам надо при кнопке создать "Открыть диалог выбора файла", далее программно мы создадим элемент в справочнике и запишем его , после чего список обновим

+ для этого в форме списка "НоменклатураПрисоедененныеФайлы" в свойстах таблицы формы есть событие "ПередНачаломДобавление" в этом собитии пишем код

+ Чтобы в "ПараметрыФормы" вставить ВладелецФайла
	1. Нам нужно создать на урофне формы списка "НоменклатураПрисоединенныеФайлы" реквизит "ВладелецФайла" с типом Номенклатура, туда будет помещаться "ВладелецФайла"
```
&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;
	ДобавитьФайл();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьФайл()

	// 1. Показать диалог выбора файла с помещением файла на сервер
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	ПараметрыДиалога.Заголовок = "Выберите файл";
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога, УникальныйИдентификатор);
	
	Если ОписаниеФайла <> Неопределено Тогда
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ВладелецФайла", ВладелецФайла);
		ПараметрыФайла.Вставить("Адрес", ОписаниеФайла.Адрес);
		ПараметрыФайла.Вставить("Имя", ОписаниеФайла.СсылкаНаФайл.Имя);
		ПараметрыФайла.Вставить("Расширение", ОписаниеФайла.СсылкаНаФайл.Расширение);
		ПараметрыФайла.Вставить("Размер", ОписаниеФайла.СсылкаНаФайл.Размер());
		
		ПрисоединенныйФайл = ДобавитьФайлСервер(ПараметрыФайла);   
		ПоказатьОповещениеПользователя("Файл успешно добавлен: " + ПрисоединенныйФайл);
		Элементы.Список.Обновить();
			
	КонецЕсли;

КонецПроцедуры // ДобавитьФайл()

&НаСервереБезКонтекста
Функция ДобавитьФайлСервер(СтруктураПараметров)
	
	Возврат РаботаСФайлами.ДобавитьФайл(СтруктураПараметров);

КонецФункции // ДобавитьФайлСервер()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Отбор.Свойство("ВладелецФайла", ВладелецФайла);
	
	Если НЕ ЗначениеЗаполнено(ВладелецФайла) Тогда
		ВызватьИсключение "Список присоединенных файлов можно посмотреть
							|только в форме объекта-владельца";
	КонецЕсли;

КонецПроцедуры
```
+ Общий модуль РаботаСФайлами
```
Функция ДобавитьФайл(СтруктураПараметров) Экспорт
	
	// получаем двоичные данные файла из временного хранилища
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураПараметров.Адрес);
	
	// создаем элемент справочника НоменклатураПрисоединенныеФайлы
	НовыйФайл = Справочники.НоменклатураПрисоединенныеФайлы.СоздатьЭлемент();
	НовыйФайл.Наименование = СтруктураПараметров.Имя;
	НовыйФайл.ВладелецФайла = СтруктураПараметров.ВладелецФайла;
	НовыйФайл.Расширение = СтруктураПараметров.Расширение;
	НовыйФайл.Размер = СтруктураПараметров.Размер;
	НовыйФайл.Автор = ПараметрыСеанса.ТекущийПользователь;
	НовыйФайл.ДатаСоздания = ТекущаяДата();
	
	НовыйФайл.Записать();
	
	// записываем двоичные данные файла в регистр сведений
	ФайлСсылка = НовыйФайл.Ссылка;
	
	МенеджерЗаписи = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Файл = ФайлСсылка;
	МенеджерЗаписи.ДвоичныеДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	МенеджерЗаписи.Записать(Истина);
	
	Возврат ФайлСсылка;
	
КонецФункции // ДобавитьФайлСервер()
```

### Шаг пятый
+ Далее создаем команду "Просмотреть", чтобы можно было смотреть файлы которые добавили
```
&НаКлиенте
Асинх Процедура Просмотреть(Команда)
	
	ФайлСсылка = Элементы.Список.ТекущаяСтрока;
	Если ФайлСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = ДанныеФайлаДляОткрытия(ФайлСсылка);
	
	ПутьКФайлуКлиент = ПолучитьИмяВременногоФайла(ДанныеФайла.Расширение);
	
	ОписаниеФайла = Ждать ПолучитьФайлССервераАсинх(ДанныеФайла.АдресДанных, ПутьКФайлуКлиент);
	
	Попытка
		ЗапуститьПриложениеАсинх(ПутьКФайлуКлиент);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось открыть файл по причине: " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение.Сообщить();	                                               	
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ДанныеФайлаДляОткрытия(ФайлСсылка)
	
	Возврат РаботаСФайлами.ДанныеФайлаДляОткрытия(ФайлСсылка);
	
КонецФункции // ДанныеФайлаДляОткрытия()


```

+ Общий модуль Работа с файлами
```
Функция ДанныеФайлаДляОткрытия(ФайлСсылка) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Файл", ФайлСсылка);
	
	ЗначенияРесурсов = РегистрыСведений.ДвоичныеДанныеФайлов.Получить(ПараметрыОтбора);
	
	ДвоичныеДанные = ЗначенияРесурсов.ДвоичныеДанныеФайла.Получить();
	
	АдресДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
	Расширение = ФайлОбъект.Расширение;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("АдресДанных", АдресДанных);
	СтруктураДанных.Вставить("Расширение", Расширение);
	
	Возврат СтруктураДанных;

КонецФункции 

```

### Шаг шестой
+ Нам надо показать каринку на форрме
+ Для этого нужно 

![Картинка](/images/Работа%20с%20файлами%20и%20картинками/Картинка%20на%20форме.jpg)
![Картинка](/images/Работа%20с%20файлами%20и%20картинками/Картинка%20на%20форме2.jpg)

+ Для этого устанавливаем свойсто гиперссылка для картинки
![Картинка](/images/Работа%20с%20файлами%20и%20картинками/Картинка%20на%20форме3.jpg)

+ Описываем событие "нажатие"
```
&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Если Объект.Ссылка.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Для выбора изображения нужно записать текущий элемент";
		Сообщение.Сообщить();	                                                  
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если ПустаяСтрока(АдресКартинки) Тогда
		ДобавитьКартинку();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьКартинку(ПутьКФайлу = Неопределено)
	
	Если ПутьКФайлу = Неопределено Тогда
	
		// 1. Показать диалог выбора файла с помещением файла на сервер
		ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
		ПараметрыДиалога.Заголовок = "Выберите файл";
		ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png; *.bmp";
		
		ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога, УникальныйИдентификатор);

	Иначе	
		ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПутьКФайлу, УникальныйИдентификатор);
	КонецЕсли;
	
	Если ОписаниеФайла <> Неопределено Тогда
		
		АдресКартинки = ОписаниеФайла.Адрес;
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ВладелецФайла", Объект.Ссылка);
		ПараметрыФайла.Вставить("Адрес", ОписаниеФайла.Адрес);
		ПараметрыФайла.Вставить("Имя", ОписаниеФайла.СсылкаНаФайл.Имя);
		ПараметрыФайла.Вставить("Расширение", ОписаниеФайла.СсылкаНаФайл.Расширение);
		ПараметрыФайла.Вставить("Размер", ОписаниеФайла.СсылкаНаФайл.Размер());
		
		Объект.ФайлКартинки = ДобавитьФайлСервер(ПараметрыФайла);   
					
	КонецЕсли;

КонецПроцедуры // ДобавитьКартинку()

&НаСервереБезКонтекста
Функция ДобавитьФайлСервер(СтруктураПараметров)
	
	Возврат РаботаСФайлами.ДобавитьФайл(СтруктураПараметров);

КонецФункции // ДобавитьФайлСервер()


```
+ Общий модуль "Работа с файлами"
```
Функция ДобавитьФайл(СтруктураПараметров) Экспорт
	
	// получаем двоичные данные файла из временного хранилища
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураПараметров.Адрес);
	
	// создаем элемент справочника НоменклатураПрисоединенныеФайлы
	НовыйФайл = Справочники.НоменклатураПрисоединенныеФайлы.СоздатьЭлемент();
	НовыйФайл.Наименование = СтруктураПараметров.Имя;
	НовыйФайл.ВладелецФайла = СтруктураПараметров.ВладелецФайла;
	НовыйФайл.Расширение = СтруктураПараметров.Расширение;
	НовыйФайл.Размер = СтруктураПараметров.Размер;
	НовыйФайл.Автор = ПараметрыСеанса.ТекущийПользователь;
	НовыйФайл.ДатаСоздания = ТекущаяДата();
	
	НовыйФайл.Записать();
	
	// записываем двоичные данные файла в регистр сведений
	ФайлСсылка = НовыйФайл.Ссылка;
	
	МенеджерЗаписи = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Файл = ФайлСсылка;
	МенеджерЗаписи.ДвоичныеДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	МенеджерЗаписи.Записать(Истина);
	
	Возврат ФайлСсылка;
	
КонецФункции // ДобавитьФайлСервер()


Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	
	Если ЗначениеЗаполнено(Объект.ФайлКартинки) Тогда
		ДанныеКартинки = РаботаСФайлами.ДанныеФайлаДляОткрытия(Объект.ФайлКартинки);
		АдресКартинки = ДанныеКартинки.АдресДанных;
	КонецЕсли;
	
КонецПроцедуры



```
+ Общий модуль
```
Функция ДанныеФайлаДляОткрытия(ФайлСсылка) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Файл", ФайлСсылка);
	
	ЗначенияРесурсов = РегистрыСведений.ДвоичныеДанныеФайлов.Получить(ПараметрыОтбора);
	
	ДвоичныеДанные = ЗначенияРесурсов.ДвоичныеДанныеФайла.Получить();
	
	АдресДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
	Расширение = ФайлОбъект.Расширение;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("АдресДанных", АдресДанных);
	СтруктураДанных.Вставить("Расширение", Расширение);
	
	Возврат СтруктураДанных;

КонецФункции 


```

### Шаг седьмой

Нужно очищать картнику по нопке ПКМ --> Очистить
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Картинка%20на%20форме5.jpg)

1. Создаем конмаду "удалить картинку"
2. помещаем эту команду в контекстное меню поля "Адрес Картинки" 

```
&НаКлиенте
Процедура УдалитьКартинку(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлКартинки) Тогда
		Возврат;
	КонецЕсли;
	
	ПометитьНаУдалениеПрисоединенныйФайл(Объект.ФайлКартинки);
	Объект.ФайлКартинки = Неопределено;
	АдресКартинки = "";
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеПрисоединенныйФайл(ФайлСсылка)
	
	ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
	ФайлОбъект.УстановитьПометкуУдаления(Истина);

КонецПроцедуры // ПометитьНаУдалениеПрисоединенныйФайл()
```

### Шаг Восьмой перетаскивание


![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Перетаскивание%20файлов1.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Перетаскивание%20файлов2.jpg)
![Программная работа с управляемой формой](/images/Работа%20с%20файлами%20и%20картинками/Перетаскивание%20файлов3.jpg)

```
&НаКлиенте
Процедура АдресКартинкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)

	Если ПараметрыПеретаскивания = Неопределено 
		ИЛИ ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("СсылкаНаФайл") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
		
	ДопустимыеРасширения = Новый Массив;
	ДопустимыеРасширения.Добавить(".jpg");
	ДопустимыеРасширения.Добавить(".png");
	ДопустимыеРасширения.Добавить(".bmp");
	
	Если ДопустимыеРасширения.Найти(ПараметрыПеретаскивания.Значение.Расширение) = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;			
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СсылкаНаФайл = ПараметрыПеретаскивания.Значение;
	ПутьКФайлуКлиент = СсылкаНаФайл.Файл.ПолноеИмя;

	ДобавитьКартинку(ПутьКФайлуКлиент);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьКартинку(ПутьКФайлу = Неопределено)
	
	Если ПутьКФайлу = Неопределено Тогда
	
		// 1. Показать диалог выбора файла с помещением файла на сервер
		ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
		ПараметрыДиалога.Заголовок = "Выберите файл";
		ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png; *.bmp";
		
		ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога, УникальныйИдентификатор);

	Иначе	
		ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПутьКФайлу, УникальныйИдентификатор);
	КонецЕсли;
	
	Если ОписаниеФайла <> Неопределено Тогда
		
		АдресКартинки = ОписаниеФайла.Адрес;
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ВладелецФайла", Объект.Ссылка);
		ПараметрыФайла.Вставить("Адрес", ОписаниеФайла.Адрес);
		ПараметрыФайла.Вставить("Имя", ОписаниеФайла.СсылкаНаФайл.Имя);
		ПараметрыФайла.Вставить("Расширение", ОписаниеФайла.СсылкаНаФайл.Расширение);
		ПараметрыФайла.Вставить("Размер", ОписаниеФайла.СсылкаНаФайл.Размер());
		
		Объект.ФайлКартинки = ДобавитьФайлСервер(ПараметрыФайла);   
					
	КонецЕсли;

КонецПроцедуры // ДобавитьКартинку()

&НаСервереБезКонтекста
Функция ДобавитьФайлСервер(СтруктураПараметров)
	
	Возврат РаботаСФайлами.ДобавитьФайл(СтруктураПараметров);

КонецФункции // ДобавитьФайлСервер()


```

+ Общий модуль Работа м файлами
```
Функция ДобавитьФайл(СтруктураПараметров) Экспорт
	
	// получаем двоичные данные файла из временного хранилища
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураПараметров.Адрес);
	
	// создаем элемент справочника НоменклатураПрисоединенныеФайлы
	НовыйФайл = Справочники.НоменклатураПрисоединенныеФайлы.СоздатьЭлемент();
	НовыйФайл.Наименование = СтруктураПараметров.Имя;
	НовыйФайл.ВладелецФайла = СтруктураПараметров.ВладелецФайла;
	НовыйФайл.Расширение = СтруктураПараметров.Расширение;
	НовыйФайл.Размер = СтруктураПараметров.Размер;
	НовыйФайл.Автор = ПараметрыСеанса.ТекущийПользователь;
	НовыйФайл.ДатаСоздания = ТекущаяДата();
	
	НовыйФайл.Записать();
	
	// записываем двоичные данные файла в регистр сведений
	ФайлСсылка = НовыйФайл.Ссылка;
	
	МенеджерЗаписи = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Файл = ФайлСсылка;
	МенеджерЗаписи.ДвоичныеДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	МенеджерЗаписи.Записать(Истина);
	
	Возврат ФайлСсылка;
	
КонецФункции // ДобавитьФайлСервер()
```

