# Oglavlenie
1. [База данных. Способы чтения данных из базы. Язык запросов 1С.](#1)
	+ [Базы Данных](#1-1)
    + [Способ чтения дыннх из базы](#1-2)
	+ [ЯзыкЗапросов 1С. Конструктор запроса](#1-3)
	+ [ПростыеЗапросы Секции ВЫБРАТЬ,ИЗ](#1-4)

2. [Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](#2)
	+ [Представление](#2-1)
	+ [Сортировка](#2-2)
	+ [Ключевые слова(Первые, Различные, Разрешенные)(Раздел дополнительно)](#2-3)
	+ [Запросы с условием (Секция ГДЕ)](#2-4)
	+ [ГРуппировка данных. Секция СГРУППИРОВАТЬ ПО](#2-5)

3. [Виртульные таблицы](#3)
	+ [Реальные и Виртуальные таблицы](#3-1)
    + [Критерии отбора](#3-2)
	+ [РегистрСведений](#3-3)
	+ [Регистр накопления](#3-4)
	+ [Регистр бухгалтерии](#3-5)
	+ [РегистрРасчета](#3-6)
	+ [Задача](#3-7)
4. [Вложенные запросы. Временные таблицы и пакетные запросы](#4)
	+ [Вложенный запрос](#4-1)
	+ [Павкетные запросы](#4-2)
5. [Соединение и объединение таблиц в запросе](#5)
	+ [Способы соединения и объединения таблиц](#5-1)
	+ [Объединение](#5-2)
	+ [Соединение](#5-3)
6. [Программная работа с запросом](#6)
	+ [Простые запросы](#6-1)
	+ [Варинаты обхода результата запроса](#6-2)
	+ [Пакетный запрос](#6-3)
	+ [Менеджер временной таблицы](#6-4)
	+ [Предача данных в запросе (Страница таблица значенией в запросе)](#6-5)
	+ [Полчуение данных запроса из отладки](#6-6)
	+ [Презентация](#6-7)
7. [Примеры задач](#7)
	+ [Сформировать движения документов при проведении](#7-1)
	+ [Заполнение связанных реквизитов на форме](#7-2)
	+ [Изменение запроса динамического списка](#7-3)
	+ [Контроль остатков товаров при продаже](#7-4)
	+ [Заполнения табличной части документа](#7-5)
	+ [Загрузка данных из файла](#7-6)
	+ [Провести документы "Приходная накладная" за выбранный период по выбранному контрагенту (исключая помеченные на удаление)](#7-7)
	+ [Изменить статус Заказов клиентов, которые находятся в статусе, выбранном в поле "Текущий статус" на статус, который выбран в поле "Новый статус" (исключая помеченные на удаление)](#7-8)
	+ [По введенному штрихкоду получить информацию о номенклатуре: - Наименование - Артикул - Текущий остаток (в разрезе складов) - Текущие цены (в разрезе видов цен)](#7-9)
	+ [Заполнить таблицу контрагентов вместе с контактной информацией (Юридический адрес, Телефон, Email):](#7-10)
	+ [формировать диаграмму, отображающую сумму продаж по месяцам за выбранный период. Данные о продажах нужно получать из регистра накопления "Продажи".](#7-11)

	8. [Типичные причины неоптимальной работы запросов](#8)
	+ [Фильтрация виртуальных таблиц без использования параметров](#8-1)
	+ [Соединения с подзапросами](#8-2)
	+ [Соединения с виртуальными таблицами](#8-3)
	+ [Не проиндексированы поля временных таблиц](#8-4)
	+ [Несоответствие индексов и условий запроса](#8-5)
	+ [Использование логического ИЛИ в условиях](#8-6)
	+ [Применение функций к полям условий](#8-7)
	+ [Использование условий с НЕ](#8-8)
	+ [Получение данных через точку от полей составного типа](#8-9)
	+ [Условие на реквизиты поля через точку](#8-10)
	


# 1
# База данных. Способы чтения данных из базы. Язык запросов 1С.
[Оглавление](#Oglavlenie)
+ Выбор константных значений (без источника данных)
```
ВЫБРАТЬ
	10 КАК Число,
	"Иван" КАК Имя,
	ИСТИНА КАК ЕстьДолжность,
	ДАТАВРЕМЯ(2023, 12, 21) КАК ДатаНачалаРаботы
```
# 1-1
# Базы Данных
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных1.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных2.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных3.jpg)
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных4.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных5.jpg)
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных6.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных7.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных8.jpg)
# 1-2
# Способ чтения дыннх из базы
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных9.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных10.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных11.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных12.jpg)
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных13.jpg)
# 1-3
# ЯзыкЗапросов 1С. Конструктор запроса
[Оглавление](#Oglavlenie)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных14.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных15.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных16.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных17.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных18.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных19.jpg)
# 1-4 
# ПростыеЗапросы Секции ВЫБРАТЬ,ИЗ
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных20.jpg)
![БазыДанных](/images/База%20данных.%20Способы%20чтения%20данных%20из%20базы.%20Язык%20запросов%201С/БазыДанных21.jpg)

# 2
# Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С
[Оглавление](#Oglavlenie)
# 2-1 
# Представление
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Представление.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Представление2.jpg)
[Оглавление](#Oglavlenie)
1. Получение представления ссылки
```
ВЫБРАТЬ
	ПРЕДСТАВЛЕНИЕ(Номенклатура.Ссылка) КАК Представление2,
	Номенклатура.Представление КАК Представление,
	ПРЕДСТАВЛЕНИЕССЫЛКИ(Номенклатура.Ссылка) КАК Представлени3
ИЗ
	Справочник.Номенклатура КАК Номенклатура
```
2. Представление() и ПредставлениеСсылки()
+ Функция `Представление` отличается от `ПредставлениеСсылки` тем что функция представение получает из ссылочного типа и примитивного типа строку всегда, а функция `ПредставлениеСсылки` работает так , если мы передаем значение ссылочного типа, то мы получаем представление сыллки, а если мы передаем значение не ссылочного типа, то мы возрващается само значение(Есои передаем дату будет передаваться дата)
```
ВЫБРАТЬ
	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗаказКлиента.Дата) КАК Дата,
	ПРЕДСТАВЛЕНИЕ(ЗаказКлиента.Дата) КАК ДатаПредставление
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
```
[Оглавление](#Oglavlenie)
3. УИД товары для сайта
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул,
	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Номенклатура.Ссылка) КАК Ссылка
ИЗ
	Справочник.Номенклатура КАК Номенклатура
```

# 2-2
# Сортировка
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Упорядочить1.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Упорядочить2.jpg)

1. Список заказов клиентов
```
ВЫБРАТЬ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента

УПОРЯДОЧИТЬ ПО
	Дата УБЫВ,
	Номер
```
2. Сортировка по представлению
```
ВЫБРАТЬ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента

УПОРЯДОЧИТЬ ПО
	Ссылка
АВТОУПОРЯДОЧИВАНИ
```
[Оглавление](#Oglavlenie)
3. Сортировка по моменту времени
```
ВЫБРАТЬ
	РасходнаяНакладная.МоментВремени КАК МоментВремени,
	РасходнаяНакладная.Ссылка КАК Ссылка
ИЗ
	Документ.РасходнаяНакладная КАК РасходнаяНакладная

УПОРЯДОЧИТЬ ПО
	МоментВремени УБЫВ
```
[Оглавление](#Oglavlenie)
4. Товары с сортировкой по наименованию
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура

УПОРЯДОЧИТЬ ПО
	Наименование
```
[Оглавление](#Oglavlenie)
5. Заказы поставщикам с сортировкой
```
ВЫБРАТЬ
	ЗаказПоставщику.Номер КАК Номер,
	ЗаказПоставщику.Дата КАК Дата,
	ЗаказПоставщику.Ссылка КАК Ссылка
ИЗ
	Документ.ЗаказПоставщику КАК ЗаказПоставщику

УПОРЯДОЧИТЬ ПО
	Дата
```
[Оглавление](#Oglavlenie)
6. Заказы поставщикам с сортировкой по моменту времени
```
ВЫБРАТЬ
	ЗаказПоставщику.Ссылка КАК Ссылка
ИЗ
	Документ.ЗаказПоставщику КАК ЗаказПоставщику

УПОРЯДОЧИТЬ ПО
	ЗаказПоставщику.МоментВремени
```
[Оглавление](#Oglavlenie)
7. Статусы заказов клиентов
```
ВЫБРАТЬ
	СтатусыЗаказовКлиентов.Наименование КАК Наименование,
	СтатусыЗаказовКлиентов.Цвет КАК Цвет,
	СтатусыЗаказовКлиентов.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания
ИЗ
	Справочник.СтатусыЗаказовКлиентов КАК СтатусыЗаказовКлиентов

УПОРЯДОЧИТЬ ПО
	РеквизитДопУпорядочивания
```
[Оглавление](#Oglavlenie)
8. Товары заказов поставщикам по убыванию суммы
```
ВЫБРАТЬ
	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	ЗаказПоставщикуТовары.Количество КАК Количество,
	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	ЗаказПоставщикуТовары.Цена КАК Цена,
	ЗаказПоставщикуТовары.Сумма КАК Сумма,
	ЗаказПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	ЗаказПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	ЗаказПоставщикуТовары.СуммаВсего КАК СуммаВсего
ИЗ
	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары

УПОРЯДОЧИТЬ ПО
	СуммаВсего УБЫВ
```

# 2-3
# Ключевые слова(Первые, Различные, Разрешенные)(Раздел дополнительно)
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Дополнительно.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Дополнительно2.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Дополнительно3.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Дополнительно4.jpg)
1. ПЕРВЫЕ <количество>
```
ВЫБРАТЬ ПЕРВЫЕ 1
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента

УПОРЯДОЧИТЬ ПО
	ЗаказКлиента.МоментВремени УБЫВ
```

2. РАЗЛИЧНЫЕ
+ Галочка без поторяющихся приведет к тому что появится "Различные", значит плаформа будет отсекать все дублирующиеся значения
+ Дублирующиеся значения являются те у кторых совподают все значения в калонках
```
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.Договор КАК Договор
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
```
[Оглавление](#Oglavlenie)
3. РАЗРЕШЕННЫЕ
+ Ставим галочку когда рабоатем в типовых конфигруациях, т.к могут быть ограниченные права доступа и запрос не будет выполянтся, но если поставить разрешенные , то запрос выполнится с разрешенными данными пользователя
```
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Организация КАК Организация,
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
```

# 2-4
# Запросы с условием (Секция ГДЕ)
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Секция%20ГДЕ.jpg)

1. Выбрать все товары (без групп)
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул,
	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	НЕ Номенклатура.ЭтоГруппа
```
[Оглавление](#Oglavlenie)
2. Выбрать все группы из справочника "Номенклатура"
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.ЭтоГруппа
```
[Оглавление](#Oglavlenie)
3. Выбрать помеченные на удаление товары
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.ПометкаУдаления
```
[Оглавление](#Oglavlenie)
4. Найти товар по артикулу
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Артикул = &Артикул
```
5. Найти несколько товаров по их артикулам
[Оглавление](#Oglavlenie)
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Артикул В(&Артикул)
```
+ Найти несколько товаров по их артикулам
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Артикул В ("MM9C3", "xpad68256blkgl", "XR-65X90K")
```
6. Найти товар по наименованию
[Оглавление](#Oglavlenie)
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Артикул КАК Артикул,
	Номенклатура.Наименование КАК Наименование
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Наименование = &Наименование
```
[Оглавление](#Oglavlenie)
7. Найти товар по наименованию (по шаблону)
+ `Подобно` Если у нас стоит "%" это означает произвольное количество символов
+ Если стоит %"Apple"% - означает то будет искаться слово Apple
+ Если %"Apple" - То будет искаться наименование котрых заканчивается на слово "Apple"
+ Если "Apple"% - То будет искаться наименование котрых начинается на слово "Apple"
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул,
	Номенклатура.Ссылка КАК Ссылка
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Наименование ПОДОБНО "%Apple%"
```
+ Найти товар по наименованию (по шаблону)
```
ВЫБРАТЬ
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул,
	Номенклатура.Ссылка КАК Ссылка
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Наименование ПОДОБНО &ШаблонСтроки
```


8. Выбирать заказы за период
[Оглавление](#Oglavlenie)
+ `Между` Это условие "Больше или равно или меньше или равно" между двумя значенями
```
ВЫБРАТЬ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
```

[Оглавление](#Oglavlenie)
9. Выбрать товары, входящие в группу 
+ Если нам надо получать товар в группе и в подчиненных группах тоже, нам надо выбрать в условии "В ИЕРАРХИИ"
```
ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Родитель В ИЕРАРХИИ(&Группа)
	И НЕ Номенклатура.ЭтоГруппа

```
10. Выбрать всех физических лиц и ИП 
+ Если нам надо выбрать несколько значений для условия мы используем "В" 
[Оглавление](#Oglavlenie)
```
ВЫБРАТЬ
	Контрагенты.Ссылка КАК Ссылка,
	Контрагенты.Наименование КАК Наименование,
	Контрагенты.ИНН КАК ИНН,
	Контрагенты.КПП КАК КПП,
	Контрагенты.ВидКонтрагента КАК ВидКонтрагента
ИЗ
	Справочник.Контрагенты КАК Контрагенты
ГДЕ
	Контрагенты.ВидКонтрагента В(&ВидКонтрагента)
```
11. Выбрать договоры контрагента
```
ВЫБРАТЬ
	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	ДоговорыКонтрагентов.НомерДоговора КАК НомерДоговора,
	ДоговорыКонтрагентов.ДатаДоговора КАК ДатаДоговора
ИЗ
	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
ГДЕ
	ДоговорыКонтрагентов.Владелец = &Владелец
```
[Оглавление](#Oglavlenie)
12. Выбрать заказы с суммой, больше чем X
```
ВЫБРАТЬ ПЕРВЫЕ 1
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.СуммаДокумента >= &СуммаДокумента

УПОРЯДОЧИТЬ ПО
	СуммаДокумента УБЫВ
```
[Оглавление](#Oglavlenie)
13. Выбрать проведенные заказы поставщикам за период
```
ВЫБРАТЬ
	ЗаказПоставщику.Ссылка КАК Ссылка,
	ЗаказПоставщику.Контрагент КАК Контрагент,
	ЗаказПоставщику.Валюта КАК Валюта,
	ЗаказПоставщику.СуммаДокумента КАК СуммаДокумента
ИЗ
	Документ.ЗаказПоставщику КАК ЗаказПоставщику
ГДЕ
	ЗаказПоставщику.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ЗаказПоставщику.Проведен
```
[Оглавление](#Oglavlenie)
14. Выбрать товары документа
+ В документах когда выбираем табличную часть, то не надо забывать про то что тадо указыавть из какого документы(Ссылка) мы берем табличную часть
```
ВЫБРАТЬ
	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	ЗаказПоставщикуТовары.Количество КАК Количество,
	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	ЗаказПоставщикуТовары.Цена КАК Цена,
	ЗаказПоставщикуТовары.Сумма КАК Сумма,
	ЗаказПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	ЗаказПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	ЗаказПоставщикуТовары.СуммаВсего КАК СуммаВсего
ИЗ
	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
ГДЕ
	ЗаказПоставщикуТовары.Ссылка = &Ссылка
```
[Оглавление](#Oglavlenie)
15. Выбрать заказы поставщикам с конкретным товаров
```
ВЫБРАТЬ
	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
	ЗаказПоставщикуТовары.Цена КАК Цена,
	ЗаказПоставщикуТовары.Ссылка.Контрагент КАК Контрагент
ИЗ
	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
ГДЕ
	ЗаказПоставщикуТовары.Номенклатура = &Номенклатура
```
[Оглавление](#Oglavlenie)
16. Выбрать движения документа по регистру
```
ВЫБРАТЬ
	Продажи.Период КАК Период,
	Продажи.Регистратор КАК Регистратор,
	Продажи.НомерСтроки КАК НомерСтроки,
	Продажи.Активность КАК Активность,
	Продажи.Номенклатура КАК Номенклатура,
	Продажи.Контрагент КАК Контрагент,
	Продажи.Количество КАК Количество,
	Продажи.Сумма КАК Сумма
ИЗ
	РегистрНакопления.Продажи КАК Продажи
ГДЕ
	Продажи.Регистратор = &Регистратор
```
[Оглавление](#Oglavlenie)
17. Выбрать все невыполненные задачи конкретного автора
```
ВЫБРАТЬ
	ЗадачаИсполнителя.Номер КАК Номер,
	ЗадачаИсполнителя.Дата КАК Дата,
	ЗадачаИсполнителя.Наименование КАК Наименование,
	ЗадачаИсполнителя.СрокИсполнения КАК СрокИсполнения
ИЗ
	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
ГДЕ
	НЕ ЗадачаИсполнителя.Выполнена
	И ЗадачаИсполнителя.Автор = &Автор
```
[Оглавление](#Oglavlenie)
18. История изменения курса USD за период
```
ВЫБРАТЬ
	КурсыВалют.Период КАК Период,
	КурсыВалют.Курс КАК Курс,
	КурсыВалют.Кратность КАК Кратность
ИЗ
	РегистрСведений.КурсыВалют КАК КурсыВалют
ГДЕ
	КурсыВалют.Валюта = &Валюта
	И КурсыВалют.Период МЕЖДУ &ДатаНачала И &ДатаОкончания

УПОРЯДОЧИТЬ ПО
	Период
```
# 2-5
# ГРуппировка данных. Секция СГРУППИРОВАТЬ ПО
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Группировка.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Группировка2.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/Группировка3.jpg)
1. Товары из заказов клиентов
```
ВЫБРАТЬ
	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
	СРЕДНЕЕ(ЗаказКлиентаТовары.Цена) КАК ЦенаСредняя,
	МАКСИМУМ(ЗаказКлиентаТовары.Цена) КАК ЦенаМаксимальная,
	СРЕДНЕЕ(ЗаказКлиентаТовары.Количество * ЗаказКлиентаТовары.Цена) КАК Сумма
ИЗ
	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
ГДЕ
	ЗаказКлиентаТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания

СГРУППИРОВАТЬ ПО
	ЗаказКлиентаТовары.Номенклатура
```
[Оглавление](#Oglavlenie)
2. Цены товаров из документа Установка цен
```
ВЫБРАТЬ
	УстановкаЦенТовары.Номенклатура КАК Номенклатура,
	МАКСИМУМ(УстановкаЦенТовары.Цена) КАК Цена
ИЗ
	Документ.УстановкаЦен.Товары КАК УстановкаЦенТовары
ГДЕ
	УстановкаЦенТовары.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	УстановкаЦенТовары.Номенклатура
```
[Оглавление](#Oglavlenie)
3. Количество рабочих дней между двумя датами
```
ВЫБРАТЬ
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеПроизводственногоКалендаря.Дата) КАК Дата
ИЗ
	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
ГДЕ
	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	И ДанныеПроизводственногоКалендаря.ВидДня = &ВидДня
```
[Оглавление](#Oglavlenie)
4. Количество заказов в разрезе клиентов
```
ВЫБРАТЬ
	ЗаказКлиента.Контрагент КАК Контрагент,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказКлиента.Ссылка) КАК Ссылка
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.Проведен

СГРУППИРОВАТЬ ПО
	ЗаказКлиента.Контрагент

УПОРЯДОЧИТЬ ПО
	Ссылка УБЫВ
```
[Оглавление](#Oglavlenie)
5. Количество заказов по дням
```
ВЫБРАТЬ
	НАЧАЛОПЕРИОДА(ЗаказКлиента.Дата, ДЕНЬ) КАК Дата,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказКлиента.Ссылка) КАК Ссылка
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания

СГРУППИРОВАТЬ ПО
	НАЧАЛОПЕРИОДА(ЗаказКлиента.Дата, ДЕНЬ)

УПОРЯДОЧИТЬ ПО
	Дата
```
[Оглавление](#Oglavlenie)
6. Условие после группировки
```
ВЫБРАТЬ
	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
	СРЕДНЕЕ(ЗаказКлиентаТовары.Цена) КАК Цена
ИЗ
	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
ГДЕ
	ЗаказКлиентаТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания

СГРУППИРОВАТЬ ПО
	ЗаказКлиентаТовары.Номенклатура

ИМЕЮЩИЕ
	СУММА(ЗаказКлиентаТовары.Количество) > 3
```
[Оглавление](#Oglavlenie)
7. Товары с группировкой
```
ВЫБРАТЬ
	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
	ЗаказКлиентаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения
ИЗ
	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
ГДЕ
	ЗаказКлиентаТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания

СГРУППИРОВАТЬ ПО
	ЗаказКлиентаТовары.Номенклатура,
	ЗаказКлиентаТовары.ЕдиницаИзмерения
```
# 2-6 Запросы с итогами
[Оглавление](#Oglavlenie)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/СекцияИтогиПО.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/СекцияИтогиПО2.jpg)
![Представление ссылочных полей, сортировка, условия, группировка, итоги в запросах 1С](/images/Представление%20ссылочных%20полей,%20сортировка,%20условия,%20группировка,%20итоги%20в%20запросах%201С/СекцияИтогиПО3.jpg)

1. Товары с итогами по количеству в заказах
```
ВЫБРАТЬ
	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
	ЗаказКлиентаТовары.Количество КАК Количество
ИЗ
	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
ИТОГИ
	СУММА(Количество)
ПО
	Номенклатура
```
[Оглавление](#Oglavlenie)
2. Количество заказов по контрагенту + заказы
```
ВЫБРАТЬ
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.Ссылка КАК Ссылка
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.Проведен

УПОРЯДОЧИТЬ ПО
	Ссылка УБЫВ
ИТОГИ
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Ссылка)
ПО
	ОБЩИЕ,
	Контрагент
```
[Оглавление](#Oglavlenie)
3. Продажи в разрезе товаров и контрагентов с итогами по товарам
```
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.Контрагент КАК Контрагент,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот
ИЗ
	РегистрНакопления.Продажи.Обороты КАК ПродажиОбороты
ИТОГИ
	СУММА(КоличествоОборот),
	СУММА(СуммаОборот)
ПО
	Номенклатура
```
[Оглавление](#Oglavlenie)
4. История изменения курса USD за период с итогами
```
ВЫБРАТЬ
	КурсыВалют.Период КАК Период,
	КурсыВалют.Валюта КАК Валюта,
	КурсыВалют.Курс КАК Курс
ИЗ
	РегистрСведений.КурсыВалют КАК КурсыВалют
ГДЕ
	КурсыВалют.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	И КурсыВалют.Валюта = &Валюта
ИТОГИ
	МАКСИМУМ(Курс)
ПО
	ОБЩИЕ
```
[Оглавление](#Oglavlenie)
5. Запрос с итогами без итогового поля
```
ВЫБРАТЬ
	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	ЗаказКлиентаТовары.Количество КАК Количество,
	ЗаказКлиентаТовары.Цена КАК Цена,
	ЗаказКлиентаТовары.Сумма КАК Сумма,
	ЗаказКлиентаТовары.СуммаВсего КАК СуммаВсего
ИЗ
	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
ИТОГИ
	СУММА(СуммаВсего)
ПО
	ОБЩИЕ
```

# 3
# Виртульные таблицы
[Оглавление](#Oglavlenie)
+ Остаки получаем на дату не включая её
+ В Виртуальных таблицах условие надо настраивать в параметрах
+ Виртуальные таблицы не хранятся в базах данных

# 3-1
# Реальные и Виртуальные таблицы
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РельныеИВиртуальныеТаблицы.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РельныеИВиртуальныеТаблицы2.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РельныеИВиртуальныеТаблицы3.jpg)

# 3-2
# Критерии отбора
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/КритерииОтбора.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/КритерииОтбора1.jpg)

# 3-3
# РегистрСведений
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрСведений.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрСведений2.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрСведений3.jpg)

# 3-4
# Регистр накопления
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрНакопления.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрНакопления2.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрНакопления3.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрНакопления4.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрНакопления5.jpg)

# 3-5
# Регистр бухгалтерии
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии2.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии3.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии4.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии5.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрБухгалтерии6.jpg)


# 3-6
# РегистрРасчета
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрРасчета.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрРасчета2.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрРасчета3.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрРасчета4.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/РегистрРасчета5.jpg)

# 3-7
# Задача
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/Задача.jpg)
![Виртуальные таблицы](/images/Виртульаные%20таблицы/Задача2.jpg)

# 4 
# Вложенные запросы. Временные таблицы и пакетные запросы
[Оглавление](#Oglavlenie)
# 4-1 
# Вложенный запрос
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ВложенныеЗапросы.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ВложенныеЗапросы2.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ВложенныеЗапросы3.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ВложенныеЗапросы4.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ВложенныеЗапросы5.jpg)

# 4-2
# Павкетные запросы
[Оглавление](#Oglavlenie)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос2.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос3.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос4.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос5.jpg)
![Виртуальные таблицы](/images/Вложенные%20запросы.%20Временные%20таблицы%20и%20пакетные%20запросы/ПакетныйЗапрос6.jpg)

# 5
# Соединение и объединение таблиц в запросе
[Оглавление](#Oglavlenie)

 Файл к лекциям `ЗапросыСоединения`
# 5-1
# Способы соединения и объединения таблиц
[Оглавление](#Oglavlenie)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Способы%20получения%20данных.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Способы%20получения%20данных2.jpg)


# 5-2
# Объединение
[Оглавление](#Oglavlenie)

![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Объединение.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Объединение2.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Объединение3.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Объединение4.jpg)

# 5-3
# Соединение
[Оглавление](#Oglavlenie)

![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение2.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение3.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение4.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение5.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение6.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение7.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение8.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение9.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение10.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение11.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение12.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение13.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение14.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение15.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение16.jpg)
![Соединение и объединение таблиц в запросе](/images/Соединение%20и%20объединение%20таблиц%20в%20запросе/Соединение17.jpg)

# 6 
# Программная работа с запросом
[Оглавление](#Oglavlenie)

# 6-1
# Простые запросы
[Оглавление](#Oglavlenie)
+ Простой запрос без параметров
```
&НаКлиенте
Процедура ЗаполнитьТовары(Команда)
	ЗаполнитьТоварыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыНаСервере()
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.Наименование КАК Наименование,
		|	Номенклатура.Артикул КАК Артикул,
		|	Номенклатура.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ Номенклатура.ЭтоГруппа
		|	И НЕ Номенклатура.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
		
КонецПроцедуры
```

+ Запрос с параметром (С отбором)
```
&НаКлиенте
Процедура ЗаполнитьТоварыСОтбором(Команда)
	ЗаполнитьТоварыСОтборомНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыСОтборомНаСервере()
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.Наименование КАК Наименование,
		|	Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ Номенклатура.ЭтоГруппа
		|	И НЕ Номенклатура.ПометкаУдаления
		|	И Номенклатура.Ссылка В ИЕРАРХИИ(&Группа)";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);		
	КонецЦикла;
	
КонецПроцедуры
```
+ Запрос с несколькими параметрами
```
НаКлиенте
Процедура ЗаполнитьОстатки(Команда)
	ЗаполнитьОстаткиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиНаСервере()
	
	Объект.Остатки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаОстатков, Склад = &Склад) КАК ТоварыНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("ДатаОстатков", ТекущаяДата());
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Остатки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
```

# 6-2
# Варинаты обхода результата запроса
[Оглавление](#Oglavlenie)
+ Заполнить продажи таблица
```
&НаСервере
Процедура ЗаполнитьПродажиТаблицаНаСервере()
	
	Объект.Продажи.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.Контрагент КАК Контрагент,
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.КоличествоОборот КАК Количество,
		|	ПродажиОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ПродажиОбороты";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// вариант 1: через выборку
	//Выборка = РезультатЗапроса.Выбрать();
	//
	//Пока Выборка.Следующий() Цикл
	//	НоваяСтрока = Объект.Продажи.Добавить();
	//	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	//КонецЦикла;
	
	// вариант 2: через выгрузку в таблицу значений и последующую загрузку
	ТаблицаПродаж = РезультатЗапроса.Выгрузить();
	Объект.Продажи.Загрузить(ТаблицаПродаж);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПродажиТаблица(Команда)
	ЗаполнитьПродажиТаблицаНаСервере();
КонецПроцедуры
```

+ Заполнить дерево продажи
```

&НаКлиенте
Процедура ЗаполнитьДеревоПродажи(Команда)
	ЗаполнитьДеревоПродажиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПродажиНаСервере()
	
	Объект.Продажи.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.Контрагент КАК Контрагент,
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.КоличествоОборот КАК Количество,
		|	ПродажиОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ПродажиОбороты
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма)
		|ПО
		|	Контрагент";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();

	// вариант 1: через выборку
	//ДеревоДанных = РеквизитФормыВЗначение("ДеревоПродажи");
	//ВыборкаКонтрагенты = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//
	//// обход результата запроса по группировкам
	//Пока ВыборкаКонтрагенты.Следующий() Цикл
	//	
	//	НоваяСтрока = ДеревоДанных.Строки.Добавить();
	//	ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаКонтрагенты);
	//	
	//	// обход детальных записей (прямой)
	//	ВыборкаДетали = ВыборкаКонтрагенты.Выбрать();
	//	Пока ВыборкаДетали.Следующий() Цикл
	//		СтрокаДетали = НоваяСтрока.Строки.Добавить();         
	//		ЗаполнитьЗначенияСвойств(СтрокаДетали, ВыборкаДетали);
	//	КонецЦикла;
	//	
	//КонецЦикла;    
	//
	//ЗначениеВРеквизитФормы(ДеревоДанных, "ДеревоПродажи");
	
	// вариант 2: через выгрузку в дерево значений и загрузку его в нужную коллекцию
	ДеревоРезультат = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЗначениеВРеквизитФормы(ДеревоРезультат, "ДеревоПродажи");
		
КонецПроцедуры
```

+ Заполнить заказы со списком товары
```
&НаКлиенте
Процедура ЗаполнитьЗаказыСоСпискомТоваров(Команда)
	ЗаполнитьЗаказыСоСпискомТоваровНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаказыСоСпискомТоваровНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК Заказ,
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Контрагент КАК Контрагент,
		|	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
		|	ЗаказКлиента.Товары.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Количество КАК Количество,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма,
		|		СтавкаНДС КАК СтавкаНДС,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаВсего КАК СуммаВсего
		|	) КАК Товары
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(&МассивЗаказов)";
	
	Запрос.УстановитьПараметр("МассивЗаказов", СписокЗаказов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Заказы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		СписокТоваров = "";
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СписокТоваров = СписокТоваров + ?(СписокТоваров <> "", "; ", "") + ВыборкаТовары.Номенклатура;	
		КонецЦикла;
		
		НоваяСтрока.СписокНоменклатуры = СписокТоваров;
		
	КонецЦикла;
	
КонецПроцедуры
```

# 6-3
# Пакетный запрос
[Оглавление](#Oglavlenie)
+ Заполнить остатки и цены
```
&НаСервере
Процедура ЗаполнитьОстаткиИЦеныНаСервере()
	
	Объект.ОстаткиИЦены.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_Цены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.КоличествоОстаток КАК Количество,
		|	ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		|		ПО ВТ_Остатки.Номенклатура = ВТ_Цены.Номенклатура";
	
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	Запрос.УстановитьПараметр("Склад", СкладОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.ОстаткиИЦены.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткиИЦены(Команда)
	ЗаполнитьОстаткиИЦеныНаСервере();
КонецПроцедуры
``` 

+ Заполнить остатки и цены из двух таблиц
```
&НаКлиенте
Процедура ЗаполнитьОстаткиИЦеныИзДвухТаблиц(Команда)
	ЗаполнитьОстаткиИЦеныИзДвухТаблицНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиИЦеныИзДвухТаблицНаСервере()
	
	Объект.ОстаткиИЦены.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	Запрос.УстановитьПараметр("Склад", СкладОстатков);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();  
	
	РезультатОстатки = МассивРезультатов[0];
	РезультатЦены = МассивРезультатов[1];
	
	ТаблицаЦен = РезультатЦены.Выгрузить();
	
	ВыборкаОстатки = РезультатОстатки.Выбрать();
	
	Пока ВыборкаОстатки.Следующий() Цикл
		
		НоваяСтрока = Объект.ОстаткиИЦены.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаОстатки);
		
		СтрокаЦен = ТаблицаЦен.Найти(ВыборкаОстатки.Номенклатура, "Номенклатура");
		Если СтрокаЦен <> Неопределено Тогда
			НоваяСтрока.Цена = СтрокаЦен.Цена;		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры
```

+ Поиск данных в выборке
```
&НаСервере
Процедура ЗаполнитьДанныеШапкиИТабличнойЧастиНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК Заказ,
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Контрагент КАК Контрагент
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(&МассивЗаказов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Заказ,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказКлиентаТовары.Цена КАК Цена,
		|	ЗаказКлиентаТовары.Сумма КАК Сумма
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка В(&МассивЗаказов)";
	
	Запрос.УстановитьПараметр("МассивЗаказов", СписокЗаказов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатШапка = МассивРезультатов[0];
	РезультатТовары = МассивРезультатов[1]; 
	
	ВыборкаТовары = РезультатТовары.Выбрать();
	
	ВыборкаШапка = РезультатШапка.Выбрать();
	Пока ВыборкаШапка.Следующий() Цикл
		
		СтрокаШапки = Объект.ЗаказыШапка.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаШапки, ВыборкаШапка);
		
		Пока ВыборкаТовары.НайтиСледующий(СтрокаШапки.Заказ, "Заказ") Цикл
			СтрокаТовары = Объект.ЗаказыТовары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовары, ВыборкаТовары);
		КонецЦикла;
		
		ВыборкаТовары.Сбросить();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеШапкиИТабличнойЧасти(Команда)
	ЗаполнитьДанныеШапкиИТабличнойЧастиНаСервере();
КонецПроцедуры
```

# Менеджер временной таблицы
# 6-4
[Оглавление](#Oglavlenie)
+ Временная таблица существует пока выполняется запрос
+ Как узнать что хранится во временной таблице? В отладчике пишем
```
МенеджерВТ.Таблицы.Найти.("ВТ_Отстаки).ПолучитьДанные().Выгрузить()

Мы получаем таблицу значение которую можно просмореть что в ней
```
+ Код с продлением жизни временной таблицы
```
&НаКлиенте
Процедура МенеджерВТ(Команда)
	МенеджерВТНаСервере();
КонецПроцедуры

&НаСервере
Процедура МенеджерВТНаСервере()
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	// Запрос № 1: помещает данные во временную таблицу
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.Склад КАК Склад,
		|	ВТ_Остатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "" + Выборка.Номенклатура + ": " + Выборка.КоличествоОстаток;
		Сообщение.Сообщить();
	КонецЦикла;	
	
	ЗапросКВременнойТаблице(МенеджерВТ);
	
КонецПроцедуры  

&НаСервере
Процедура ЗапросКВременнойТаблице(МенеджерВТ)
	
	// Запрос № 2: выбирает данные из временной таблицы
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.Склад КАК Склад,
		|	ВТ_Остатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "" + Выборка.Номенклатура + ": " + Выборка.КоличествоОстаток;
		Сообщение.Сообщить();
	КонецЦикла;	

КонецПроцедуры // ()
```

# Предача данных в запросе (Страница таблица значенией в запросе)
# 6-5 
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Процедура ДанныеФайлаВЗапросе(Команда)
	ДанныеФайлаВЗапросеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДанныеФайлаВЗапросеНаСервере()
	
	//ТаблицаДанных = Объект.ДанныеФайла.Выгрузить();
	
	// таблица значений формируется в коде
	ТипСтрока = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(150));
	ТипЧисло = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15,3));
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Наименование", ТипСтрока);
	ТаблицаДанных.Колонки.Добавить("Количество", ТипЧисло);
	
	ЗаполнитьТаблицу(ТаблицаДанных);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДанных.Наименование КАК Наименование,
		|	ТаблицаДанных.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_ДанныеФайла
		|ИЗ
		|	&ТаблицаДанных КАК ТаблицаДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеФайла.Наименование КАК Наименование,
		|	ВТ_ДанныеФайла.Количество КАК Количество
		|ИЗ
		|	ВТ_ДанныеФайла КАК ВТ_ДанныеФайла";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Наименование + ": " + Выборка.Количество;
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицу(ТаблицаДанных)
	
	СтрокаТаблицы = ТаблицаДанных.Добавить();
	СтрокаТаблицы.Наименование = "Беззеркальный фотоаппарат Nikon Z5 Body";
	СтрокаТаблицы.Количество = 11;

	СтрокаТаблицы = ТаблицаДанных.Добавить();
	СтрокаТаблицы.Наименование = "Монитор";
	СтрокаТаблицы.Количество = 15;

КонецПроцедуры // ЗаполнитьТаблицу()
```

# 6-6
# Полчуение данных запроса из отладки
[Оглавление](#Oglavlenie)

![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами21.jpg)

# 6-7
# Презентация
[Оглавление](#Oglavlenie)

![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами.jpg)
![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами2.jpg)
![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами3.jpg)
![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами4.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами5.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами6.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами7.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами8.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами9.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами10.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами11.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами12.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами13.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами14.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами15.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами16.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами17.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами18.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами19.jpg)![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами20.jpg)
![Программаня работа с запросами](/images/Программная%20работа%20с%20запросами/Программная%20работа%20с%20запросами22.jpg)

# 7
# Примеры задач
[Оглавление](#Oglavlenie)
# 7-1
# Сформировать движения документов при проведении
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример.jpg)
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяТовары.Количество) КАК Количество,
		|	МАКСИМУМ(ПриходнаяНакладнаяТовары.Цена) КАК Цена,
		|	СУММА(ПриходнаяНакладнаяТовары.СуммаВсего) КАК Сумма,
		|	ПриходнаяНакладнаяТовары.Ссылка.Контрагент КАК Контрагент,
		|	ПриходнаяНакладнаяТовары.Ссылка.Склад КАК Склад,
		|	ПриходнаяНакладнаяТовары.Ссылка.Дата КАК Период,
		|	ПриходнаяНакладнаяТовары.Ссылка.Валюта КАК Валюта,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
		|ИЗ
		|	Документ.ПриходнаяНакладная.Товары КАК ПриходнаяНакладнаяТовары
		|ГДЕ
		|	ПриходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяТовары.Номенклатура,
		|	ПриходнаяНакладнаяТовары.Ссылка.Контрагент,
		|	ПриходнаяНакладнаяТовары.Ссылка.Склад,
		|	ПриходнаяНакладнаяТовары.Ссылка.Дата,
		|	ПриходнаяНакладнаяТовары.Ссылка.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходнаяНакладнаяУслуги.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяУслуги.Количество) КАК Количество,
		|	МАКСИМУМ(ПриходнаяНакладнаяУслуги.Цена) КАК Цена,
		|	СУММА(ПриходнаяНакладнаяУслуги.СуммаВсего) КАК Сумма,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Контрагент КАК Контрагент,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Склад КАК Склад,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Дата КАК Дата,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Валюта КАК Валюта
		|ИЗ
		|	Документ.ПриходнаяНакладная.Услуги КАК ПриходнаяНакладнаяУслуги
		|ГДЕ
		|	ПриходнаяНакладнаяУслуги.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяУслуги.Номенклатура,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Контрагент,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Склад,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Дата,
		|	ПриходнаяНакладнаяУслуги.Ссылка.Валюта";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатТовары = МассивРезультатов[0];
	РезультатУслуги = МассивРезультатов[1];
	
	Движения.ЦеныПоставщиков.Записывать = Истина;
    Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Закупки.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	// пример загрузки данных в набор записей регистра
	ТаблицаТоваров = РезультатТовары.Выгрузить();
	Движения.ЦеныПоставщиков.Загрузить(ТаблицаТоваров);
	Движения.ТоварыНаСкладах.Загрузить(ТаблицаТоваров);
	Движения.Закупки.Загрузить(ТаблицаТоваров);
	// конец примера
	
	ВыборкаТовары = РезультатТовары.Выбрать();
	СчетТовары = ПланыСчетов.Хозрасчетный.Товары;       
	СчетРасчетыСПоставщиками = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
	Пока ВыборкаТовары.Следующий() Цикл

		Движение = Движения.ЦеныПоставщиков.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаТовары); 
		
		Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаТовары); 

		Движение = Движения.Закупки.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаТовары); 
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = СчетТовары;
		Движение.СчетКт = СчетРасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Сумма = ВыборкаТовары.Сумма;
		Движение.КоличествоДт = ВыборкаТовары.Количество;
		Движение.СодержаниеПроводки = "Закупка товаров у поставщика";
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = ВыборкаТовары.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады] = ВыборкаТовары.Склад;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = ВыборкаТовары.Контрагент;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоговорыКонтрагентов] = Договор;
		
	КонецЦикла;
	
	ВыборкаУслуги = РезультатУслуги.Выбрать();
	Пока ВыборкаУслуги.Следующий() Цикл
		Движение = Движения.ЦеныПоставщиков.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаТовары);
	КонецЦикла;
	
	Если Услуги.Количество() > 0 Тогда
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы;
		Движение.СчетКт = СчетРасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Сумма = Услуги.Итог("СуммаВсего");
		Движение.СодержаниеПроводки = "Прочие затраты";
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Контрагент;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоговорыКонтрагентов] = Договор;
	
	КонецЕсли;
	
КонецПроцедуры
```

# 7-2
# Заполнение связанных реквизитов на форме
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример2.jpg)
```
&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", ВыбранноеЗначение);

	МассивСтрок = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
	
	Если МассивСтрок.Количество() = 0 Тогда
		СтрокаТаблицы = Объект.Товары.Добавить();
		СтрокаТаблицы.Номенклатура = ВыбранноеЗначение; 
		ЗначенияРеквизитов = РеквизитыНоменклатуры(ВыбранноеЗначение);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЗначенияРеквизитов);
	Иначе
		СтрокаТаблицы = МассивСтрок[0];
	КонецЕсли;
	
	СтрокаТаблицы.Количество = СтрокаТаблицы.Количество + 1; 
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТЧ(СтрокаТаблицы);

КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыНоменклатуры(Номенклатура)

	Единица = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
	СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
	
	Если НЕ Номенклатура.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Номенклатура.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();		
		Выборка.Следующий();
				
		Единица = Выборка.ЕдиницаИзмерения;
		СтавкаНДС = Выборка.СтавкаНДС;

	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЕдиницаИзмерения", Единица);
	СтруктураДанных.Вставить("СтавкаНДС", СтавкаНДС);
	
	Возврат СтруктураДанных;
	
КонецФункции // РеквизитыНоменклатуры()
```

# 7-3
# Изменение запроса динамического списка
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример3.jpg)

1. Идем в форму подбора товар в докуамент продажи
2. там находим динамический список "Список товаров"
3. Там добавляем таблицу регистр сведений "ЦеныНоменклатурыСрезПоследних" -- Поле Цена и делаем левое соединение к основной таблицы
4. Убираем галочку `обязательная` на "вкладке" `КомпоновкаДанных` c регистра сведений `ЦеныНоменклатурыСрезПослдених. Это делается для того, чтобы, если пользователь уберет видимость "Цены" на форме, то левое соединение выполняться не будет.
```
ВЫБРАТЬ
	СправочникНоменклатура.Ссылка КАК Ссылка,
	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
	СправочникНоменклатура.Родитель КАК Родитель,
	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	СправочникНоменклатура.Код КАК Код,
	СправочникНоменклатура.Наименование КАК Наименование,
	СправочникНоменклатура.Артикул КАК Артикул,
	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	СправочникНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	СправочникНоменклатура.Поставщик КАК Поставщик,
	СправочникНоменклатура.СпособПополнения КАК СпособПополнения,
	СправочникНоменклатура.Комментарий КАК Комментарий,
	СправочникНоменклатура.СтавкаНДС КАК СтавкаНДС,
	СправочникНоменклатура.ФайлКартинки КАК ФайлКартинки,
	СправочникНоменклатура.ДополнительныеРеквизиты.(
		Ссылка КАК Ссылка,
		НомерСтроки КАК НомерСтроки,
		ДополнительныйРеквизит КАК ДополнительныйРеквизит,
		Значение КАК Значение
	) КАК ДополнительныеРеквизиты,
	СправочникНоменклатура.Предопределенный КАК Предопределенный,
	СправочникНоменклатура.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
ИЗ
	Справочник.Номенклатура КАК СправочникНоменклатура
		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		ПО СправочникНоменклатура.Ссылка = ЦеныНоменклатурыСрезПоследних.Номенклатура}
```
# 7-4
# Контроль остатков товаров при продаже
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример4.jpg)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример5.jpg)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример6.jpg)

```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// 1. Формирование движений в регистрах
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Ссылка.Дата КАК Период,
		|	РасходнаяНакладнаяТовары.Ссылка.Организация КАК Организация,
		|	РасходнаяНакладнаяТовары.Ссылка.Контрагент КАК Контрагент,
		|	РасходнаяНакладнаяТовары.Ссылка.Договор КАК Договор,
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТовары.СуммаВсего) КАК Сумма,
		|	РасходнаяНакладнаяТовары.Ссылка.Склад КАК Склад
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Ссылка.Договор,
		|	РасходнаяНакладнаяТовары.Ссылка.Склад,
		|	РасходнаяНакладнаяТовары.Ссылка.Контрагент,
		|	РасходнаяНакладнаяТовары.Ссылка.Организация,
		|	РасходнаяНакладнаяТовары.Номенклатура,
		|	РасходнаяНакладнаяТовары.Ссылка.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Период КАК Период,
		|	ВТ_Товары.Организация КАК Организация,
		|	ВТ_Товары.Контрагент КАК Контрагент,
		|	ВТ_Товары.Договор КАК Договор,
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Сумма КАК Сумма,
		|	ВТ_Товары.Склад КАК Склад
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СчетРасчетыСПокупателями = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями; 
	СчетТовары = ПланыСчетов.Хозрасчетный.Товары;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Движение = Движения.Продажи.Добавить();     
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = СчетРасчетыСПокупателями;
		Движение.СчетКт = СчетТовары;
		Движение.Период = Выборка.Период;
		Движение.Организация = Выборка.Организация;
		Движение.Сумма = Выборка.Сумма;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.СодержаниеПроводки = "Закупка товаров у поставщика";
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Контрагент;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоговорыКонтрагентов] = Договор;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады] = Склад;

	КонецЦикла;
	
	// 2. Запись движений в базу данных
	Движения.Записать();
	
	// 3. Контроль остатков
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	-ТоварыНаСкладахОстатки.КоличествоОстаток КАК Дефицит,
		|	ПРЕДСТАВЛЕНИЕ(ТоварыНаСкладахОстатки.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					ВТ_Товары.Номенклатура КАК Номенклатура,
		|					ВТ_Товары.Склад КАК Склад
		|				ИЗ
		|					ВТ_Товары КАК ВТ_Товары)) КАК ТоварыНаСкладахОстатки
		|ГДЕ
		|	ТоварыНаСкладахОстатки.КоличествоОстаток < 0";
	
	ГраницаОстатков = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;	
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 ед.",
									Выборка.НоменклатураПредставление, Выборка.Дефицит);
			Сообщение.Сообщить();
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры
```

# 7-5
# Заполнения табличной части документа
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример7.jpg)
+ Пишем в форме документа и создаем группу, а в ней команды.
1. Сначала спрашиваем очистить табличную часть или нет и открываем форму и при закрытии формы есть "обработчик закрытия" туда помещается то что выбрали
2. Если резутльат закрытия неопределено то тогда возврат иначе вызывается функция где через запрос ищутся товары входящую в то что выбрали и после происходит возврат товаров , а после идет заполнение табличной части
```
#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗаполнитьПоГруппамНоменклатуры(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		Ответ = Ждать ВопросАсинх("Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;  
		Объект.Товары.Очистить();
	КонецЕсли;
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПослеВыбораГруппы", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбораГруппы", ПараметрыФормы, ЭтотОбъект,,,,ОбработчикЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьПоДокументамПоступления(Команда)

	Если Объект.Товары.Количество() > 0 Тогда
		Ответ = Ждать ВопросАсинх("Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;  
		Объект.Товары.Очистить();
	КонецЕсли;
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПослеВыбораДокументовЗакупки", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Документ.ПриходнаяНакладная.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,,ОбработчикЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораГруппы(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивТоваров = ТоварыВходящиеВГруппы(РезультатЗакрытия); 
	Для каждого Товар Из МассивТоваров Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.Номенклатура = Товар;
	КонецЦикла;

КонецПроцедуры   

&НаКлиенте
Процедура ПослеВыбораДокументовЗакупки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивТоваров = ТоварыДокументовЗакупки(РезультатЗакрытия); 
	Для каждого Товар Из МассивТоваров Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.Номенклатура = Товар;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ТоварыВходящиеВГруппы(МассивГрупп)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В ИЕРАРХИИ(&МассивГрупп)
		|	И НЕ Номенклатура.ПометкаУдаления
		|	И НЕ Номенклатура.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("МассивГрупп", МассивГрупп);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	МассивТоваров = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивТоваров.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат МассивТоваров;

КонецФункции // ТоварыВходящиеВГруппы()

&НаСервереБезКонтекста
Функция ТоварыДокументовЗакупки(МассивДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяТовары.Номенклатура КАК Номенклатура
		|ИЗ
		|	Документ.ПриходнаяНакладная.Товары КАК ПриходнаяНакладнаяТовары
		|ГДЕ
		|	ПриходнаяНакладнаяТовары.Ссылка В(&МассивДокументов)";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатЗапроса = Запрос.Выполнить();

	МассивТоваров = Новый Массив;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивТоваров.Добавить(Выборка.Номенклатура);
	КонецЦикла;      
	
	Возврат МассивТоваров;

КонецФункции

#КонецОбласти
```

# 7-6
# Загрузка данных из файла
[Оглавление](#Oglavlenie)
![ПримерыЗадач](/images/ПримерыЗадачсЗапросами/Пример8.jpg)
```
&НаКлиенте
Асинх Процедура ЗагрузитьИзФайла(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		Ответ = Ждать ВопросАсинх("Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Товары.Очистить();
	КонецЕсли;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите файл с товарами";
	ПараметрыДиалога.Фильтр = "Текстовый документ|*.txt";
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога, УникальныйИдентификатор);
	
	Если ОписаниеФайла <> Неопределено Тогда
		ОбработатьФайлНаСервере(ОписаниеФайла.Адрес, ОписаниеФайла.СсылкаНаФайл.Расширение);
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ОбработатьФайлНаСервере(АдресДанных, Расширение)
	
	ПутьКФайлуНаСервере = ПолучитьИмяВременногоФайла(Расширение);
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресДанных);
	ДанныеФайла.Записать(ПутьКФайлуНаСервере);
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПутьКФайлуНаСервере, КодировкаТекста.UTF8);
	
	НомерСтроки = 1;
	СтрокаДанных = Текст.ПрочитатьСтроку();
	
	ТипЧисло = Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,3));
	ТипСтрока = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Наименование", ТипСтрока);
	ТаблицаТоваров.Колонки.Добавить("Количество", ТипЧисло);
	ТаблицаТоваров.Колонки.Добавить("Цена", ТипЧисло);
	
	Пока СтрокаДанных <> Неопределено Цикл
		
		ЧастиСтроки = СтрРазделить(СтрокаДанных, ";");
		Если ЧастиСтроки.Количество() < 3 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Неверный формат файла в строке № " + НомерСтроки;
			Сообщение.Сообщить();
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		НоваяСтрока.Наименование = ЧастиСтроки[0];
		НоваяСтрока.Количество	 = ТипЧисло.ПривестиЗначение(ЧастиСтроки[1]);
		НоваяСтрока.Цена		 = ТипЧисло.ПривестиЗначение(ЧастиСтроки[2]);
		
		СтрокаДанных = Текст.ПрочитатьСтроку();
		НомерСтроки = НомерСтроки + 1;

	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Наименование КАК Наименование,
		|	ТаблицаТоваров.Количество КАК Количество,
		|	ТаблицаТоваров.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Наименование КАК Наименование,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Цена КАК Цена,
		|	ЕСТЬNULL(Номенклатура.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ЕСТЬNULL(Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(Номенклатура.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) КАК СтавкаНДС
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ВТ_Товары.Наименование = Номенклатура.Наименование
		|ИТОГИ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатура)
		|ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоНаименованиям = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НомерСтроки = 1;
	Пока ВыборкаПоНаименованиям.Следующий() Цикл
		
		Если ВыборкаПоНаименованиям.Номенклатура > 1 Тогда
			ТекстСообщения = СтрШаблон("Строка №%1: найдено несколько товаров с наименованием %2",
									НомерСтроки, ВыборкаПоНаименованиям.Наименование);
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();	
		КонецЕсли;
		
		Выборка = ВыборкаПоНаименованиям.Выбрать();
		Выборка.Следующий();
		
		Если Выборка.Номенклатура.Пустая() Тогда
			ТекстСообщения = СтрШаблон("Строка №%1: не удалось найти номенклатуру с наименованием %2",
			НомерСтроки, Выборка.Наименование);
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
		КонецЕсли;
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ОбработкаТабличныхЧастей.РассчитатьСуммыВСтрокеТЧ(НоваяСтрока);
				
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры
```


# 7-7
# Провести документы "Приходная накладная" за выбранный период по выбранному контрагенту (исключая помеченные на удаление)
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Процедура Задача1_ПровестиПриходныеНакладныеЗаПериод(Команда)
	
	Если Не ЗначениеЗаполнено(Период) Или Не ЗначениеЗаполнено(Контрагент) Тогда
		НеЗаполнен = ?(ЗначениеЗаполнено(Период), "Контрагент", "Период");
		Текст = СтрШаблон("Не заполнен: %1", НеЗаполнен);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Поле = НеЗаполнен;
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
	Иначе	
		МассивРезультатов = Задача1_ПровестиПриходныеНакладныеЗаПериодНаСервере(Период, Контрагент);
        Для каждого Элем Из МассивРезультатов Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Элем;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция Задача1_ПровестиПриходныеНакладныеЗаПериодНаСервере(Период, Контрагент)
	
	МассивРезультатов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладная.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
		|ГДЕ
		|	ПриходнаяНакладная.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И НЕ ПриходнаяНакладная.ПометкаУдаления
		|	И ПриходнаяНакладная.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		МассивРезультатов.Добавить("Не найденой ни одной приходной накладной!");
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ДокументОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				МассивРезультатов.Добавить((СтрШаблон("%1 ПРОВЕДЁН", ВыборкаДетальныеЗаписи.Ссылка)));
			Исключение
				МассивРезультатов.Добавить((СтрШаблон("Ошибка: %1 Не удалось провести %2", ВыборкаДетальныеЗаписи.Ссылка, ОписаниеОшибки())));
			КонецПопытки;	
		КонецЦикла;		
		
	КонецЕсли;
		
	Возврат МассивРезультатов;
	
КонецФункции
```

# 7-8
# Изменить статус Заказов клиентов, которые находятся в статусе, выбранном в поле "Текущий статус" на статус, который выбран в поле "Новый статус" (исключая помеченные на удаление)
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Процедура Задача2_ИзменитьСтатусЗаказовКлиентов(Команда)
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или Не ЗначениеЗаполнено(НовыйСтатус) Тогда
		НеЗаполнен = ?(ЗначениеЗаполнено(ТекущийСтатус), "НовыйСтатус", "ТекущийСтатус");
		Текст = СтрШаблон("Не заполнен: %1", НеЗаполнен);
		ПредупреждениеАсинх(Текст, 30, "Пустое значение!");
	ИначеЕсли ТекущийСтатус = НовыйСтатус Тогда
		ПредупреждениеАсинх(Текст, 30, "Новый статус должен отличаться от текущего статуса");
	Иначе
		МассивРезультатов = Задача2_ИзменитьСтатусЗаказовКлиентовНаСервере(ТекущийСтатус, НовыйСтатус);
		Для каждого Элем Из МассивРезультатов Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Элем;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры 


&НаСервереБезКонтекста
Функция Задача2_ИзменитьСтатусЗаказовКлиентовНаСервере(ТекущийСтатус, НовыйСтатус)
	МассивРезультатов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	НЕ ЗаказКлиента.ПометкаУдаления
	|	И ЗаказКлиента.Статус = &Статус";
	
	Запрос.УстановитьПараметр("Статус", ТекущийСтатус);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		МассивРезультатов.Добавить((СтрШаблон("Нет ни одного заказа в статусе ""%1""", ТекущийСтатус)))
		
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаказОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗаказОбъект.Статус = НовыйСтатус;
			
			Если ЗаказОбъект.Проведен Тогда
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Иначе
				РежимЗаписи = РежимЗаписиДокумента.Запись;
			КонецЕсли;
			
			Попытка
				ЗаказОбъект.Записать(РежимЗаписи);
				МассивРезультатов.Добавить((СтрШаблон("%1 Изменен статус на %2", Выборка.Ссылка, НовыйСтатус)))
			Исключение
				МассивРезультатов.Добавить((СтрШаблон("%1 не удалось записать изменения по причине: %2", Выборка.Ссылка, ОписаниеОшибки())));
			КонецПопытки;	
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивРезультатов;
	
КонецФункции

```

# 7-9
# По введенному штрихкоду получить информацию о номенклатуре: - Наименование - Артикул - Текущий остаток (в разрезе складов) - Текущие цены (в разрезе видов цен)
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Процедура Задача3_НайтиДанныеНоменклатурыПоШтрихкоду(Команда)
	ИнформацияОНоменклатуре = "";
	
	Если ПустаяСтрока(ШтрихКод) Тогда
		ПредупреждениеАсинх("Заполните штрихкод", 30, "Не заполнен штрихкод!");
	Иначе	
		Задача3_НайтиДанныеНоменклатурыПоШтрихкодуНаСервере(Штрихкод, ИнформацияОНоменклатуре);
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура Задача3_НайтиДанныеНоменклатурыПоШтрихкодуНаСервере(Штрихкод, ИнформацияОНоменклатуре)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Номенклатура.Номенклатура.Наименование КАК Наименование,
	|	ВТ_Номенклатура.Номенклатура.Артикул КАК Артикул
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура";
	
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИнформацияОНоменклатуре = СтрШаблон("По данному штрихкоду %1 информации нет", Штрихкод);
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + "Номенклатура : "+ Выборка.Наименование + Символы.ПС + "Артикул : " + Выборка.Артикул;  
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТ_Номенклатура.Номенклатура КАК Номенклатура
	|				ИЗ
	|					ВТ_Номенклатура КАК ВТ_Номенклатура)) КАК ТоварыНаСкладахОстатки
	|ИТОГИ ПО
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен КАК ВидЦен,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТ_Номенклатура.Номенклатура КАК Номенклатура
	|				ИЗ
	|					ВТ_Номенклатура КАК ВТ_Номенклатура)) КАК ЦеныНоменклатурыСрезПоследних
	|ИТОГИ ПО
	|	ВидЦен	";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаОстатки = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаЦены = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + Символы.ПС + "Остатки на складах: ";
	
	Пока ВыборкаОстатки.Следующий() Цикл
		ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + Символы.ПС+ Символы.Таб  + ВыборкаОстатки.Склад + " : ";
		
		ВыборкаТовары = ВыборкаОстатки.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			
			ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + ВыборкаТовары.Количество + " шт. ";
			
		КонецЦикла;
	КонецЦикла; 
	
	ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + Символы.ПС + "Вид цен: ";
	Пока ВыборкаЦены.Следующий() Цикл
		ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + Символы.ПС+ Символы.Таб  + ВыборкаЦены.ВидЦен + " : ";
		
		ВыборкаТовары = ВыборкаЦены.Выбрать(); 
		Пока ВыборкаТовары.Следующий() Цикл
			
			ИнформацияОНоменклатуре = ИнформацияОНоменклатуре + ВыборкаТовары.Цена + " руб. "
			
		КонецЦикла;
		
	КонецЦикла;	
КонецПроцедуры 

```

# 7-10
# Заполнить таблицу контрагентов вместе с контактной информацией (Юридический адрес, Телефон, Email):
[Оглавление](#Oglavlenie)
```
&НаКлиенте
Процедура Задача4_ЗаполнитьТаблицуКонтрагентов(Команда)
	Задача4_ЗаполнитьТаблицуКонтрагентовНаСервере();
КонецПроцедуры                                      

&НаСервере
Процедура Задача4_ЗаполнитьТаблицуКонтрагентовНаСервере()	

	ТаблицаКонтрагентов.Очистить();	

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента)
		|				ТОГДА КонтрагентыКонтактнаяИнформация.Представление
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК ЮрАдрес,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтрагента)
		|				ТОГДА КонтрагентыКонтактнаяИнформация.Представление
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК Email,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
		|				ТОГДА КонтрагентыКонтактнаяИнформация.Представление
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК Телефон
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|		ПО Контрагенты.Ссылка = КонтрагентыКонтактнаяИнформация.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Контрагенты.Наименование,
		|	Контрагенты.ИНН";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
	КонецЦикла;
КонецПроцедуры 	
```

# 7-11
# Сформировать диаграмму, отображающую сумму продаж по месяцам за выбранный период. Данные о продажах нужно получать из регистра накопления "Продажи".
[Оглавление](#Oglavlenie)
+ ВАЖНО: в диаграмме нужно отображать данные по всем месяцам периода, даже если в каком-то месяце продаж не было.

```
&НаКлиенте
Процедура Задача5_СформироватьДиаграмму(Команда)
	 Задача5_СформироватьДиаграммуНаСервере();
КонецПроцедуры

&НаСервере
Процедура Задача5_СформироватьДиаграммуНаСервере()

	  Диаграмма.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.Период КАК Период,
		|	ПродажиОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ПериодНачало, &ПериодКонец, Месяц, ) КАК ПродажиОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	Период ПЕРИОДАМИ(МЕСЯЦ, , )";
	
	Запрос.УстановитьПараметр("ПериодКонец", ПериодПродаж.ДатаОкончания);
	Запрос.УстановитьПараметр("ПериодНачало", ПериодПродаж.ДатаНачала);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	
	Диаграмма.Обновление = Ложь;

	Серия = Диаграмма.УстановитьСерию("Сумма");
	
	Пока Выборка.Следующий() Цикл
		Точка = Диаграмма.УстановитьТочку(Выборка.Период);
		Точка.Текст = Формат(Выборка.Период, "ДФ=dd.MM.yyyy");
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.Сумма);	
	КонецЦикла;
	
	  Диаграмма.Обновление = Истина;	

КонецПроцедуры 

```
# 8
# Типичные причины неоптимальной работы запросов
[Оглавление](#Oglavlenie)

![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины2.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины3.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины4.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины5.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины6.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины7.jpg)
![Типичные причины](/images/Типичные%20причины%20неоптимальной%20работы%20запросов/Типичные%20причины8.jpg)

# 8-1
# Фильтрация виртуальных таблиц без использования параметров
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.Склад КАК Склад,
	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
ГДЕ
	ТоварыНаСкладахОстатки.Номенклатура = &Номенклатура

-------------------------------------------------------------------
-------------------------------------------------------------------	
Правильно

ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.Склад КАК Склад,
	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(, Номенклатура = &Номенклатура) КАК ТоварыНаСкладахОстатки
```

# 8-2
# Соединения с подзапросами
[Оглавление](#Oglavlenie)
```
Неправильно


ВЫБРАТЬ
	Контрагенты.Ссылка КАК Ссылка,
	Контрагенты.Код КАК Код,
	Контрагенты.Наименование КАК Наименование,
	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
	Контрагенты.ИНН КАК ИНН
ИЗ
	Справочник.Контрагенты КАК Контрагенты
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			ДоговорыКонтрагентов.Ссылка КАК Ссылка,
			ДоговорыКонтрагентов.Владелец КАК Владелец,
			ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
			ДоговорыКонтрагентов.НомерДоговора КАК НомерДоговора,
			ДоговорыКонтрагентов.ДатаДоговора КАК ДатаДоговора
		ИЗ
			Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		ГДЕ
			ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.СПокупателем)) КАК ДоговорыСПокупателем
		ПО Контрагенты.Ссылка = ДоговорыСПокупателем.Владелец
ГДЕ
	ДоговорыСПокупателем.Ссылка ЕСТЬ NULL

----------------------------------------------------------------------------------
-------------------------------------------------------------------
Правильно

ВЫБРАТЬ
	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	ДоговорыКонтрагентов.Владелец КАК Владелец,
	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
	ДоговорыКонтрагентов.НомерДоговора КАК НомерДоговора,
	ДоговорыКонтрагентов.ДатаДоговора КАК ДатаДоговора
ПОМЕСТИТЬ ВТ_ДоговорыСПокупателем
ИЗ
	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
ГДЕ
	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.СПокупателем)
	И НЕ ДоговорыКонтрагентов.ПометкаУдаления

ИНДЕКСИРОВАТЬ ПО
	Владелец
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Контрагенты.Ссылка КАК Ссылка,
	Контрагенты.Код КАК Код,
	Контрагенты.Наименование КАК Наименование,
	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
	Контрагенты.ИНН КАК ИНН
ИЗ
	Справочник.Контрагенты КАК Контрагенты
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговорыСПокупателем КАК ДоговорыСПокупателем
		ПО Контрагенты.Ссылка = ДоговорыСПокупателем.Владелец
ГДЕ
	ДоговорыСПокупателем.Ссылка ЕСТЬ NULL
	И НЕ Контрагенты.ЭтоГруппа
```

# 8-3
# Соединения с виртуальными таблицами
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	СпрНоменклатура.Ссылка КАК Ссылка,
	СпрНоменклатура.Наименование КАК Наименование,
	СпрНоменклатура.Артикул КАК Артикул,
	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК Остаток
ИЗ
	Справочник.Номенклатура КАК СпрНоменклатура
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
		ПО СпрНоменклатура.Ссылка = ТоварыНаСкладахОстатки.Номенклатура
ГДЕ
	НЕ СпрНоменклатура.ЭтоГруппа
------------------------------------------------------------------------
-------------------------------------------------------------------
Правильно

ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
ПОМЕСТИТЬ ВТ_Остатки
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	СпрНоменклатура.Ссылка КАК Ссылка,
	СпрНоменклатура.Наименование КАК Наименование,
	СпрНоменклатура.Артикул КАК Артикул,
	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК Остаток
ИЗ
	ВТ_Остатки КАК ВТ_Остатки
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		ПО ВТ_Остатки.Номенклатура = СпрНоменклатура.Ссылка
```

# 8-4
# Не проиндексированы поля временных таблиц
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
ПОМЕСТИТЬ ВТ_Остатки
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
ПОМЕСТИТЬ ВТ_Цены
ИЗ
	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_Остатки.Номенклатура КАК Номенклатура,
	ВТ_Остатки.КоличествоОстаток КАК Остаток,
	ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена
ИЗ
	ВТ_Остатки КАК ВТ_Остатки
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		ПО ВТ_Остатки.Номенклатура = ВТ_Цены.Номенклатура

-------------------------------------------------------------------
-------------------------------------------------------------------
Правильно
ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
ПОМЕСТИТЬ ВТ_Остатки
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
ПОМЕСТИТЬ ВТ_Цены
ИЗ
	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_Остатки.Номенклатура КАК Номенклатура,
	ВТ_Остатки.КоличествоОстаток КАК Остаток,
	ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена
ИЗ
	ВТ_Остатки КАК ВТ_Остатки
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		ПО ВТ_Остатки.Номенклатура = ВТ_Цены.Номенклатура
```

# 8-5 
# Несоответствие индексов и условий запроса
[Оглавление](#Oglavlenie)
```
Пример

ВЫБРАТЬ
	ЗакупкиОбороты.Номенклатура КАК Номенклатура,
	ЗакупкиОбороты.Контрагент КАК Контрагент,
	ЗакупкиОбороты.КоличествоОборот КАК КоличествоОборот,
	ЗакупкиОбороты.СуммаОборот КАК СуммаОборот
ИЗ
	РегистрНакопления.Закупки.Обороты(, , , Контрагент = &Контрагент) КАК ЗакупкиОбороты

-----------------------------------------------------------------------
-------------------------------------------------------------------

Как исправить

// Способ № 1. Добавить индекс по измерению "Контрагент"


// Способ № 2. Изменить порядок полей в индексе (изменить порядок измерений в регистре)
```

# 8-6
# Использование логического ИЛИ в условиях
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	(Номенклатура.Родитель = &Группа1
			ИЛИ Номенклатура.Родитель = &Группа2)
	И НЕ Номенклатура.ЭтоГруппа
---------------------------------------------------------------------------
----------------------------------------------------------------------

Правильно

ВЫБРАТЬ
	Номенклатура.Ссылка КАК Ссылка,
	Номенклатура.Код КАК Код,
	Номенклатура.Наименование КАК Наименование,
	Номенклатура.Артикул КАК Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Родитель = &Группа1
	И НЕ Номенклатура.ЭтоГруппа

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	Номенклатaура.Ссылка,
	Номенклатура.Код,
	Номенклатура.Наименование,
	Номенклатура.Артикул
ИЗ
	Справочник.Номенклатура КАК Номенклатура
ГДЕ
	Номенклатура.Родитель = &Группа2
	И НЕ Номенклатура.ЭтоГруппа
```

# 8-7
# Применение функций к полям условий
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
	ЗаказКлиента.Валюта КАК Валюта
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	НАЧАЛОПЕРИОДА(ЗаказКлиента.Дата, ДЕНЬ) = &Дата
--------------------------------------------------------------------
--------------------------------------------------------------------
Правильно

ВЫБРАТЬ
	ЗаказКлиента.Ссылка КАК Ссылка,
	ЗаказКлиента.Номер КАК Номер,
	ЗаказКлиента.Дата КАК Дата,
	ЗаказКлиента.Контрагент КАК Контрагент,
	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
	ЗаказКлиента.Валюта КАК Валюта
ИЗ
	Документ.ЗаказКлиента КАК ЗаказКлиента
ГДЕ
	ЗаказКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
```

# 8-8
# Использование условий с НЕ
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	Контрагенты.Ссылка КАК Ссылка,
	Контрагенты.Код КАК Код,
	Контрагенты.Наименование КАК Наименование,
	Контрагенты.ИНН КАК ИНН
ИЗ
	Справочник.Контрагенты КАК Контрагенты
ГДЕ
	НЕ Контрагенты.Ссылка В
				(ВЫБРАТЬ
					ДоговорыКонтрагентов.Владелец КАК Владелец
				ИЗ
					Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				ГДЕ
					НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	И НЕ Контрагенты.ЭтоГруппа
---------------------------------------------------------------------------------
------------------------------------------------------------------------
Правильно

ВЫБРАТЬ
	Контрагенты.Ссылка КАК Ссылка,
	Контрагенты.Код КАК Код,
	Контрагенты.Наименование КАК Наименование,
	Контрагенты.ИНН КАК ИНН
ИЗ
	Справочник.Контрагенты КАК Контрагенты
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		ПО ДоговорыКонтрагентов.Владелец = Контрагенты.Ссылка
ГДЕ
	НЕ Контрагенты.ЭтоГруппа
	И ДоговорыКонтрагентов.Ссылка ЕСТЬ NULL
```

# 8-9
# Получение данных через точку от полей составного типа
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	ВЫБОР
		КОГДА ТоварыНаСкладах.Регистратор ССЫЛКА Документ.РасходнаяНакладная
			ТОГДА ВЫРАЗИТЬ(ТоварыНаСкладах.Регистратор КАК Документ.РасходнаяНакладная).Дата
		КОГДА ТоварыНаСкладах.Регистратор ССЫЛКА Документ.ПриходнаяНакладная
			ТОГДА ВЫРАЗИТЬ(ТоварыНаСкладах.Регистратор КАК Документ.ПриходнаяНакладная).Дата
		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	КОНЕЦ КАК РегистраторДата,
	ТоварыНаСкладах.Регистратор КАК Регистратор,
	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	ТоварыНаСкладах.Склад КАК Склад,
	ТоварыНаСкладах.Количество КАК Количество
ИЗ
	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
ГДЕ
	ТоварыНаСкладах.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
---------------------------------------------------------------------
-----------------------------------------------------------------------
Правильно

ВЫБРАТЬ
	ВЫРАЗИТЬ(ТоварыНаСкладах.Регистратор КАК Документ.РасходнаяНакладная).Дата КАК РегистраторДата,
	ТоварыНаСкладах.Регистратор КАК Регистратор,
	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	ТоварыНаСкладах.Склад КАК Склад,
	ТоварыНаСкладах.Количество КАК Количество
ИЗ
	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
ГДЕ
	ТоварыНаСкладах.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
```

# 8-10
# Условие на реквизиты поля через точку
[Оглавление](#Oglavlenie)
```
Неправильно

ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.Контрагент КАК Контрагент,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот
ИЗ
	РегистрНакопления.Продажи.Обороты(, , , Контрагент.ВидКонтрагента = &ВидКонтрагента) КАК ПродажиОбороты

----------------------------------------------------------------
------------------------------------------------------------------

Правильно

ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.Контрагент КАК Контрагент,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот
ИЗ
	РегистрНакопления.Продажи.Обороты(
			,
			,
			,
			Контрагент В
				(ВЫБРАТЬ
					Контрагенты.Ссылка КАК Ссылка
				ИЗ
					Справочник.Контрагенты КАК Контрагенты
				ГДЕ
					Контрагенты.ВидКонтрагента = &ВидКонтрагента)) КАК ПродажиОбороты
```