# Oglavlenie
1. [Оперативный учет](#1)
    + [Старая методика контроля остатков](#1-2)
    + [Новая методика контроля остатков](#1-3)
1. [Оперативный учет. Расчет себестоимости](#2)
    + [Расчет себестоимости](#2-1)
    + [Методы расчета себестоимости](#2-2)
		+ [Расчет по средней](#2-2-1)
		+ [Расчет по FIFO](#2-2-2)
		+ [Расчет по средней LIFO](#2-2-3)
3. [Оперативный учет. Резервирование товаров](#3)
	+ [Первый шаг делаем документ поступления](#3-1)
    + [Второй шаг делаем документ резервирования](#3-2)
	+ [Третий шаг делаем документ Продажа товаров](#3-3)
	+ [Четвертый шаг делаем документ снятие с резерва](#3-4)
	+ [Какие регистры накопления должны быть накопления](#3-5)
4. [Оперативный учет. Взаиморасчеты.](#4)
    + [Модуль объекта "Расходная накладная"](#4-1)
    + [Модуль объекта "ПриходДенег"](#4-2)









# 1
# Оперативный учет
[Оглавление](#Oglavlenie)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет1.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет2.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет3.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет4.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет5.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет6.png)
![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет7.png)

# 1-2
# Старая методика
[Оглавление](#Oglavlenie)

![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет8.png)

+ Приход 
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.Товары.Записывать = Истина;
	Пока Выборка.Следующий() Цикл

		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;

	КонецЦикла;

КонецПроцедуры

```

+ Расход 
```
Процедура ОбработкаПроведения_СтараяМетодика(Отказ, Режим)
	
	// 1. Очистка старых движений
	Движения.Товары.Очистить();
	Движения.Товары.Записывать = Истина;
	Движения.Записать();
	Движения.Товары.Записывать = Истина;
	
	// 2. Установка управляемой блокировки на записи регистра по номенклатуре из документа
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Товары");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	// 3. Получение запросом данных документа и остатков по регистру	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.Товары.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ТоварыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Количество КАК Количество,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК Остаток,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втТовары.Номенклатура = втОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// 4. Контроль остатков
	Движения.Товары.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > Выборка.Остаток Тогда
			
			Отказ = Истина;
			Дефицит = Выборка.Количество - Выборка.Остаток;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 ед.",
										Выборка.НоменклатураПредставление, Дефицит);
			Сообщение.Сообщить();
			Продолжить;
			
		КонецЕсли;
		
		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;       
		
	КонецЦикла;
	
КонецПроцедуры

```
# 1-3 
# Новая методика
[Оглавление](#Oglavlenie)

![Отчеты](/images/Оперативный%20учет/Оперативынй%20учет9.png)

+ Приход 
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.Товары.Записывать = Истина;
	Пока Выборка.Следующий() Цикл

		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;

	КонецЦикла;

КонецПроцедуры

```

+ Расход 
```
Процедура ОбработкаПроведения_НоваяМетодика(Отказ, РежимПроведения)
	
	// 1. Получение запросом сгруппированных данных документа
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Количество КАК Количество
		|ИЗ
		|	втТовары КАК втТовары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// 2. Формирование движений
	Движения.Товары.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		
	КонецЦикла;
	
	// 3. Запись движений документа
	Движения.Товары.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	// 4. Контроль остатков
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыОстатки.Номенклатура КАК Номенклатура,
		|	-ТоварыОстатки.КоличествоОстаток КАК Дефицит,
		|	ПРЕДСТАВЛЕНИЕ(ТоварыОстатки.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.Товары.Остатки(
		|			&ГраницаДокумент,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ТоварыОстатки
		|ГДЕ
		|	ТоварыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("ГраницаДокумент", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
	
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 ед.",
										Выборка.НоменклатураПредставление, Выборка.Дефицит);
			Сообщение.Сообщить();
	
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры


```

# 2
# Оперативный учет. Расчет себестоимости
[Оглавление](#Oglavlenie)

# 2-1
# Расчет себестоимости
[Оглавление](#Oglavlenie)
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/РАсчет%20себестоимости1.jpg)

# 2-2
# Методы расчета себестоимости
[Оглавление](#Oglavlenie)
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/Методы%20расчета%20себестоимости.jpg)
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/Методы%20расчета%20себестоимости1.jpg)
# 2-2-1
# Расчет по средней
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/Методы%20расчета%20себестоимости2.jpg)
[Оглавление](#Oglavlenie)

+ Поступление товаров
```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	МетодРасчета = ОбщегоНазначения.МетодРасчетаСебестоимости(Дата);
	Если МетодРасчета = Неопределено Тогда
		
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("На дату %1 не заполнена учетная политика. Проведение невозможно",
							Формат(Дата, "ДФ=dd.MM.yyyy"));
		Сообщение.Сообщить();
    	Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
		|	СУММА(ПоступлениеТоваровТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		Движение.Стоимость = Выборка.Сумма;
		
		Если МетодРасчета <> Перечисления.МетодыРасчетаСебестоимости.ПоСредней Тогда
			Движение.Партия = Ссылка;		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
```
+ Продажа товаров
```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	МетодРасчета = ОбщегоНазначения.МетодРасчетаСебестоимости(Дата);
	Если МетодРасчета = Неопределено Тогда
		
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("На дату %1 не заполнена учетная политика. Проведение невозможно",
							Формат(Дата, "ДФ=dd.MM.yyyy"));
		Сообщение.Сообщить();
    	Возврат;
		
	КонецЕсли;
	
	Если МетодРасчета = Перечисления.МетодыРасчетаСебестоимости.ПоСредней Тогда
		ОбработкаПроведения_ПоСредней(Отказ, РежимПроведения);
	ИначеЕсли МетодРасчета = Перечисления.МетодыРасчетаСебестоимости.FIFO Тогда
		ОбработкаПроведения_FIFO(Отказ, РежимПроведения);                                            
	Иначе                                                                      
		ОбработкаПроведения_LIFO(Отказ, РежимПроведения);                                            
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения_ПоСредней(Отказ, РежимПроведения)
	
	// 1. Очистка старых движений
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Записать();
	
	// 2. Установки управляемой блокировки на записи регистра
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	// 3. Получение запросом данных документа и остатков по регистру
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество,
		|	&Склад КАК Склад
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыНаСкладахОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура,
		|					втТовары.Склад КАК Склад
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление,
		|	втТовары.Склад КАК Склад,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Склад) КАК СкладПредставление,
		|	втТовары.Количество КАК Количество,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(втОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втТовары.Номенклатура = втОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// 4. Контроль остатков
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Дефицит = Выборка.Количество - Выборка.КоличествоОстаток;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 на складе %2 в количестве %3 ед.",
									Выборка.НоменклатураПредставление, Выборка.СкладПредставление, Дефицит);
			Сообщение.Сообщить();   
			Продолжить;
			
		КонецЕсли; 
		
		// 5. Формирование движений
		Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Выборка.Склад;
		Движение.Количество = Выборка.Количество;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СтоимостьОстаток;
		Иначе
			Себестоимость = Выборка.Количество / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток;	
		КонецЕсли;
		
		Движение.Стоимость = Себестоимость;
		
	КонецЦикла;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;

КонецПроцедуры
```
# 2-2-2
# Расчет по FIFO
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/Методы%20расчета%20себестоимости3.jpg)
[Оглавление](#Oglavlenie)

```
Процедура ОбработкаПроведения_FIFO(Отказ, РежимПроведения)
	
	// 1. Очистка старых движений
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Записать();
	
	// 2. Установки управляемой блокировки на записи регистра
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	// 3. Получение запросом данных документа и остатков по регистру
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество,
		|	&Склад КАК Склад
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Партия КАК Партия,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыНаСкладахОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура,
		|					втТовары.Склад КАК Склад
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление,
		|	втТовары.Склад КАК Склад,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Склад) КАК СкладПредставление,
		|	втОстатки.Партия КАК Партия,
		|	втТовары.Количество КАК Количество,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(втОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втТовары.Номенклатура = втОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	втОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();  

	// 4. Контроль остатков
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Дефицит = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 ед.",
								ВыборкаНоменклатура.НоменклатураПредставление, Дефицит);
			Сообщение.Сообщить();                                                     
			Продолжить;
		
		КонецЕсли;
		
		// 5. Формирование движений. Расчет себестоимости по FIFO
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаПартии = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаПартии.Следующий() Цикл
			
			Количество = Мин(КоличествоСписать, ВыборкаПартии.КоличествоОстаток);
			
			Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаПартии.Номенклатура;
			Движение.Склад = ВыборкаПартии.Склад;
			Движение.Партия = ВыборкаПартии.Партия;
			Движение.Количество = Количество;
			
			Если Количество = ВыборкаПартии.КоличествоОстаток Тогда
				Себестоимость = ВыборкаПартии.СтоимостьОстаток;
			Иначе
				Себестоимость = Количество / ВыборкаПартии.КоличествоОстаток * ВыборкаПартии.СтоимостьОстаток;				
			КонецЕсли;  
			
			Движение.Стоимость = Себестоимость;
			
			КоличествоСписать = КоличествоСписать - Количество;
			
			Если КоличествоСписать = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	
КонецПроцедуры
```
# 2-2-3
# Расчет по LIFO
![Расчет себестоимости](/images/Оперативный%20учет.%20Расчет%20себестоимости/Методы%20расчета%20себестоимости4.jpg)
[Оглавление](#Oglavlenie)

```
Процедура ОбработкаПроведения_LIFO(Отказ, РежимПроведения)
	
		// 1. Очистка старых движений
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Записать();
	
	// 2. Установки управляемой блокировки на записи регистра
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	// 3. Получение запросом данных документа и остатков по регистру
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество,
		|	&Склад КАК Склад
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Партия КАК Партия,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыНаСкладахОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура,
		|					втТовары.Склад КАК Склад
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление,
		|	втТовары.Склад КАК Склад,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Склад) КАК СкладПредставление,
		|	втОстатки.Партия КАК Партия,
		|	втТовары.Количество КАК Количество,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(втОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втТовары.Номенклатура = втОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	втОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();  

	// 4. Контроль остатков
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Дефицит = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 ед.",
								ВыборкаНоменклатура.НоменклатураПредставление, Дефицит);
			Сообщение.Сообщить();                                                     
			Продолжить;
		
		КонецЕсли;
		
		// 5. Формирование движений. Расчет себестоимости по FIFO
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаПартии = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаПартии.Следующий() Цикл
			
			Количество = Мин(КоличествоСписать, ВыборкаПартии.КоличествоОстаток);
			
			Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаПартии.Номенклатура;
			Движение.Склад = ВыборкаПартии.Склад;
			Движение.Партия = ВыборкаПартии.Партия;
			Движение.Количество = Количество;
			
			Если Количество = ВыборкаПартии.КоличествоОстаток Тогда
				Себестоимость = ВыборкаПартии.СтоимостьОстаток;
			Иначе
				Себестоимость = Количество / ВыборкаПартии.КоличествоОстаток * ВыборкаПартии.СтоимостьОстаток;				
			КонецЕсли;  
			
			Движение.Стоимость = Себестоимость;
			
			КоличествоСписать = КоличествоСписать - Количество;
			
			Если КоличествоСписать = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;

КонецПроцедуры
```

# 3
# Оперативный учет. Резервирование товаров
[Оглавление](#Oglavlenie)

![Оперативный учет. Резервирование товаров](/images/Резервирование%20товаров/РезервированиеТоваров1.png)
![Оперативный учет. Резервирование товаров](/images/Резервирование%20товаров/РезервированиеТоваров2.png)
![Оперативный учет. Резервирование товаров](/images/Резервирование%20товаров/РезервированиеТоваров3.png)
![Оперативный учет. Резервирование товаров](/images/Резервирование%20товаров/РезервированиеТоваров4.png)

# 3-1
# Первый шаг делаем документ поступления
[Оглавление](#Oglavlenie)
```

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		
	КонецЦикла;
	
КонецПроцедуры
```

# 3-2
# Второй шаг делаем документ резервирования
[Оглавление](#Oglavlenie)

```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;                     
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезерированиеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РезерированиеТоваровТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.РезерированиеТоваров.Товары КАК РезерированиеТоваровТовары
		|ГДЕ
		|	РезерированиеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РезерированиеТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Количество КАК Количество
		|ИЗ
		|	втТовары КАК втТовары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.ТоварыВРезерве.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыВРезерве.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Контрагент = Контрагент;
		Движение.ДокументРезерва = Ссылка;
		Движение.Количество = Выборка.Количество;
	КонецЦикла;   
	
	Движения.ТоварыВРезерве.БлокироватьДляИзменения = Истина;
	Движения.Записать();    
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(ОстаткиИРезервы.КоличествоРезерв) КАК КоличествоРезерв,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток - ОстаткиИРезервы.КоличествоРезерв) КАК КоличествоСвободныйОстаток,
	|	ПРЕДСТАВЛЕНИЕ(ОстаткиИРезервы.Номенклатура) КАК НоменклатураПредставление
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&Граница,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура
	|					ИЗ
	|						втТовары)) КАК ТоварыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыВРезервеОстатки.Номенклатура,
	|		0,
	|		ТоварыВРезервеОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыВРезерве.Остатки(
	|				&Граница,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура
	|					ИЗ
	|						втТовары)) КАК ТоварыВРезервеОстатки) КАК ОстаткиИРезервы
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) - СУММА(ОстаткиИРезервы.КоличествоРезерв) < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно свободного остатка товара %1 в количестве %2 ед.",
									Выборка.НоменклатураПредставление, -Выборка.КоличествоСвободныйОстаток);
			Сообщение.Сообщить();
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

```


# 3-3
# Третий шаг делаем документ Продажа товаров
[Оглавление](#Oglavlenie)

```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// 1. Очистка старых движений
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ТоварыВРезерве.Записывать = Истина;
	Движения.Записать();
	
	// 2. Блокировка остатков в регистрах "ТоварыНаСкладах" и "ТоварыВРезерве"
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыВРезерве");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	// 3. Получение данных для контроля остатков и формирования движений
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
		|	СУММА(ОстаткиИРезервы.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ОстаткиИРезервы.КоличествоРезерв) КАК КоличествоРезерв,
		|	СУММА(ОстаткиИРезервы.КоличествоОстаток - ОстаткиИРезервы.КоличествоРезерв) КАК КоличествоСвободныйОстаток,
		|	ПРЕДСТАВЛЕНИЕ(ОстаткиИРезервы.Номенклатура) КАК НоменклатураПредставление
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|		ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|		0 КАК КоличествоРезерв
		|	ИЗ
		|		РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТовары.Номенклатура
		|					ИЗ
		|						втТовары)) КАК ТоварыНаСкладахОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыВРезервеОстатки.Номенклатура,
		|		0,
		|		ТоварыВРезервеОстатки.КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.ТоварыВРезерве.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТовары.Номенклатура
		|					ИЗ
		|						втТовары)) КАК ТоварыВРезервеОстатки) КАК ОстаткиИРезервы
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиИРезервы.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВРезервеОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВРезервеОстатки.ДокументРезерва КАК ДокументРезерва,
		|	ТоварыВРезервеОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ втРезервы
		|ИЗ
		|	РегистрНакопления.ТоварыВРезерве.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						втТовары.Номенклатура
		|					ИЗ
		|						втТовары)) КАК ТоварыВРезервеОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Количество КАК Количество,
		|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(втОстатки.КоличествоРезерв, 0) КАК КоличествоРезервВсего,
		|	ЕСТЬNULL(втРезервы.ДокументРезерва, ЗНАЧЕНИЕ(Документ.РезерированиеТоваров.ПустаяСсылка)) КАК ДокументРезерва,
		|	ЕСТЬNULL(втРезервы.КоличествоОстаток, 0) КАК КоличествоРезервКонтрагента,
		|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втТовары.Номенклатура = втОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ втРезервы КАК втРезервы
		|		ПО втТовары.Номенклатура = втРезервы.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	втРезервы.ДокументРезерва.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(КоличествоОстаток),
		|	МАКСИМУМ(КоличествоРезервВсего),
		|	СУММА(КоличествоРезервКонтрагента)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		// 4. Контроль остатков
		ДоступныйОстаток = ВыборкаНоменклатура.КоличествоОстаток 
							- ВыборкаНоменклатура.КоличествоРезервВсего 
							+ ВыборкаНоменклатура.КоличествоРезервКонтрагента;
							
		Если ВыборкаНоменклатура.Количество > ДоступныйОстаток Тогда
			
			Дефицит = ВыборкаНоменклатура.Количество - ДоступныйОстаток;
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно свободного остатка товара %1 в количестве %2 ед.",
								ВыборкаНоменклатура.НоменклатураПредставление, Дефицит);
			Сообщение.Сообщить();
			Продолжить;
			
		КонецЕсли;	
		
		// списание товаров из остатка
		Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		
		// списание резервов по документам резерва
		КоличествоРезервСписать = Мин(ВыборкаНоменклатура.Количество, ВыборкаНоменклатура.КоличествоРезервКонтрагента);
		
		ВыборкаРезервы = ВыборкаНоменклатура.Выбрать();
	
		Пока ВыборкаРезервы.Следующий() И КоличествоРезервСписать > 0 Цикл
			
			Количество = Мин(КоличествоРезервСписать, ВыборкаРезервы.КоличествоРезервКонтрагента);
			
			Движение = Движения.ТоварыВРезерве.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.ДокументРезерва = ВыборкаРезервы.ДокументРезерва;
			Движение.Номенклатура = ВыборкаРезервы.Номенклатура;
			Движение.Количество = Количество;
			
			КоличествоРезервСписать = КоличествоРезервСписать - Количество;
			
		КонецЦикла;

	КонецЦикла;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ТоварыВРезерве.Записывать = Истина;
	
КонецПроцедуры

```


# 3-4
# Четвертый шаг делаем документ снятие с резерва
[Оглавление](#Oglavlenie)

```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// 1. Очистка старых движений документа
	Движения.ТоварыВРезерве.Записывать = Истина;
	Движения.Записать();
	
	//2. Блокировка остатков по резервам
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыВРезерве");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Если ЗначениеЗаполнено(ДокументРезерва) Тогда
		ЭлементБлокировки.УстановитьЗначение("ДокументРезерва", ДокументРезерва);
	КонецЕсли;
	
	Блокировка.Заблокировать();
	
	// 3. Получить остатки резервов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыВРезервеОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВРезервеОстатки.ДокументРезерва КАК ДокументРезерва,
		|	ТоварыВРезервеОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ТоварыВРезерве.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И ДокументРезерва = &ДокументРезерва) КАК ТоварыВРезервеОстатки";
	
	Если НЕ ЗначениеЗаполнено(ДокументРезерва) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументРезерва = &ДокументРезерва", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ДокументРезерва", ДокументРезерва);
	КонецЕсли;

	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыВРезерве.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.ДокументРезерва = Выборка.ДокументРезерва;
		Движение.Количество = Выборка.КоличествоОстаток;
	КонецЦикла;      
	
	Движения.ТоварыВРезерве.Записывать = Истина;
	
КонецПроцедуры

```

# 3-5
# Какие регистры накопления должны быть накопления
[Оглавление](#Oglavlenie)

![Оперативный учет. Резервирование товаров](/images/Резервирование%20товаров/2024-08-03_20-12-34.png)


# 4 
# Оперативный учет. Взаиморасчеты.
[Оглавление](#Oglavlenie)
![Оперативный учет. Взаиморасчеты](/images/Взаиморасчеты/ОперативыныйУчет.Взаиморасчеты.png)

![Оперативный учет. Взаиморасчеты](/images/Взаиморасчеты/Задача4.1.png)
![Оперативный учет. Взаиморасчеты](/images/Взаиморасчеты/Задача4.2.png)
![Оперативный учет. Взаиморасчеты](/images/Взаиморасчеты/Задача4.3.png)

# 4-1
# Модуль объекта "Расходная накладная"
[Оглавление](#Oglavlenie)
```
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаДокумента = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// 1. Очистка старых движений
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	// 2. Увеличение долга контрагента по накладной
	Движения.Взаиморасчеты.Записывать = Истина;
	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Накладная = Ссылка;
	Движение.Валюта = Валюта;
	Движение.Сумма = СуммаДокумента;
	
	// 3. Блокировка остатков авансов
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Накладная", Документы.РасходнаяНакладная.ПустаяСсылка());
	Блокировка.Заблокировать();
	
	// 4. Получение остатков авансов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаАвансаОстаток
		|ПОМЕСТИТЬ втОстаткиАвансов
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОстаткиАвансов.СуммаАвансаОстаток КАК СуммаАвансаОстатокВРублях,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	ВЫРАЗИТЬ(втОстаткиАвансов.СуммаАвансаОстаток / ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК ЧИСЛО(15, 2)) КАК СуммаАвансаОстатокВВалюте
		|ИЗ
		|	втОстаткиАвансов КАК втОстаткиАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
		|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// 5. Формирование движений по зачету авансов
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.СуммаАвансаОстатокВВалюте > 0 Тогда
		
		СуммаДокументаВВалюте = СуммаДокумента;
		СуммаДокументаВРублях = СуммаДокумента * Выборка.Курс;
		
		Если Выборка.СуммаАвансаОстатокВРублях >= СуммаДокументаВРублях Тогда
			СуммаЗачетаАвансаВРублях = СуммаДокументаВРублях;
			СуммаЗачетаАвансаВВалюте = СуммаДокументаВВалюте;
		Иначе
			СуммаЗачетаАвансаВРублях = Выборка.СуммаАвансаОстатокВРублях;
			СуммаЗачетаАвансаВВалюте = Выборка.СуммаАвансаОстатокВВалюте;
		КонецЕсли;

		// 5.1. Уменьшение аванса
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.Рубль;
		Движение.Сумма = СуммаЗачетаАвансаВРублях;       
		
		// 5.2. Зачет аванса по текущей накладной
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Ссылка;
		Движение.Валюта = Валюта;
		Движение.Сумма = СуммаЗачетаАвансаВВалюте;       
		
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти
```
# 4-2
#  Модуль объекта "ПриходДенег"
[Оглавление](#Oglavlenie)
```
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// 1. Очистика старых движений
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	// 2. Блокировка остатков долгов по накладным
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();
	
	// 3. Получение остатков долгов в разрезе накладных
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Накладная КАК Накладная,
		|	ВзаиморасчетыОстатки.Валюта КАК Валюта,
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ втОстаткиДолгов
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная <> ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
		|	КурсыВалютСрезПоследних.Курс КАК Курс
		|ПОМЕСТИТЬ втКурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			&Дата,
		|			Валюта В
		|				(ВЫБРАТЬ
		|					втОстаткиДолгов.Валюта
		|				ИЗ
		|					втОстаткиДолгов)) КАК КурсыВалютСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОстаткиДолгов.Накладная КАК Накладная,
		|	втОстаткиДолгов.Валюта КАК Валюта,
		|	втОстаткиДолгов.СуммаОстаток КАК СуммаДолгаВВалюте,
		|	ЕСТЬNULL(втКурсыВалют.Курс, 1) КАК Курс,
		|	втОстаткиДолгов.СуммаОстаток * ЕСТЬNULL(втКурсыВалют.Курс, 1) КАК СуммаДолгаВРублях
		|ИЗ
		|	втОстаткиДолгов КАК втОстаткиДолгов
		|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК втКурсыВалют
		|		ПО втОстаткиДолгов.Валюта = втКурсыВалют.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	втОстаткиДолгов.Накладная.МоментВремени";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
	РезультатЗапроса = Запрос.Выполнить();
	
	// 4. Формирование движений
	СуммаКРаспределению = СуммаДокумента;

	Выборка = РезультатЗапроса.Выбрать();
	
	// 4.1. Уменьшение суммы долга по накладным
	Пока Выборка.Следующий() И СуммаКРаспределению > 0 Цикл
		
		СуммаКЗачетуВРублях = Мин(СуммаКРаспределению, Выборка.СуммаДолгаВРублях);
		
		Если СуммаКЗачетуВРублях = Выборка.СуммаДолгаВРублях Тогда
			СуммаКЗачетуВВалюте = Выборка.СуммаДолгаВВалюте;
		Иначе
        	СуммаКЗачетуВВалюте = Окр(СуммаКЗачетуВРублях / Выборка.Курс, 2); 	
		КонецЕсли;   
		
		Если СуммаКЗачетуВВалюте > 0 Тогда
		
			Движение = Движения.Взаиморасчеты.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Накладная = Выборка.Накладная;
			Движение.Валюта = Выборка.Валюта;
			Движение.Сумма = СуммаКЗачетуВВалюте;
			
			СуммаКРаспределению = СуммаКРаспределению - СуммаКЗачетуВРублях;
		
		КонецЕсли;
		
	КонецЦикла;
	
	// 4. Нераспределенная сумма - это аванс
	Если СуммаКРаспределению > 0 Тогда
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.Рубль;
		Движение.Сумма = СуммаКРаспределению;
	КонецЕсли;
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
КонецПроцедуры

```